/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus, Rajesh Gandham

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

//r = D^{-1}b
//d = lambda*r
//x = d
@kernel void SmoothChebyshevStart(const dlong   Nrows,
                      const dfloat  lambda,
                      @restrict const  dfloat * diagInv,
                      @restrict const  dfloat * b,
                      @restrict        dfloat * r,
                      @restrict        dfloat * d,
                      @restrict        dfloat * x){

  for(dlong n=0;n<Nrows;++n;@tile(p_BLOCKSIZE,@outer,@inner)){

    const dfloat r_r = diagInv[n]*b[n];
    r[n] = r_r;
    d[n] = lambda*r_r;
    x[n] = lambda*r_r;
  }
}

//Computes either:
//
//r = D^{-1}(b-A*x)
//
// on the first Chebyshev iteration, or
//
//r = r - D^{-1}A*d
//
//on later iterations.
//The general form is:
//r = alpha*r + D^{-1}(beta*b-A*x)
//
@kernel void SmoothChebyshevCSR(const dlong   Nblocks,
                      @restrict const  dlong  * blockStarts,
                      @restrict const  dlong  * rowStarts,
                      @restrict const  dlong  * cols,
                      @restrict const  pfloat * vals,
                      const dfloat  alpha,
                      const dfloat  beta,
                      @restrict const  dfloat * diagInv,
                      @restrict const  dfloat * B,
                      @restrict const  dfloat * x,
                      @restrict        dfloat * r){

  for(dlong b=0;b<Nblocks;++b;@outer){
    @exclusive dlong blockStart, blockEnd, start;
    @shared dfloat temp[p_NonzerosPerBlock];

    for(dlong n=0;n<p_BLOCKSIZE;++n;@inner){
      blockStart = blockStarts[b];
      blockEnd   = blockStarts[b+1];
      start = rowStarts[blockStart];

      for (dlong id=start+n;id<rowStarts[blockEnd];id+=p_BLOCKSIZE) {
        temp[id-start] = vals[id]*x[cols[id]];
      }
    }

    for(dlong n=0;n<p_BLOCKSIZE;++n;@inner){
      for (dlong row=blockStart+n;row<blockEnd;row+=p_BLOCKSIZE) {

        const dlong colStart = rowStarts[row]  -start;
        const dlong colEnd   = rowStarts[row+1]-start;

        dfloat result = (beta!=0.0) ? beta*B[row] : 0.0;
        for (int i=colStart;i<colEnd;i++) {
          result -= temp[i];
        }

        const dfloat r_k = (alpha!=0.0) ? alpha*r[row] : 0.0;
        r[row] = r_k + diagInv[row]*result;
      }
    }
  }
}

@kernel void SmoothChebyshevMCSR(const dlong   Nblocks,
                      @restrict const  dlong  * blockStarts,
                      @restrict const  dlong  * rowStarts,
                      @restrict const  dlong  * rows,
                      @restrict const  dlong  * cols,
                      @restrict const  pfloat * vals,
                      @restrict const  dfloat * diagInv,
                      @restrict const  dfloat * x,
                      @restrict        dfloat * r){

  for(dlong b=0;b<Nblocks;++b;@outer){
    @exclusive dlong blockStart, blockEnd, start;
    @shared dfloat temp[p_NonzerosPerBlock];

    for(dlong n=0;n<p_BLOCKSIZE;++n;@inner){
      blockStart = blockStarts[b];
      blockEnd   = blockStarts[b+1];
      start = rowStarts[blockStart];

      for (dlong id=start+n;id<rowStarts[blockEnd];id+=p_BLOCKSIZE) {
        temp[id-start] = vals[id]*x[cols[id]];
      }
    }

    for(dlong n=0;n<p_BLOCKSIZE;++n;@inner){
      for (dlong row=blockStart+n;row<blockEnd;row+=p_BLOCKSIZE) {

        const dlong m = rows[row];
        const dlong colStart = rowStarts[row]  -start;
        const dlong colEnd   = rowStarts[row+1]-start;

        dfloat result = 0.0;
        for (int i=colStart;i<colEnd;i++) {
          result -= temp[i];
        }

        r[m] += diagInv[m]*result;
      }
    }
  }
}

//Computes
// d_k+1 = alpha*d_k + beta*r_k+1
// x_k+1 = x_k + d_k+1
@kernel void SmoothChebyshevUpdate(const dlong   Nrows,
                      const dfloat  alpha,
                      const dfloat  beta,
                      const int last_it,
                      @restrict const  dfloat * r,
                      @restrict        dfloat * d,
                      @restrict        dfloat * x){

  for(dlong n=0;n<Nrows;++n;@tile(p_BLOCKSIZE,@outer,@inner)){

    const dfloat d_k = (alpha!=0.0) ? d[n] : 0.0;
    const dfloat d_kp1 = alpha*d_k + beta*r[n];

    //skip writing d on last Chebyshev iteration
    if (!last_it)
      d[n] = d_kp1;

    x[n] += d_kp1;
  }
}
