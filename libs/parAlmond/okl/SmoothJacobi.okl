/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus, Rajesh Gandham

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

@kernel void SmoothJacobiCSR(const dlong   Nblocks,
                      @restrict const  dlong  * blockStarts,
                      @restrict const  dlong  * rowStarts,
                      @restrict const  dlong  * cols,
                      @restrict const  dfloat * vals,
                      const dfloat  lambda,
                      @restrict const  dfloat * diagInv,
                      @restrict const  dfloat * r,
                      @restrict const  dfloat * x,
                      @restrict        dfloat * d){

  // d = lambda*inv(D)*(r-A*x)
  for(dlong b=0;b<Nblocks;++b;@outer){
    @exclusive dlong blockStart, blockEnd, start;
    @shared dfloat temp[p_NonzerosPerBlock];

    for(dlong n=0;n<p_BLOCKSIZE;++n;@inner){
      blockStart = blockStarts[b];
      blockEnd   = blockStarts[b+1];
      start = rowStarts[blockStart];

      for (dlong id=start+n;id<rowStarts[blockEnd];id+=p_BLOCKSIZE) {
        temp[id-start] = vals[id]*x[cols[id]];
      }
    }

    for(dlong n=0;n<p_BLOCKSIZE;++n;@inner){
      for (dlong row=blockStart+n;row<blockEnd;row+=p_BLOCKSIZE) {

        const dlong colStart = rowStarts[row]  -start;
        const dlong colEnd   = rowStarts[row+1]-start;

        dfloat result = r[row];
        for (int i=colStart;i<colEnd;i++) {
          result -= temp[i];
        }

        d[row] = lambda*diagInv[row]*result;
      }
    }
  }
}

@kernel void SmoothJacobiMCSR(const dlong   Nblocks,
                      @restrict const  dlong  * blockStarts,
                      @restrict const  dlong  * rowStarts,
                      @restrict const  dlong  * rows,
                      @restrict const  dlong  * cols,
                      @restrict const  dfloat * vals,
                      const dfloat  lambda,
                      @restrict const  dfloat * diagInv,
                      @restrict const  dfloat * x,
                      @restrict        dfloat * d){

  // d = lambda*inv(D)*(-A*x)
  for(dlong b=0;b<Nblocks;++b;@outer){
    @exclusive dlong blockStart, blockEnd, start;
    @shared dfloat temp[p_NonzerosPerBlock];

    for(dlong n=0;n<p_BLOCKSIZE;++n;@inner){
      blockStart = blockStarts[b];
      blockEnd   = blockStarts[b+1];
      start = rowStarts[blockStart];

      for (dlong id=start+n;id<rowStarts[blockEnd];id+=p_BLOCKSIZE) {
        temp[id-start] = vals[id]*x[cols[id]];
      }
    }

    for(dlong n=0;n<p_BLOCKSIZE;++n;@inner){
      for (dlong row=blockStart+n;row<blockEnd;row+=p_BLOCKSIZE) {

        const dlong m = rows[row];
        const dlong colStart = rowStarts[row]  -start;
        const dlong colEnd   = rowStarts[row+1]-start;

        dfloat result = 0.0;
        for (int i=colStart;i<colEnd;i++) {
          result -= temp[i];
        }

        d[m] += lambda*diagInv[m]*result;
      }
    }
  }
}