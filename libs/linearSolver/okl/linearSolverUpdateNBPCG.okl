/*

  The MIT License (MIT)

  Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.

*/

// WARNING: p_blockSize must be a power of 2

@kernel void update1NBPCG(const dlong N,
                          const dlong Nblocks,
                          const int weighted,
                          @restrict const dfloat *weight,
                          @restrict const dfloat *z,
                          @restrict const dfloat *Z,
                          const dfloat beta,
                          @restrict dfloat *p,
                          @restrict dfloat *s,
                          @restrict dfloat *pdots){

  for(dlong b=0;b<Nblocks;++b;@outer(0)){

    @shared volatile dfloat s_dot[p_blockSize];

    for(int t=0;t<p_blockSize;++t;@inner(0)){

      dfloat sum = 0;
      for(int n=t+b*p_blockSize;n<N;n+=Nblocks*p_blockSize){
        dfloat pn = p[n];
        dfloat sn = s[n];

        const dfloat zn = z[n];
        const dfloat Zn = Z[n];

        pn = zn + beta*pn;
        sn = Zn + beta*sn;

        const dfloat wn = (weighted) ? weight[n] : 1.0;

        sum += wn*pn*sn;

        p[n] = pn;
        s[n] = sn;
      }
      s_dot[t] = sum;
    }

    @barrier("local");

#if p_blockSize>512
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<512) s_dot[t] += s_dot[t+512];
    @barrier("local");
#endif

#if p_blockSize>256
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<256) s_dot[t] += s_dot[t+256];
    @barrier("local");
#endif

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<128) s_dot[t] += s_dot[t+128];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 64) s_dot[t] += s_dot[t+ 64];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 32) s_dot[t] += s_dot[t+ 32];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 16) s_dot[t] += s_dot[t+ 16];
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  8) s_dot[t] += s_dot[t+  8];
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  4) s_dot[t] += s_dot[t+  4];
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  2) s_dot[t] += s_dot[t+  2];
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  1) pdots[b] = s_dot[0] + s_dot[1];
  }
}

@kernel void update2NBPCG(const dlong N,
                          const dlong Nblocks,
                          const int weighted,
                          @restrict const dfloat *weight,
                          @restrict const dfloat *s,
                          @restrict const dfloat *S,
                          const dfloat alpha,
                          @restrict dfloat *r,
                          @restrict dfloat *z,
                          @restrict dfloat *dots){

  for(dlong b=0;b<Nblocks;++b;@outer(0)){

    @shared volatile dfloat s_dot[3][p_blockSize];

    for(int t=0;t<p_blockSize;++t;@inner(0)){

      dfloat sumrdotz = 0;
      dfloat sumzdotz = 0;
      dfloat sumrdotr = 0;
      for(int n=t+b*p_blockSize;n<N;n+=Nblocks*p_blockSize){
        dfloat rn = r[n];
        dfloat zn = z[n];

        const dfloat sn = s[n];
        const dfloat Sn = S[n];

        rn = rn - alpha*sn;
        zn = zn - alpha*Sn;

        const dfloat wn = (weighted) ? weight[n] : 1.0;

        sumrdotz += wn*rn*zn;
        sumzdotz += wn*zn*zn;
        sumrdotr += wn*rn*rn;

        r[n] = rn;
        z[n] = zn;
      }

      s_dot[0][t] = sumrdotz;
      s_dot[1][t] = sumzdotz;
      s_dot[2][t] = sumrdotr;
    }

    @barrier("local");

#if p_blockSize>512
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<512) {
      s_dot[0][t] += s_dot[0][t+512];
      s_dot[1][t] += s_dot[1][t+512];
      s_dot[2][t] += s_dot[2][t+512];
    }
    @barrier("local");
#endif

#if p_blockSize>256
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<256) {
      s_dot[0][t] += s_dot[0][t+256];
      s_dot[1][t] += s_dot[1][t+256];
      s_dot[2][t] += s_dot[2][t+256];
    }
    @barrier("local");
#endif

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<128) {
      s_dot[0][t] += s_dot[0][t+128];
      s_dot[1][t] += s_dot[1][t+128];
      s_dot[2][t] += s_dot[2][t+128];
    }
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 64) {
      s_dot[0][t] += s_dot[0][t+ 64];
      s_dot[1][t] += s_dot[1][t+ 64];
      s_dot[2][t] += s_dot[2][t+ 64];
    }
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 32) {
      s_dot[0][t] += s_dot[0][t+ 32];
      s_dot[1][t] += s_dot[1][t+ 32];
      s_dot[2][t] += s_dot[2][t+ 32];
    }
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 16) {
      s_dot[0][t] += s_dot[0][t+ 16];
      s_dot[1][t] += s_dot[1][t+ 16];
      s_dot[2][t] += s_dot[2][t+ 16];
    }
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  8) {
      s_dot[0][t] += s_dot[0][t+  8];
      s_dot[1][t] += s_dot[1][t+  8];
      s_dot[2][t] += s_dot[2][t+  8];
    }
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  4) {
      s_dot[0][t] += s_dot[0][t+  4];
      s_dot[1][t] += s_dot[1][t+  4];
      s_dot[2][t] += s_dot[2][t+  4];
    }
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  2) {
      s_dot[0][t] += s_dot[0][t+  2];
      s_dot[1][t] += s_dot[1][t+  2];
      s_dot[2][t] += s_dot[2][t+  2];
    }
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  1) {
      dots[0+3*b] = s_dot[0][0] + s_dot[0][1];
      dots[1+3*b] = s_dot[1][0] + s_dot[1][1];
      dots[2+3*b] = s_dot[2][0] + s_dot[2][1];
    }
  }
}
