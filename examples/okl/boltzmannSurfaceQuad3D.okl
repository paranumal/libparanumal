
void flux(const dfloat nx, const dfloat ny, const dfloat nz,
	  const dfloat alpha, const dfloat beta,
	  @restrict const  dfloat *  dq,
	  @restrict dfloat *  fluxq){

  fluxq[0] = alpha*dq[0] - beta*p_sqrtRT*(nx*dq[1] + ny*dq[2] + nz*dq[3]);

  fluxq[1] = alpha*dq[1] - beta*p_sqrtRT*(nx*dq[0] + nx*p_sqrt2*dq[4] + ny*dq[7] + nz*dq[8]);
  fluxq[2] = alpha*dq[2] - beta*p_sqrtRT*(ny*dq[0] + ny*p_sqrt2*dq[5] + nx*dq[7] + nz*dq[9]);
  fluxq[3] = alpha*dq[3] - beta*p_sqrtRT*(nz*dq[0] + nz*p_sqrt2*dq[6] + nx*dq[8] + ny*dq[9]);

  fluxq[4] = alpha*dq[4] - beta*p_sqrtRT*(p_sqrt2*nx*dq[1]);
  fluxq[5] = alpha*dq[5] - beta*p_sqrtRT*(p_sqrt2*ny*dq[2]);
  fluxq[6] = alpha*dq[6] - beta*p_sqrtRT*(p_sqrt2*nz*dq[3]);

  fluxq[7] = alpha*dq[7] - beta*p_sqrtRT*(nx*dq[2]+ny*dq[1]);
  fluxq[8] = alpha*dq[8] - beta*p_sqrtRT*(nx*dq[3]+nz*dq[1]);
  fluxq[9] = alpha*dq[9] - beta*p_sqrtRT*(ny*dq[3]+nz*dq[2]);
}



// pointwise SEM-DG lift
@kernel void boltzmannSurfaceQuad3D(const int Nelements,
				   @restrict const  dfloat *  sgeo,
				   @restrict const  dfloat *  LIFTT,
				   @restrict const  int   *  vmapM,
				   @restrict const  int   *  vmapP,
				   const dfloat time,
				   @restrict const  dfloat *  x,
				   @restrict const  dfloat *  y,
				   @restrict const  dfloat *  z,
				   @restrict const  dfloat *  q,
				   @restrict dfloat *  rhsq){
  
  
#define p_LIFT (0.5f*(p_Nq-1)*p_Nq)
  
  // for all elements
  for(int eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){
    
    // for all faces (global barrier at end of body of this loop)
    for(int face=0;face<p_Nfaces;++face){      

      
      for(int es=0;es<p_NblockS;++es;@inner(1)){
	for(int i=0;i<p_Nq;++i;@inner(0)){ 

	  const int eM = eo + es;
	  
	  if(eM<Nelements){
	    
	    // load surface geofactors for this face
	    const int sid = eM*p_Nsgeo*p_Nq*p_Nfaces + face*p_Nsgeo*p_Nq + i; 
	    const dfloat nx = sgeo[sid+0*p_Nq];
	    const dfloat ny = sgeo[sid+1*p_Nq];
	    const dfloat nz = sgeo[sid+2*p_Nq];
	    const dfloat sJ = sgeo[sid+3*p_Nq];
	    const dfloat invJ = sgeo[sid+4*p_Nq];

	    // indices of negative and positive traces of face node
	    const int id  = eM*p_Nq*p_Nfaces + face*p_Nq + i;
	    const int idM = vmapM[id];
	    const int idP = vmapP[id];
	    const int eP = idP/p_Np;

	    const int vidM = eM*p_Np*p_Nfields + (idM%p_Np);
	    const int vidP = eP*p_Np*p_Nfields + (idP%p_Np);

	    dfloat dq[p_Nfields], fluxq[p_Nfields];
	    
	    #pragma unroll p_Nfields
	      for(int fld=0;fld<p_Nfields;++fld){
		const dfloat qM = q[vidM+fld*p_Np];
		const dfloat qP = q[vidP+fld*p_Np]; // assumes chunks of p_Np*p_Nfields per element
		dq[fld] = qP-qM;
	      }

	    // D = nx*A + ny*B + nz*C
   	    // compute 0.5*[q] + 0.5*D*[q]/sqrt(RT)
	    const dfloat alpha = 0.5, beta = 0.5*p_invsqrtRT;

	    flux(nx, ny, nz, alpha, beta, dq, fluxq);

	    const dfloat one = 1.0, zero = 0.0;

	    // compute D*(0.5*[q] + 0.5*D*[q])
	    flux(nx, ny, nz, zero, one, fluxq, dq);
	    
	    // evaluate "flux" terms: (sJ/J)*(D+sigma*D*D)*(q^* - q^-)
	    const dfloat sc = invJ*sJ*p_LIFT;

	    #pragma unroll p_Nfields
	      for(int fld=0;fld<p_Nfields;++fld){
		rhsq[vidM+fld*p_Np] += sc*dq[fld]; // TW dq[fld];
	      }
	  }
	}
      }

      // wait for all global memory writes of the previous inner loop to complete
      @barrier("global");

      
    }
  }
}

