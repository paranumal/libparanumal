@kernel void ellipticRhsBCTri2D(const int Nelements,
                              @restrict const  dfloat *  ggeo,
                              @restrict const  dfloat *  sgeo,
                              @restrict const  dfloat *  SrrT,
                              @restrict const  dfloat *  SrsT,
                              @restrict const  dfloat *  SsrT,
                              @restrict const  dfloat *  SssT,
                              @restrict const  dfloat *  MM,
                              @restrict const  int   *  vmapM,
                              @restrict const  dfloat *  sMT,
                              const dfloat lambda,
                              const dfloat t,
                              @restrict const  dfloat *  x,
                              @restrict const  dfloat *  y,
                              @restrict const  int    *  mapB,
                              @restrict dfloat  *  rhs){
  
  for(int e=0;e<Nelements;e++;@outer(0)){
    @shared dfloat s_q[p_Np];
    @shared dfloat s_ndq[p_Nfp*p_Nfaces];

    for(int n=0;n<p_Nmax;++n;@inner(0)){
      if(n<p_Np){
        s_q[n] = 0.;
      }
      if(n<p_NfacesNfp){
        s_ndq[n] = 0.;
      }
    }

    @barrier("local");

    for(int n=0;n<p_Nmax;++n;@inner(0)){
      if(n<p_NfacesNfp){
        const int id  = n + e*p_Nfaces*p_Nfp;
        const int idM = vmapM[id];          
        const int nid = idM%p_Np; 

        const int face = n/p_Nfp;

        dfloat dudxP=0, dudyP=0, uP=0;
        
        // load surface geofactors for this face
        const int sid = p_Nsgeo*(e*p_Nfaces+face);
        const dfloat nx = sgeo[sid+p_NXID];
        const dfloat ny = sgeo[sid+p_NYID];
        const dfloat sJ = sgeo[sid+p_SJID];

        const int bc = mapB[idM];
        if(bc>0) {
          ellipticBoundaryConditions2D(bc, t, x[idM], y[idM], nx, ny, \
                                        0.f, 0.f, 0.f,                \
                                        uP, dudxP, dudyP);
        }  

        s_q[nid] = uP;
        s_ndq[n] = sJ*(nx*dudxP + ny*dudyP);
      }
    }

    @barrier("local");
    
    for(int n=0;n<p_Nmax;++n;@inner(0)){ 
      if(n<p_Np){
        //volume Dirichlet data
        const int id = n + e*p_Np;
        const int gid = e*p_Nggeo;
        const dfloat Grr = ggeo[gid + p_G00ID];
        const dfloat Grs = ggeo[gid + p_G01ID];
        const dfloat Gss = ggeo[gid + p_G11ID];
        const dfloat J   = ggeo[gid + p_GWJID];

        dfloat qrr = 0.;
        dfloat qrs = 0.;
        dfloat qsr = 0.;
        dfloat qss = 0.;
        dfloat qM = 0.;

        #pragma unroll p_Np
          for (int k=0;k<p_Np;k++) {
            qrr += SrrT[n+k*p_Np]*s_q[k];
            qrs += SrsT[n+k*p_Np]*s_q[k];
            qsr += SsrT[n+k*p_Np]*s_q[k];
            qss += SssT[n+k*p_Np]*s_q[k];
            qM  += MM[n+k*p_Np]*s_q[k];
          }

        dfloat Lndq = 0;            
        // surface mass * surface terms
        #pragma unroll p_NfacesNfp
          for(int i=0;i<p_NfacesNfp;++i){
            Lndq += sMT[n+i*p_Np]*s_ndq[i];
          }

        rhs[id] -= Grr*qrr+Grs*qrs+Grs*qsr+Gss*qss + J*lambda*qM - Lndq;
      }
    }
  }
}
