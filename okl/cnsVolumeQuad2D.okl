
// isothermal Compressible Navier-Stokes
kernel void cnsVolumeQuad2D(const dlong Nelements,
                            const int advSwitch,
                            const dfloat * restrict vgeo,
                            const dfloat * restrict D,
                            const dfloat * restrict viscousStresses,
                            const dfloat * restrict q,
                            dfloat * restrict rhsq){
  
  for(dlong e=0;e<Nelements;++e;outer0){

    shared dfloat s_D[p_Nq][p_Nq];
    shared dfloat s_F[p_Nfields][p_Nq][p_Nq];
    shared dfloat s_G[p_Nfields][p_Nq][p_Nq];
    
    for(int j=0;j<p_Nq;++j;inner1){
      for(int i=0;i<p_Nq;++i;inner0){
        s_D[j][i] = D[j*p_Nq+i];

        // geometric factors
        const dlong gbase = e*p_Np*p_Nvgeo + j*p_Nq + i;
        const dfloat rx = vgeo[gbase+p_Np*p_RXID];
        const dfloat ry = vgeo[gbase+p_Np*p_RYID];
        const dfloat sx = vgeo[gbase+p_Np*p_SXID];
        const dfloat sy = vgeo[gbase+p_Np*p_SYID];
        const dfloat JW = vgeo[gbase+p_Np*p_JWID];

        // conserved variables
        const dlong  qbase = e*p_Np*p_Nfields + j*p_Nq + i;
        const dfloat r  = q[qbase+0*p_Np];
        const dfloat ru = q[qbase+1*p_Np];
        const dfloat rv = q[qbase+2*p_Np];
        const dfloat p  = r*p_RT;

        // primitive variables (velocity)
        const dfloat u = ru/r, v = rv/r;

        // viscous stresses (precomputed by cnsStressesVolumeQuad2D)
        const dlong id = e*p_Np*p_Nstresses + j*p_Nq + i;
        const dfloat T11 = viscousStresses[id+0*p_Np];
        const dfloat T12 = viscousStresses[id+1*p_Np];
        const dfloat T22 = viscousStresses[id+2*p_Np];
        
        // (1/J) \hat{div} (G*[F;G])

        {
          // F0 = ru, G0 = rv
          const dfloat f = -advSwitch*ru;
          const dfloat g = -advSwitch*rv;
          s_F[0][j][i] = JW*(rx*f + ry*g);
          s_G[0][j][i] = JW*(sx*f + sy*g);
        }

        {
          // F1 = 2*mu*S11 - (ru^2+p), G1 = 2*mu*S12 - (rvu)
          const dfloat f = T11-advSwitch*(ru*u+p);
          const dfloat g = T12-advSwitch*(rv*u);
          s_F[1][j][i] = JW*(rx*f + ry*g);
          s_G[1][j][i] = JW*(sx*f + sy*g);
        }

        {
          // F2 = 2*mu*S21 - (ruv), G2 = 2*mu*S22 - (rv^2+p)
          const dfloat f = T12-advSwitch*(rv*u);
          const dfloat g = T22-advSwitch*(rv*v+p);
          s_F[2][j][i] = JW*(rx*f + ry*g);
          s_G[2][j][i] = JW*(sx*f + sy*g);
        }
      }
    }

    barrier(localMemFence);
    
    for(int j=0;j<p_Nq;++j;inner1){
      for(int i=0;i<p_Nq;++i;inner0){    
        const dlong gid = e*p_Np*p_Nvgeo+ j*p_Nq +i;
        const dfloat invJW = vgeo[gid + p_IJWID*p_Np];

        dfloat rhsq0 = 0, rhsq1 = 0, rhsq2 = 0;

        for(int n=0;n<p_Nq;++n){
          const dfloat Din = s_D[n][i];
          const dfloat Djn = s_D[n][j];
          rhsq0 += Din*s_F[0][j][n];
          rhsq0 += Djn*s_G[0][n][i];
          rhsq1 += Din*s_F[1][j][n];
          rhsq1 += Djn*s_G[1][n][i];
          rhsq2 += Din*s_F[2][j][n];
          rhsq2 += Djn*s_G[2][n][i];
        }
        
        const dlong base = e*p_Np*p_Nfields + j*p_Nq + i;
        
        // move to rhs
        rhsq[base+0*p_Np] = -invJW*rhsq0;
        rhsq[base+1*p_Np] = -invJW*rhsq1;
        rhsq[base+2*p_Np] = -invJW*rhsq2;
        
      }
    }
  }
}


kernel void cnsStressesVolumeQuad2D(const dlong Nelements,
                                    const dfloat * restrict vgeo,
                                    const dfloat * restrict D,
                                    const dfloat mu,
                                    const dfloat * restrict q,
                                    dfloat * restrict viscousStresses){
  
  for(dlong e=0;e<Nelements;++e;outer0){

    shared dfloat s_D[p_Nq][p_Nq];
    shared dfloat s_u[p_Nq][p_Nq];
    shared dfloat s_v[p_Nq][p_Nq];
    
    for(int j=0;j<p_Nq;++j;inner1){
      for(int i=0;i<p_Nq;++i;inner0){
        
        s_D[j][i] = D[j*p_Nq+i];

        const dlong qbase = e*p_Nfields*p_Np + j*p_Nq + i;
        const dfloat r  = q[qbase + 0*p_Np];
        const dfloat ru = q[qbase + 1*p_Np];
        const dfloat rv = q[qbase + 2*p_Np];
        
        s_u[j][i] = ru/r;
        s_v[j][i] = rv/r;
        
      }
    }
    
    barrier(localMemFence);
    
    for(int j=0;j<p_Nq;++j;inner1){
      for(int i=0;i<p_Nq;++i;inner0){
        
        dfloat dudr = 0, duds = 0, dvdr = 0, dvds = 0;

        for(int n=0;n<p_Nq;++n){
          const dfloat Din = s_D[i][n];
          const dfloat Djn = s_D[j][n];

          dudr += Din*s_u[j][n];
          duds += Djn*s_u[n][i];

          dvdr += Din*s_v[j][n];
          dvds += Djn*s_v[n][i];
        }

        const dlong gbase = e*p_Np*p_Nvgeo + j*p_Nq + i;
        const dfloat rx = vgeo[gbase+p_Np*p_RXID];
        const dfloat ry = vgeo[gbase+p_Np*p_RYID];
        const dfloat sx = vgeo[gbase+p_Np*p_SXID];
        const dfloat sy = vgeo[gbase+p_Np*p_SYID];

        const dfloat dudx = rx*dudr + sx*duds;
        const dfloat dudy = ry*dudr + sy*duds;
        const dfloat dvdx = rx*dvdr + sx*dvds;
        const dfloat dvdy = ry*dvdr + sy*dvds;
        
        const dlong sbase = e*p_Nstresses*p_Np + j*p_Nq + i;

        const dfloat S11 = p_half*(dudx+dudx) - p_third*(dudx+dvdy);
        const dfloat S12 = p_half*(dudy+dvdx);
        const dfloat S22 = p_half*(dvdy+dvdy) - p_third*(dudx+dvdy);
        
        viscousStresses[sbase + 0*p_Np] = p_two*mu*S11;
        viscousStresses[sbase + 1*p_Np] = p_two*mu*S12;
        viscousStresses[sbase + 2*p_Np] = p_two*mu*S22;
      }
    }
  }
}
