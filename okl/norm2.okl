
@kernel void norm2(const dlong N,
		  @restrict const  dfloat *  w,
		  @restrict const  dfloat *  x,
		  @restrict dfloat *  wxx){
  
  
  for(dlong b=0;b<(N+p_blockSize-1)/p_blockSize;++b;@outer(0)){
    
    @shared volatile dfloat s_wxx[p_blockSize];

    for(int t=0;t<p_blockSize;++t;@inner(0)){
      const dlong id = t + b*p_blockSize;
      const dfloat xid = (id<N)?x[id]:0;
      const dfloat wid = (id<N)?w[id]:0;
      s_wxx[t] = wid*xid*xid;
    }

    @barrier("local");

#if p_blockSize>512
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<512) s_wxx[t] += s_wxx[t+512];
    @barrier("local");
#endif

#if p_blockSize>256
    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<256) s_wxx[t] += s_wxx[t+256];
    @barrier("local");
#endif

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<128) s_wxx[t] += s_wxx[t+128];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 64) s_wxx[t] += s_wxx[t+ 64];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 32) s_wxx[t] += s_wxx[t+ 32];
    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t< 16) s_wxx[t] += s_wxx[t+ 16];
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  8) s_wxx[t] += s_wxx[t+  8];
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  4) s_wxx[t] += s_wxx[t+  4];
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  2) s_wxx[t] += s_wxx[t+  2];
    //    @barrier("local");

    for(int t=0;t<p_blockSize;++t;@inner(0)) if(t<  1) wxx[b] = s_wxx[0] + s_wxx[1];
  }
}

