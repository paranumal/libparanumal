/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

#define p_Nvars 3
#define p_Nrelax 3

@kernel void bnsRelaxationQuad2D(const dlong Nelements,
                               @restrict const  dlong *  elementIds,
                               @restrict const  dfloat *  vgeo,
                               @restrict const  dfloat *  cubvgeo,
                               @restrict const  dfloat *  cubInterp,
                               @restrict const  dfloat *  cubProject,
                               const int semiAnalytic,
                               const dfloat tauInv,
                               @restrict const  dfloat *  q,
                                     @restrict dfloat *  rhsq){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockCub;@outer(0)){  // for all elements

    // @shared memory for q at nodes
    @shared dfloat s_q[p_NblockCub][p_Nfields][p_cubNq][p_cubNq];

    @exclusive dfloat r_q[p_Nfields];

    @shared dfloat s_cubInterp[p_Nq][p_cubNq];
    @shared dfloat s_cubProject[p_cubNq][p_Nq];

    @exclusive dlong e;

    //fetch the U and V values and store in flux arrays in @shared
    for(int es=0;es<p_NblockCub;++es;@inner(2)){
      for(int j=0;j<p_cubNq;++j;@inner(1)){
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          dlong et = eo+es; // element in block
          if (et<Nelements) {
            e = elementIds[et];
            if ((i<p_Nq) && (j<p_Nq)){
              const dlong id = e*p_Nfields*p_Np + j*p_Nq + i;

              #pragma unroll p_Nfields
              for(int fld=0; fld<p_Nfields;++fld){
                s_q[es][fld][j][i] = q[id+fld*p_Np];
              }
            }
          }

          const int ids = i+j*p_cubNq;
          if ((es==0) && (ids<p_Nq*p_cubNq)) {
            s_cubInterp[0][ids] = cubInterp[ids];
            s_cubProject[0][ids] = cubProject[ids];
          }
        }
      }
    }

    @barrier("local");

    //interpolate in i, store in register
    for(int es=0;es<p_NblockCub;++es;@inner(2)){
      for(int j=0;j<p_cubNq;++j;@inner(1)){
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if (j<p_Nq) {

            for(int fld=0; fld<p_Nfields; fld++)
              r_q[fld] = 0.f;

            #pragma unroll p_Nq
            for (int n=0;n<p_Nq;n++) {
              const dfloat Ini = s_cubInterp[n][i];
              #pragma unroll p_Nfields
              for(int fld=0; fld<p_Nfields; fld++){
                r_q[fld]  += Ini*s_q[es][fld][j][n];
              }
            }
          }
        }
      }
    }

    @barrier("local");

    //write register back to @shared
    for(int es=0;es<p_NblockCub;++es;@inner(2)){
      for(int j=0;j<p_cubNq;++j;@inner(1)){
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if (j<p_Nq) {
            for(int fld=0; fld<p_Nfields; fld++){
              s_q[es][fld][j][i] = r_q[fld];
            }
          }
        }
      }
    }

    @barrier("local");


    //interpolate in j, store in register
    for(int es=0;es<p_NblockCub;++es;@inner(2)){
      for(int j=0;j<p_cubNq;++j;@inner(1)){
        for(int i=0;i<p_cubNq;++i;@inner(0)){

          for(int fld=0; fld<p_Nfields; fld++)
            r_q[fld] = 0.f;

          #pragma unroll p_Nq
          for (int n=0;n<p_Nq;n++) {
            const dfloat Ini = s_cubInterp[n][j];
            // #pragma unroll p_Nfields
            for(int fld=0; fld<p_Nfields; fld++){
              r_q[fld]  += Ini*s_q[es][fld][n][i];
            }
          }
        }
      }
    }

    @barrier("local");

    //construct nonlinear term from registers
    for(int es=0;es<p_NblockCub;++es;@inner(2)){
      for(int j=0;j<p_cubNq;++j;@inner(1)){
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          dlong et = eo+es; // element in block
          if(et<Nelements) {
            const dlong gid = e*p_cubNp*p_Nvgeo+ j*p_cubNq +i;
            const dfloat JW = cubvgeo[gid + p_JWID*p_cubNp];

            if(semiAnalytic){
              // BGK relaxation approximation to the Boltzmann collision operator
              s_q[es][3][j][i] =  JW*tauInv*(r_q[1]*r_q[2]/(r_q[0]          ));
              s_q[es][4][j][i] =  JW*tauInv*(r_q[1]*r_q[1]/(r_q[0]*sqrt(2.0)));
              s_q[es][5][j][i] =  JW*tauInv*(r_q[2]*r_q[2]/(r_q[0]*sqrt(2.0)));
            }else{
              // BGK relaxation approximation to the Boltzmann collision operator
              // can be simplified by seperating linear part
              s_q[es][3][j][i] = -JW*tauInv*(r_q[3] - r_q[1]*r_q[2]/(r_q[0]          ));
              s_q[es][4][j][i] = -JW*tauInv*(r_q[4] - r_q[1]*r_q[1]/(r_q[0]*sqrt(2.0)));
              s_q[es][5][j][i] = -JW*tauInv*(r_q[5] - r_q[2]*r_q[2]/(r_q[0]*sqrt(2.0)));
            }
          }
        }
      }
    }

    @barrier("local");

    //project in j
    for(int es=0;es<p_NblockCub;++es;@inner(2)){
      for(int j=0;j<p_cubNq;++j;@inner(1)){
        for(int i=0;i<p_cubNq;++i;@inner(0)){

          for(int fld=0; fld<p_Nrelax; fld++)
            r_q[fld] = 0.f;

          if (j<p_Nq) {
            #pragma unroll p_cubNq
            for (int n=0;n<p_cubNq;n++) {
              const dfloat Pnj = s_cubProject[n][j];
              for(int fld=0; fld<p_Nrelax; fld++){
                r_q[fld] += Pnj*s_q[es][fld+p_Nvars][n][i];
              }
            }
          }
        }
      }
    }

    @barrier("local");

    //write register back to @shared
    for(int es=0;es<p_NblockCub;++es;@inner(2)){
      for(int j=0;j<p_cubNq;++j;@inner(1)){
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if (j<p_Nq) {
            for(int fld=0; fld<p_Nrelax; fld++){
              s_q[es][fld][j][i] = r_q[fld];
            }
          }
        }
      }
    }

    @barrier("local");

    // project in i and update
    for(int es=0;es<p_NblockCub;++es;@inner(2)){
      for(int j=0;j<p_cubNq;++j;@inner(1)){
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          const dlong et = eo+es; // element in block
          if((et<Nelements) && (i<p_Nq) && (j<p_Nq)){
            const dlong gid = e*p_Np*p_Nvgeo+ j*p_Nq +i;
            const dfloat invJW = vgeo[gid + p_IJWID*p_Np];

            for(int fld=0; fld<p_Nrelax; fld++)
              r_q[fld] = 0.f;


            #pragma unroll p_cubNq
            for(int n=0;n<p_cubNq;++n){
              const dfloat Pni = s_cubProject[n][i];
              for(int fld=0; fld<p_Nrelax; fld++){
                r_q[fld] += Pni*s_q[es][fld][j][n];
              }
            }

            const dlong rhsId = e*p_Np*p_Nfields + j*p_Nq + i;

            for(int fld=0; fld<p_Nrelax; fld++){
              rhsq[rhsId + (fld+p_Nvars)*p_Np]  += invJW*r_q[fld];
            }
          }
        }
      }
    }
  }
}




// Fully Explicit Scheme Uses Clasical half Half Splitting
@kernel void bnsPmlRelaxationCubQuad2D(const dlong pmlNelements,
                                  @restrict const  dlong *  pmlElementIds,
                                  @restrict const  dlong *  pmlIds,
                                  @restrict const  dfloat *  vgeo,
                                  @restrict const  dfloat *  cubvgeo,
                                  @restrict const  dfloat *  cubInterp,
                                  @restrict const  dfloat *  cubProject,
                                  @restrict const  dfloat *  pmlSigma,
                                            const dfloat pmlAlpha,
                                            const int semiAnalytic,
                                            const dfloat tauInv,
                                  @restrict const  dfloat *  q,
                                  @restrict const  dfloat *  pmlq,
                                        @restrict dfloat *  rhsq,
                                        @restrict dfloat *  rhspmlq){

  for(dlong et=0;et<pmlNelements;et++;@outer(0)){  // for all elements

    // @shared memory for q at nodes
    @shared dfloat s_q[p_Nfields][p_cubNq][p_cubNq];
    @shared dfloat s_qx[p_Nfields][p_cubNq][p_cubNq];
    @shared dfloat s_qy[p_Nfields][p_cubNq][p_cubNq];

    @exclusive dfloat r_q[p_Nfields];
    @exclusive dfloat r_qx[p_Nfields];
    @exclusive dfloat r_qy[p_Nfields];

    @shared dfloat s_cubInterp[p_Nq][p_cubNq];
    @shared dfloat s_cubProject[p_cubNq][p_Nq];

    @exclusive dlong e, pmlId;

    //fetch the U and V values and store in flux arrays in @shared
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        e     = pmlElementIds[et];
        pmlId = pmlIds[et];

        if( (i<p_Nq) && (j<p_Nq)){
          const dlong id =  e*p_Nfields*p_Np + j*p_Nq + i;
          const dlong pid = pmlId*p_Npmlfields*p_Np + j*p_Nq + i;

          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields;++fld){
            s_q[fld][j][i]  = q[id+fld*p_Np];
            s_qx[fld][j][i] = pmlq[pid + 0*p_Np*p_Nfields + fld*p_Np];
            s_qy[fld][j][i] = pmlq[pid + 1*p_Np*p_Nfields + fld*p_Np];
          }
        }

        const int ids = i+j*p_cubNq;
        if (ids<p_Nq*p_cubNq) {
          s_cubInterp[0][ids] = cubInterp[ids];
          s_cubProject[0][ids] = cubProject[ids];
        }
      }
    }

    @barrier("local");

    //interpolate in i, store in register
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if (j<p_Nq) {

          for(int fld=0; fld<p_Nfields; fld++){
            r_q[fld] = 0.f;
            r_qx[fld] = 0.f;
            r_qy[fld] = 0.f;
          }

          #pragma unroll p_Nq
          for (int n=0;n<p_Nq;n++) {
            const dfloat Ini = s_cubInterp[n][i];
            #pragma unroll p_Nfields
            for(int fld=0; fld<p_Nfields; fld++){
              r_q[fld]   += Ini*s_q[fld][j][n];
              r_qx[fld]  += Ini*s_qx[fld][j][n];
              r_qy[fld]  += Ini*s_qy[fld][j][n];
            }
          }
        }
      }
    }

    @barrier("local");

    //write register back to @shared
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if (j<p_Nq) {
          for(int fld=0; fld<p_Nfields; fld++){
            s_q[fld][j][i]  = r_q[fld];
            s_qx[fld][j][i] = r_qx[fld];
            s_qy[fld][j][i] = r_qy[fld];
          }
        }
      }
    }

    @barrier("local");

     //interpolate in j, store in register
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){

        for(int fld=0; fld<p_Nfields; fld++){
          r_q[fld]  = 0.f;
          r_qx[fld] = 0.f;
          r_qy[fld] = 0.f;
        }
        #pragma unroll p_Nq
        for (int n=0;n<p_Nq;n++) {
          const dfloat Ini = s_cubInterp[n][j];
          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields; fld++){
            r_q[fld]   += Ini*s_q[fld][n][i];
            r_qx[fld]  += Ini*s_qx[fld][n][i];
            r_qy[fld]  += Ini*s_qy[fld][n][i];
          }
        }
      }
    }

    @barrier("local");

    //construct nonlinear term from registers
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){

        const dlong gid = e*p_cubNp*p_Nvgeo+ j*p_cubNq +i;
        const dfloat JW = cubvgeo[gid + p_JWID*p_cubNp];

        // Prefetch sigma
        const dfloat sigmaxe = pmlSigma[2*pmlId*p_cubNp + 0*p_cubNp + j*p_cubNq + i];
        const dfloat sigmaye = pmlSigma[2*pmlId*p_cubNp + 1*p_cubNp + j*p_cubNq + i];
        // M-PML sigma midification
        const dfloat msigmaxe = sigmaxe + sigmaye*pmlAlpha;
        const dfloat msigmaye = sigmaye + sigmaxe*pmlAlpha;

        #pragma unroll p_Nfields
        for(int fld=0; fld<p_Nfields ; fld++){
          s_qx[fld][j][i] = -JW*msigmaxe*r_qx[fld];
          s_qy[fld][j][i] = -JW*msigmaye*r_qy[fld];
          s_q [fld][j][i] = -JW*(msigmaxe*r_qx[fld] + msigmaye*r_qy[fld]);
        }

        if(semiAnalytic){
        // BGK relaxation approximation to the Boltzmann collision operator
          s_q[3][j][i] +=  JW*tauInv*(r_q[1]*r_q[2]/(r_q[0]          ));
          s_q[4][j][i] +=  JW*tauInv*(r_q[1]*r_q[1]/(r_q[0]*sqrt(2.0)));
          s_q[5][j][i] +=  JW*tauInv*(r_q[2]*r_q[2]/(r_q[0]*sqrt(2.0)));
        }else{
          // can be simplified by seperating linear part
          s_q[3][j][i] -= JW*tauInv*(r_q[3] - r_q[1]*r_q[2]/(r_q[0]          ));
          s_q[4][j][i] -= JW*tauInv*(r_q[4] - r_q[1]*r_q[1]/(r_q[0]*sqrt(2.0)));
          s_q[5][j][i] -= JW*tauInv*(r_q[5] - r_q[2]*r_q[2]/(r_q[0]*sqrt(2.0)));
        }
      }
    }


    @barrier("local");

    //project in j
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        for(int fld=0; fld<p_Nfields; fld++){
          r_q[fld]  = 0.f;
          r_qx[fld] = 0.f;
          r_qy[fld] = 0.f;
        }
        if (j<p_Nq) {
          #pragma unroll p_cubNq
          for (int n=0;n<p_cubNq;n++) {
            const dfloat Pnj = s_cubProject[n][j];
            for(int fld=0; fld<p_Nfields; fld++){
              r_q[fld]  += Pnj*s_q[fld][n][i];
              r_qx[fld] += Pnj*s_qx[fld][n][i];
              r_qy[fld] += Pnj*s_qy[fld][n][i];
            }
          }
        }
      }
    }

    @barrier("local");

    //write register back to @shared
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if (j<p_Nq) {
          for(int fld=0; fld<p_Nfields; fld++){
            s_q[fld][j][i]  = r_q[fld];
            s_qx[fld][j][i] = r_qx[fld];
            s_qy[fld][j][i] = r_qy[fld];
          }
        }
      }
    }

    @barrier("local");

    // project in i and update
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if((i<p_Nq) && (j<p_Nq)){
          const dlong gid = e*p_Np*p_Nvgeo+ j*p_Nq +i;
          const dfloat invJW = vgeo[gid + p_IJWID*p_Np];

          for(int fld=0; fld<p_Nfields; fld++){
            r_q[fld] = 0.f;
            r_qx[fld] = 0.f;
            r_qy[fld] = 0.f;
          }

          #pragma unroll p_cubNq
          for(int n=0;n<p_cubNq;++n){
            const dfloat Pni = s_cubProject[n][i];
            for(int fld=0; fld<p_Nfields; fld++){
              r_q[fld]  += Pni*s_q[fld][j][n];
              r_qx[fld] += Pni*s_qx[fld][j][n];
              r_qy[fld] += Pni*s_qy[fld][j][n];
            }
          }

          const dlong rhsId    = e*p_Np*p_Nfields + j*p_Nq + i;
          const dlong pmlRhsId = pmlId*p_Np*p_Npmlfields + j*p_Nq + i;

          for(int fld=0; fld<p_Nfields; fld++){
            rhspmlq[pmlRhsId + 0*p_Np*p_Nfields + fld*p_Np] += invJW*r_qx[fld];
            rhspmlq[pmlRhsId + 1*p_Np*p_Nfields + fld*p_Np] += invJW*r_qy[fld];
            rhsq[rhsId + fld*p_Np] += invJW*r_q[fld];
          }
        }
      }
    }
  }
}
