/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

// #define p_Nvort 4

@kernel void bnsVorticityTri2D(const dlong Nelements,
                              @restrict const  dfloat *  vgeo,
                              @restrict const  dfloat *  DT,
                              @restrict const  dfloat *  q,
                                    @restrict dfloat *  Vort,
                                    @restrict dfloat *  VortMag){  
  
  // block partition of elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){
    
    @shared dfloat s_u[p_NblockV][p_Np];
    @shared dfloat s_v[p_NblockV][p_Np];
    for(int es=0;es<p_NblockV;++es;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
      const dlong e = eo+es; 
        if (e<Nelements) {
          const dlong id = e*p_Nfields*p_Np + n;
          const dfloat q0  = q[id + 0*p_Np]; // rho
          const dfloat q1  = q[id + 1*p_Np]; // q1
          const dfloat q2  = q[id + 2*p_Np]; // q2
          // get physical velocities
          s_u[es][n] = p_sqrtRT*q1/q0;
          s_v[es][n] = p_sqrtRT*q2/q0;        
        }
      }
    }

    @barrier("local");

    for(int es=0;es<p_NblockV;++es;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
        const dlong e = eo+es; 
        if(e<Nelements){

          const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
          const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
          const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
          const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];

          dfloat ur = 0, vr = 0;
          dfloat us = 0, vs = 0;

          #pragma unroll p_Np
            for(int i=0;i<p_Np;++i) {
              const dfloat Drn = DT[n + i*p_Np+0*p_Np*p_Np];
              const dfloat Dsn = DT[n + i*p_Np+1*p_Np*p_Np];
              ur += Drn*s_u[es][i];
              us += Dsn*s_u[es][i];
              vr += Drn*s_v[es][i];
              vs += Dsn*s_v[es][i];
            }

          const dfloat uy = drdy*ur + dsdy*us;
          const dfloat vx = drdx*vr + dsdx*vs;

          const dlong id = p_Nvort*e*p_Np + n; 
          Vort[id] = vx-uy;
        }
      }
    }
  }
}


