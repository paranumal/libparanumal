/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


// Only difference with boltzmannVolume2D is size of rhsq and shifting
@kernel void bnsVolumeTet3D(const dlong Nelements,
         @restrict const  dlong *  elementIds,
         const dlong offset,
         const int shift,
         const dfloat fx,
         const dfloat fy,
         const dfloat fz,
         @restrict const  dfloat *  vgeo,
         @restrict const  dfloat *  Dmatrices,
         @restrict const  dfloat *  q,
         @restrict dfloat *  rhsq){
  
  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){  // for all elements

    @shared dfloat s_q[p_NblockV][p_Nfields][p_Np];
    @exclusive dlong e;

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element

  dlong et = eo+es; // element in block

  if(et<Nelements){
    e = elementIds[et];
    const dlong id = e*p_Nfields*p_Np + n;
        
    #pragma unroll p_Nfields
      for(int fld=0; fld<p_Nfields;++fld){
        s_q[es][fld][n] = q[id+fld*p_Np];
      }

  }
      }
    }
    // make sure all node data is loaded into @shared
    @barrier("local");

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element 
  dlong et = eo+es; // element in block
  if(et<Nelements){   
    // prefetch geometric factors (constant on triangle)
    const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
    const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
    const dfloat drdz = vgeo[e*p_Nvgeo + p_RZID];
    const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
    const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
    const dfloat dsdz = vgeo[e*p_Nvgeo + p_SZID];
    const dfloat dtdx = vgeo[e*p_Nvgeo + p_TXID];
    const dfloat dtdy = vgeo[e*p_Nvgeo + p_TYID];
    const dfloat dtdz = vgeo[e*p_Nvgeo + p_TZID];

    // compute 'r' and 's' derivatives of (q_m) at node n
    dfloat r_dqdr[p_Nfields], r_dqds[p_Nfields], r_dqdt[p_Nfields]; 
    dfloat r_dqdx[p_Nfields], r_dqdy[p_Nfields], r_dqdz[p_Nfields];
    dfloat r_rhsq[p_Nfields];
        
    #pragma unroll p_Nfields
      for(int fld=0; fld<p_Nfields;++fld){
        r_dqdr[fld] = 0.f, r_dqds[fld] = 0.f, r_dqdt[fld] = 0.f;
        r_dqdx[fld] = 0.f, r_dqdy[fld] = 0.f, r_dqdz[fld] = 0.f;
        r_rhsq[fld] = 0.f;
      }

    #pragma unroll p_Np
      for(int i=0;i<p_Np;++i){
        const dfloat Drni = Dmatrices[n+i*p_Np+0*p_Np*p_Np];
        const dfloat Dsni = Dmatrices[n+i*p_Np+1*p_Np*p_Np];
        const dfloat Dtni = Dmatrices[n+i*p_Np+2*p_Np*p_Np];
        #pragma unroll p_Nfields
    for(int fld=0; fld<p_Nfields;++fld){
      r_dqdr[fld] += Drni*s_q[es][fld][i];
      r_dqds[fld] += Dsni*s_q[es][fld][i];
      r_dqdt[fld] += Dtni*s_q[es][fld][i];
    }
      }
      
    // Compute derivatives in physical coordinates 
    #pragma unroll p_Nfields
      for(int fld=0; fld<p_Nfields;++fld){
        r_dqdx[fld] = drdx*r_dqdr[fld] + dsdx*r_dqds[fld] + dtdx*r_dqdt[fld];
        r_dqdy[fld] = drdy*r_dqdr[fld] + dsdy*r_dqds[fld] + dtdy*r_dqdt[fld];
        r_dqdz[fld] = drdz*r_dqdr[fld] + dsdz*r_dqds[fld] + dtdz*r_dqdt[fld];
      }

    // transport operator
    r_rhsq[0] = -p_sqrtRT*(r_dqdx[1] + r_dqdy[2] + r_dqdz[3]);
    r_rhsq[1] = -p_sqrtRT*(r_dqdx[0] + p_sqrt2*r_dqdx[7] + r_dqdy[4] + r_dqdz[5]); 
    r_rhsq[2] = -p_sqrtRT*(r_dqdx[4] + r_dqdy[0] + p_sqrt2*r_dqdy[8] + r_dqdz[6]); 
    r_rhsq[3] = -p_sqrtRT*(r_dqdx[5] + r_dqdy[6] + r_dqdz[0] + p_sqrt2*r_dqdz[9]); 

    r_rhsq[4] = -p_sqrtRT*(r_dqdx[2] + r_dqdy[1]);
    r_rhsq[5] = -p_sqrtRT*(r_dqdx[3] + r_dqdz[1]);
    r_rhsq[6] = -p_sqrtRT*(r_dqdy[3] + r_dqdz[2]);
        
    r_rhsq[7] = -p_sqrtRT*p_sqrt2*r_dqdx[1];     
    r_rhsq[8] = -p_sqrtRT*p_sqrt2*r_dqdy[2];        
    r_rhsq[9] = -p_sqrtRT*p_sqrt2*r_dqdz[3];        

    if(fx){
      // add x-body forcing (e.g. gravity)
      r_rhsq[1] += fx*p_isqrtRT*s_q[es][0][n]; // assumes AX = gx/(sqrt(RT))
      r_rhsq[4] += fx*p_isqrtRT*s_q[es][2][n];
      r_rhsq[5] += fx*p_isqrtRT*s_q[es][3][n];
      r_rhsq[7] += p_sqrt2*fx*p_isqrtRT*s_q[es][1][n];
    }

    if(fy){
      // add y-body forcing (e.g. gravity)
      r_rhsq[2] += fy*p_isqrtRT*s_q[es][0][n]; // assumes AY = gy/(sqrt(RT))
      r_rhsq[4] += fy*p_isqrtRT*s_q[es][1][n];
      r_rhsq[6] += fy*p_isqrtRT*s_q[es][3][n];
      r_rhsq[8] += p_sqrt2*fy*p_isqrtRT*s_q[es][2][n];
    }

    if(fz){
      // add z-body forcing (e.g. gravity)
      r_rhsq[3] += fz*p_isqrtRT*s_q[es][0][n]; // assumes AZ = gz/(sqrt(RT))
      r_rhsq[5] += fz*p_isqrtRT*s_q[es][1][n];
      r_rhsq[6] += fz*p_isqrtRT*s_q[es][2][n];
      r_rhsq[9] += p_sqrt2*fz*p_isqrtRT*s_q[es][3][n];
    }

    
#ifdef p_AX
    // add x-body forcing (e.g. gravity)
    r_rhsq[1] += p_AX*s_q[es][0][n]; // assumes AX = gx/(sqrt(RT))
    r_rhsq[4] += p_AX*s_q[es][2][n];
    r_rhsq[5] += p_AX*s_q[es][3][n];
    r_rhsq[7] += p_sqrt2*p_AX*s_q[es][1][n];
#endif

#ifdef p_AY
    // add y-body forcing (e.g. gravity)
    r_rhsq[2] += p_AY*s_q[es][0][n]; // assumes AY = gy/(sqrt(RT))
    r_rhsq[4] += p_AY*s_q[es][1][n];
    r_rhsq[6] += p_AY*s_q[es][3][n];
    r_rhsq[8] += p_sqrt2*p_AX*s_q[es][2][n];
#endif

#ifdef p_AZ
    // add z-body forcing (e.g. gravity)
    r_rhsq[3] += p_AZ*s_q[es][0][n]; // assumes AZ = gz/(sqrt(RT))
    r_rhsq[5] += p_AZ*s_q[es][1][n];
    r_rhsq[6] += p_AZ*s_q[es][2][n];
    r_rhsq[9] += p_sqrt2*p_AZ*s_q[es][3][n];
#endif

    // Update 
    const dlong id    = e*p_Nfields*p_Np + n;
    dlong rhsId = id;

    if(p_MRSAAB){
      rhsId   += shift*offset;  
    }

    for(int fld=0; fld<p_Nfields;++fld){
      rhsq[rhsId + fld*p_Np] = r_rhsq[fld];
    }


  }
      }
    }
  }
}




@kernel void bnsPmlVolumeCubTet3D(const dlong pmlNelements,
            @restrict const  dlong *  pmlElementIds,
            @restrict const  dlong *  pmlIds,
            const dlong offset,
            const dlong pmloffset,
            const int   shift,
            const dfloat fx,
            const dfloat fy,
            const dfloat fz,
            @restrict const  dfloat *  vgeo,
            @restrict const  dfloat *  Dmatrices,
            @restrict const  dfloat *  q,
            @restrict const  dfloat *  pmlqx,
            @restrict const  dfloat *  pmlqy,
            @restrict const  dfloat *  pmlqz,
            @restrict dfloat *  rhsq,
            @restrict dfloat *  pmlrhsqx,
            @restrict dfloat *  pmlrhsqy,
            @restrict dfloat *  pmlrhsqz){
  
  for(dlong eo=0;eo<pmlNelements;eo+=p_NblockV;@outer(0)){  // for all elements

    @shared dfloat s_q[p_NblockV][p_Nfields][p_Np];
    @exclusive dlong e, pmlId;

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element

  dlong et = eo+es; // element in block
  if(et<pmlNelements){
    e     = pmlElementIds[et];
    pmlId = pmlIds[et];

    const dlong id = e*p_Nfields*p_Np + n;
    #pragma unroll p_Nfields
      for(int fld=0; fld<p_Nfields;++fld){
        s_q[es][fld][n] = q[id+fld*p_Np];
      }
  }
      }
    }

    // make sure all node data is loaded into @shared
    @barrier("local");

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element 
  dlong et = eo+es; // element in block
  if(et<pmlNelements){  

    // prefetch geometric factors (constant on triangle)
    const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
    const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
    const dfloat drdz = vgeo[e*p_Nvgeo + p_RZID];
    const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
    const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
    const dfloat dsdz = vgeo[e*p_Nvgeo + p_SZID];
    const dfloat dtdx = vgeo[e*p_Nvgeo + p_TXID];
    const dfloat dtdy = vgeo[e*p_Nvgeo + p_TYID];
    const dfloat dtdz = vgeo[e*p_Nvgeo + p_TZID];
        
    // compute 'r' and 's' derivatives of (q_m) at node n
    dfloat r_dqdr[p_Nfields], r_dqds[p_Nfields], r_dqdt[p_Nfields]; 
    dfloat r_dqdx[p_Nfields], r_dqdy[p_Nfields], r_dqdz[p_Nfields];
    dfloat r_Aqx[p_Nfields],  r_Bqy[p_Nfields],  r_Cqz[p_Nfields];
    dfloat r_N[p_Nfields];
        
    // Initialize
    #pragma unroll p_Nfields
      for(int fld=0; fld<p_Nfields;++fld){
        r_dqdr[fld] = 0.f, r_dqds[fld] = 0.f, r_dqdt[fld] = 0.f;
        r_dqdx[fld] = 0.f, r_dqdy[fld] = 0.f, r_dqdz[fld] = 0.f;
        r_Aqx[fld]  = 0.f, r_Bqy[fld]  = 0.f, r_Cqz[fld]  = 0.f;
        r_N[fld]    = 0.f;
      }
    
    // Compute local derivatives
    #pragma unroll p_Np
      for(int i=0;i<p_Np;++i){
        const dfloat Drni = Dmatrices[n+i*p_Np+0*p_Np*p_Np];
        const dfloat Dsni = Dmatrices[n+i*p_Np+1*p_Np*p_Np];
        const dfloat Dtni = Dmatrices[n+i*p_Np+2*p_Np*p_Np];
        #pragma unroll p_Nfields
    for(int fld=0; fld<p_Nfields;++fld){
      r_dqdr[fld] += Drni*s_q[es][fld][i];
      r_dqds[fld] += Dsni*s_q[es][fld][i];
      r_dqdt[fld] += Dtni*s_q[es][fld][i];
    }
      }
        
    // Compute derivatives in physical coordinates 
    #pragma unroll p_Nfields
      for(int fld=0; fld<p_Nfields;++fld){
        r_dqdx[fld] = drdx*r_dqdr[fld] + dsdx*r_dqds[fld] + dtdx*r_dqdt[fld];
        r_dqdy[fld] = drdy*r_dqdr[fld] + dsdy*r_dqds[fld] + dtdy*r_dqdt[fld];
        r_dqdz[fld] = drdz*r_dqdr[fld] + dsdz*r_dqds[fld] + dtdz*r_dqdt[fld];
      }
  
    // Compute volume flux for q, qx and qy
    // A*dQ/dx
    r_Aqx[0] = -p_sqrtRT*(r_dqdx[1]);
    r_Aqx[1] = -p_sqrtRT*(r_dqdx[0] + p_sqrt2*r_dqdx[7]); 
    r_Aqx[2] = -p_sqrtRT*(r_dqdx[4]); 
    r_Aqx[3] = -p_sqrtRT*(r_dqdx[5]); 
    r_Aqx[4] = -p_sqrtRT*(r_dqdx[2]);
    r_Aqx[5] = -p_sqrtRT*(r_dqdx[3]);
    r_Aqx[6] =  0.f;
    r_Aqx[7] = -p_sqrtRT*p_sqrt2*r_dqdx[1];     
    r_Aqx[8] =  0.f;        
    r_Aqx[9] =  0.f;       
    // B*dQ/dy
    r_Bqy[0] = -p_sqrtRT*(r_dqdy[2]);
    r_Bqy[1] = -p_sqrtRT*(r_dqdy[4]); 
    r_Bqy[2] = -p_sqrtRT*(r_dqdy[0] + p_sqrt2*r_dqdy[8]); 
    r_Bqy[3] = -p_sqrtRT*(r_dqdy[6]); 
    r_Bqy[4] = -p_sqrtRT*(r_dqdy[1]);
    r_Bqy[5] =  0.f;
    r_Bqy[6] = -p_sqrtRT*(r_dqdy[3]);
    r_Bqy[7] =  0.f;     
    r_Bqy[8] = -p_sqrtRT*p_sqrt2*r_dqdy[2];        
    r_Bqy[9] =  0.f;

    // C*dq/dz
    r_Cqz[0] = -p_sqrtRT*(r_dqdz[3]);
    r_Cqz[1] = -p_sqrtRT*(r_dqdz[5]); 
    r_Cqz[2] = -p_sqrtRT*(r_dqdz[6]); 
    r_Cqz[3] = -p_sqrtRT*(r_dqdz[0] + p_sqrt2*r_dqdz[9]); 
    r_Cqz[4] =  0.f;
    r_Cqz[5] = -p_sqrtRT*(r_dqdz[1]);
    r_Cqz[6] = -p_sqrtRT*(r_dqdz[2]);       
    r_Cqz[7] =  0.f;     
    r_Cqz[8] =  0.f;        
    r_Cqz[9] = -p_sqrtRT*p_sqrt2*r_dqdz[3];              

    if(fx){
      // add x-body forcing (e.g. gravity)
      r_N[1] += fx*p_isqrtRT*s_q[es][0][n]; //  
      r_N[4] += fx*p_isqrtRT*s_q[es][2][n];
      r_N[5] += fx*p_isqrtRT*s_q[es][3][n];
      r_N[7] += p_sqrt2*fx*p_isqrtRT*s_q[es][1][n];
    }

    if(fy){
      // add y-body forcing (e.g. gravity)
      r_N[2] += fy*p_isqrtRT*s_q[es][0][n]; // 
      r_N[4] += fy*p_isqrtRT*s_q[es][1][n];
      r_N[6] += fy*p_isqrtRT*s_q[es][3][n];
      r_N[8] += p_sqrt2*fy*p_isqrtRT*s_q[es][2][n];
    }

    if(fz){
      // add z-body forcing (e.g. gravity) check p_isqrtRT !!
      r_N[3] += fz*p_isqrtRT*s_q[es][0][n]; // 
      r_N[5] += fz*p_isqrtRT*s_q[es][1][n];
      r_N[6] += fz*p_isqrtRT*s_q[es][2][n];
      r_N[9] += p_sqrt2*fz*p_isqrtRT*s_q[es][3][n];
    }

    dlong rhsId    = e*p_Nfields*p_Np + n;
    dlong pmlrhsId = p_Nfields*pmlId*p_Np + n;

    if(p_MRSAAB){
      rhsId     += shift*offset;
      pmlrhsId  += shift*pmloffset;
    }
          
    #pragma unroll p_Nfields
      for(int fld=0; fld<p_Nfields; ++fld){
        pmlrhsqx[pmlrhsId + fld*p_Np] =  r_Aqx[fld];
        pmlrhsqy[pmlrhsId + fld*p_Np] =  r_Bqy[fld];
        pmlrhsqz[pmlrhsId + fld*p_Np] =  r_Cqz[fld];
        rhsq[rhsId +fld*p_Np]         =  r_Aqx[fld] + r_Bqy[fld] + r_Cqz[fld] + r_N[fld];
         
      }

  }
      }
    }
  
  }
}





@kernel void bnsPmlVolumeTet3D(const dlong pmlNelements,
            @restrict const  dlong *  pmlElementIds,
            @restrict const  dlong *  pmlIds,
            const dlong offset,
            const dlong pmloffset,
            const int   shift,
            const dfloat fx,
            const dfloat fy,
            const dfloat fz,
            @restrict const  dfloat *  vgeo,
            @restrict const  dfloat *  pmlSigmaX,
            @restrict const  dfloat *  pmlSigmaY, 
            @restrict const  dfloat *  pmlSigmaZ, 
            @restrict const  dfloat *  Dmatrices,
            @restrict const  dfloat *  q,
            @restrict const  dfloat *  pmlqx,
            @restrict const  dfloat *  pmlqy,
            @restrict const  dfloat *  pmlqz,
            @restrict dfloat *  rhsq,
            @restrict dfloat *  pmlrhsqx,
            @restrict dfloat *  pmlrhsqy,
            @restrict dfloat *  pmlrhsqz){
  
  for(dlong eo=0;eo<pmlNelements;eo+=p_NblockV;@outer(0)){  // for all elements

    @shared dfloat s_q[p_NblockV][p_Nfields][p_Np];
    @exclusive dlong e, pmlId;

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element

      dlong et = eo+es; // element in block
      if(et<pmlNelements){
        e     = pmlElementIds[et];
        pmlId = pmlIds[et];

        const dlong id = e*p_Nfields*p_Np + n;
        #pragma unroll p_Nfields
        for(int fld=0; fld<p_Nfields;++fld){
          s_q[es][fld][n] = q[id+fld*p_Np];
        }
      }
    }
  }

    // make sure all node data is loaded into @shared
    @barrier("local");

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element 
      dlong et = eo+es; // element in block
      if(et<pmlNelements){  

        // prefetch geometric factors (constant on triangle)
        const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
        const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
        const dfloat drdz = vgeo[e*p_Nvgeo + p_RZID];
        const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
        const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
        const dfloat dsdz = vgeo[e*p_Nvgeo + p_SZID];
        const dfloat dtdx = vgeo[e*p_Nvgeo + p_TXID];
        const dfloat dtdy = vgeo[e*p_Nvgeo + p_TYID];
        const dfloat dtdz = vgeo[e*p_Nvgeo + p_TZID];
            
        // compute 'r' and 's' derivatives of (q_m) at node n
        dfloat r_dqdr[p_Nfields], r_dqds[p_Nfields], r_dqdt[p_Nfields]; 
        dfloat r_dqdx[p_Nfields], r_dqdy[p_Nfields], r_dqdz[p_Nfields];
        dfloat r_Aqx[p_Nfields],  r_Bqy[p_Nfields],  r_Cqz[p_Nfields];
        dfloat r_N[p_Nfields];

        const dfloat sigmaxe = pmlSigmaX[pmlId*p_Np+n];
        const dfloat sigmaye = pmlSigmaY[pmlId*p_Np+n];
        const dfloat sigmaze = pmlSigmaZ[pmlId*p_Np+n];

        // M-PML sigma midification using same scaling in non-normal directions
        const dfloat msigmaxe = sigmaxe + sigmaye*p_pmlAlpha + sigmaze*p_pmlAlpha;
        const dfloat msigmaye = sigmaye + sigmaxe*p_pmlAlpha + sigmaze*p_pmlAlpha;
        const dfloat msigmaze = sigmaze + sigmaxe*p_pmlAlpha + sigmaye*p_pmlAlpha;

        dlong base     = e*p_Nfields*p_Np + n;
        dlong pmlbase  = pmlId*p_Nfields*p_Np + n;

        for(int fld = 0; fld<p_Nfields; fld++){
          r_Aqx[fld] = -msigmaxe*pmlqx[pmlbase + fld*p_Np]; 
          r_Bqy[fld] = -msigmaye*pmlqy[pmlbase + fld*p_Np]; 
          r_Cqz[fld] = -msigmaze*pmlqz[pmlbase + fld*p_Np];
        }

        // update index for Rhs in MRSAAB
         if(p_MRSAAB){
          base      += shift*offset;
          pmlbase   += shift*pmloffset;
        }

        // Initialize
        #pragma unroll p_Nfields
        for(int fld=0; fld<p_Nfields;++fld){
          r_dqdr[fld] = 0.f, r_dqds[fld] = 0.f, r_dqdt[fld] = 0.f;
          r_dqdx[fld] = 0.f, r_dqdy[fld] = 0.f, r_dqdz[fld] = 0.f;
          r_N[fld]    = 0.f;
        }
    
        // Compute local derivatives
        #pragma unroll p_Np
        for(int i=0;i<p_Np;++i){
          const dfloat Drni = Dmatrices[n+i*p_Np+0*p_Np*p_Np];
          const dfloat Dsni = Dmatrices[n+i*p_Np+1*p_Np*p_Np];
          const dfloat Dtni = Dmatrices[n+i*p_Np+2*p_Np*p_Np];
          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields;++fld){
            r_dqdr[fld] += Drni*s_q[es][fld][i];
            r_dqds[fld] += Dsni*s_q[es][fld][i];
            r_dqdt[fld] += Dtni*s_q[es][fld][i];
          }
        }
        
        // Compute derivatives in physical coordinates 
        #pragma unroll p_Nfields
        for(int fld=0; fld<p_Nfields;++fld){
          r_dqdx[fld] = drdx*r_dqdr[fld] + dsdx*r_dqds[fld] + dtdx*r_dqdt[fld];
          r_dqdy[fld] = drdy*r_dqdr[fld] + dsdy*r_dqds[fld] + dtdy*r_dqdt[fld];
          r_dqdz[fld] = drdz*r_dqdr[fld] + dsdz*r_dqds[fld] + dtdz*r_dqdt[fld];
        }
  
        // Compute volume flux for q, qx and qy
        // A*dQ/dx
        r_Aqx[0] -= p_sqrtRT*(r_dqdx[1]);
        r_Aqx[1] -= p_sqrtRT*(r_dqdx[0] + p_sqrt2*r_dqdx[7]); 
        r_Aqx[2] -= p_sqrtRT*(r_dqdx[4]); 
        r_Aqx[3] -= p_sqrtRT*(r_dqdx[5]); 
        r_Aqx[4] -= p_sqrtRT*(r_dqdx[2]);
        r_Aqx[5] -= p_sqrtRT*(r_dqdx[3]);
        r_Aqx[6] -= 0.f;
        r_Aqx[7] -= p_sqrtRT*p_sqrt2*r_dqdx[1];     
        r_Aqx[8] -= 0.f;        
        r_Aqx[9] -= 0.f;       
        // B*dQ/dy
        r_Bqy[0] -= p_sqrtRT*(r_dqdy[2]);
        r_Bqy[1] -= p_sqrtRT*(r_dqdy[4]); 
        r_Bqy[2] -= p_sqrtRT*(r_dqdy[0] + p_sqrt2*r_dqdy[8]); 
        r_Bqy[3] -= p_sqrtRT*(r_dqdy[6]); 
        r_Bqy[4] -= p_sqrtRT*(r_dqdy[1]);
        r_Bqy[5] -= 0.f;
        r_Bqy[6] -= p_sqrtRT*(r_dqdy[3]);
        r_Bqy[7] -= 0.f;     
        r_Bqy[8] -= p_sqrtRT*p_sqrt2*r_dqdy[2];        
        r_Bqy[9] -= 0.f;

        // C*dq/dz
        r_Cqz[0] -= p_sqrtRT*(r_dqdz[3]);
        r_Cqz[1] -= p_sqrtRT*(r_dqdz[5]); 
        r_Cqz[2] -= p_sqrtRT*(r_dqdz[6]); 
        r_Cqz[3] -= p_sqrtRT*(r_dqdz[0] + p_sqrt2*r_dqdz[9]); 
        r_Cqz[4] -= 0.f;
        r_Cqz[5] -= p_sqrtRT*(r_dqdz[1]);
        r_Cqz[6] -= p_sqrtRT*(r_dqdz[2]);       
        r_Cqz[7] -= 0.f;     
        r_Cqz[8] -= 0.f;        
        r_Cqz[9] -= p_sqrtRT*p_sqrt2*r_dqdz[3];              

        if(fx){
          // add x-body forcing (e.g. gravity)
          r_N[1] += fx*p_isqrtRT*s_q[es][0][n]; //  
          r_N[4] += fx*p_isqrtRT*s_q[es][2][n];
          r_N[5] += fx*p_isqrtRT*s_q[es][3][n];
          r_N[7] += p_sqrt2*fx*p_isqrtRT*s_q[es][1][n];
        }
        
        if(fy){
          // add y-body forcing (e.g. gravity)
          r_N[2] += fy*p_isqrtRT*s_q[es][0][n]; // 
          r_N[4] += fy*p_isqrtRT*s_q[es][1][n];
          r_N[6] += fy*p_isqrtRT*s_q[es][3][n];
          r_N[8] += p_sqrt2*fy*p_isqrtRT*s_q[es][2][n];
        }

        if(fz){
          // add z-body forcing (e.g. gravity) check p_isqrtRT !!
          r_N[3] += fz*p_isqrtRT*s_q[es][0][n]; // 
          r_N[5] += fz*p_isqrtRT*s_q[es][1][n];
          r_N[6] += fz*p_isqrtRT*s_q[es][2][n];
          r_N[9] += p_sqrt2*fz*p_isqrtRT*s_q[es][3][n];
        }
      
        #pragma unroll p_Nfields
        for(int fld=0; fld<p_Nfields; ++fld){
          pmlrhsqx[pmlbase + fld*p_Np] =  r_Aqx[fld];
          pmlrhsqy[pmlbase + fld*p_Np] =  r_Bqy[fld];
          pmlrhsqz[pmlbase + fld*p_Np] =  r_Cqz[fld];
          rhsq[base +fld*p_Np]         =  r_Aqx[fld] + r_Bqy[fld] + r_Cqz[fld] + r_N[fld];
           
        }

       }
      }
    }
  
  }
}





