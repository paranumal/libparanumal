/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

@kernel void bnsVolumeTri2D(const dlong Nelements,
                            @restrict const  dlong  *  elementIds,
                            @restrict const  dfloat *  vgeo,
                            @restrict const  dfloat *  Dmatrices,
                            @restrict const  dfloat * x,
                            @restrict const  dfloat * y,
                            @restrict const  dfloat * z,
                            const dfloat t,
                            const dfloat c,
                            const dfloat nu,
                            @restrict const  dfloat *  q,
                            @restrict dfloat *  rhsq){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){  // for all elements

    @shared dfloat s_q[p_NblockV][p_Nfields][p_Np];
    @exclusive dlong e;

    @exclusive dfloat fx[p_NblockV], fy[p_NblockV];

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element
        const dlong et = eo+es; // element in block
        if(et<Nelements){
          e = elementIds[et];
          const dlong id = e*p_Nfields*p_Np + n;

          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields;++fld){
            s_q[es][fld][n] = q[id+fld*p_Np];
          }

          const dfloat r =   s_q[es][0][n];
          const dfloat u = c*s_q[es][1][n]/r;
          const dfloat v = c*s_q[es][2][n]/r;

          //Body force contribution
          fx[es] = 0.0; fy[es] = 0.0;
          bnsBodyForce2D(c, nu, t, x[e*p_Np+n], y[e*p_Np+n], r, u, v,
                         &(fx[es]), &(fy[es]));
        }
      }
    }

    // make sure all node data is loaded into @shared
    @barrier("local");

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element
        const dlong et = eo+es; // element in block
        if(et<Nelements){
          // prefetch geometric factors (constant on triangle)
          const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
          const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
          const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
          const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];

          // compute 'r' and 's' derivatives of (q_m) at node n
          dfloat r_dqdr[p_Nfields], r_dqds[p_Nfields];
          dfloat r_dqdx[p_Nfields], r_dqdy[p_Nfields];
          dfloat r_rhsq[p_Nfields];

          #pragma unroll p_Nfields
            for(int fld=0; fld<p_Nfields;++fld){
              r_dqdr[fld] = 0.f, r_dqds[fld] = 0.f;
              r_dqdx[fld] = 0.f, r_dqdy[fld] = 0.f;
              r_rhsq[fld] = 0.f;
            }

          #pragma unroll p_Np
            for(int i=0;i<p_Np;++i){
              const dfloat Drni = Dmatrices[n+i*p_Np+0*p_Np*p_Np];
              const dfloat Dsni = Dmatrices[n+i*p_Np+1*p_Np*p_Np];
              #pragma unroll p_Nfields
              for(int fld=0; fld<p_Nfields;++fld){
                r_dqdr[fld] += Drni*s_q[es][fld][i];
                r_dqds[fld] += Dsni*s_q[es][fld][i];
              }
            }

          // Compute derivatives in physical coordinates
          #pragma unroll p_Nfields
            for(int fld=0; fld<p_Nfields;++fld){
              r_dqdx[fld] = drdx*r_dqdr[fld] + dsdx*r_dqds[fld];
              r_dqdy[fld] = drdy*r_dqdr[fld] + dsdy*r_dqds[fld];
            }

          // transport operator
          r_rhsq[0] = -c*(r_dqdx[1] + r_dqdy[2]);
          r_rhsq[1] = -c*(r_dqdx[0] + sqrt(2.0)*r_dqdx[4] + r_dqdy[3]);
          r_rhsq[2] = -c*(r_dqdx[3] + r_dqdy[0] + sqrt(2.0)*r_dqdy[5]);
          r_rhsq[3] = -c*(r_dqdx[2] + r_dqdy[1]);
          r_rhsq[4] = -c*sqrt(2.0)*r_dqdx[1];
          r_rhsq[5] = -c*sqrt(2.0)*r_dqdy[2];

          // add body forcing
          r_rhsq[1] += fx[es]*s_q[es][0][n]/c;
          r_rhsq[2] += fy[es]*s_q[es][0][n]/c;
          r_rhsq[3] += fx[es]*s_q[es][2][n]/c + fy[es]*s_q[es][1][n]/c;
          r_rhsq[4] += fx[es]*sqrt(2.0)*s_q[es][1][n]/c;
          r_rhsq[5] += fy[es]*sqrt(2.0)*s_q[es][2][n]/c;

          // Update
          const dlong id = e*p_Nfields*p_Np + n;

          for(int fld=0; fld<p_Nfields;++fld){
            rhsq[id + fld*p_Np] = r_rhsq[fld];
          }
        }
      }
    }
  }
}

@kernel void bnsPmlVolumeCubTri2D(const dlong pmlNelements,
                                  @restrict const  dlong *  pmlElementIds,
                                  @restrict const  dlong *  pmlIds,
                                  @restrict const  dfloat *  vgeo,
                                  @restrict const  dfloat *  Dmatrices,
                                  @restrict const  dfloat *  x,
                                  @restrict const  dfloat *  y,
                                  @restrict const  dfloat *  z,
                                  const dfloat t,
                                  const dfloat c,
                                  const dfloat nu,
                                  @restrict const  dfloat *  q,
                                  @restrict dfloat *  rhsq,
                                  @restrict dfloat *  rhspmlq){

  for(dlong eo=0;eo<pmlNelements;eo+=p_NblockV;@outer(0)){  // for all elements

    @shared dfloat s_q[p_NblockV][p_Nfields][p_Np];
    @exclusive dlong e;

    @exclusive dfloat fx[p_NblockV], fy[p_NblockV];

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element

        const dlong et = eo+es; // element in block
        if(et<pmlNelements){
          e     = pmlElementIds[et];

          const dlong id = e*p_Nfields*p_Np + n;
          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields;++fld){
            s_q[es][fld][n] = q[id+fld*p_Np];
          }

          const dfloat r =   s_q[es][0][n];
          const dfloat u = c*s_q[es][1][n]/r;
          const dfloat v = c*s_q[es][2][n]/r;

          //Body force contribution
          fx[es] = 0.0; fy[es] = 0.0;
          bnsBodyForce2D(c, nu, t, x[e*p_Np+n], y[e*p_Np+n], r, u, v,
                         &(fx[es]), &(fy[es]));
        }
      }
    }

    // make sure all node data is loaded into @shared
    @barrier("local");

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element
        const dlong et = eo+es; // element in block
        if(et<pmlNelements){

          // prefetch geometric factors
          const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
          const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
          const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
          const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];

          // Pack register variables into arrays
          dfloat r_dqdr[p_Nfields],  r_dqds[p_Nfields];
          dfloat r_dqdx[p_Nfields],  r_dqdy[p_Nfields];
          dfloat r_Aqx[p_Nfields],   r_Bqy[p_Nfields];
          dfloat r_f[p_Nfields];

          // Initialize
          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields; ++fld){
            r_dqdr[fld]  = 0.f, r_dqds[fld]  = 0.f;
            r_dqdx[fld]  = 0.f, r_dqdy[fld]  = 0.f;
            r_f[fld]     = 0.f;
          }

          // Compute Derivatives
          #pragma unroll p_Np
          for(int i=0;i<p_Np;++i){
            const dfloat Drni = Dmatrices[n+i*p_Np+0*p_Np*p_Np];
            const dfloat Dsni = Dmatrices[n+i*p_Np+1*p_Np*p_Np];
            #pragma unroll p_Nfields
            for(int fld=0; fld<p_Nfields; ++fld){
              r_dqdr[fld] += Drni*s_q[es][fld][i];
              r_dqds[fld] += Dsni*s_q[es][fld][i];
            }
          }

          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields; ++fld){
            r_dqdx[fld] = drdx*r_dqdr[fld] + dsdx*r_dqds[fld];
            r_dqdy[fld] = drdy*r_dqdr[fld] + dsdy*r_dqds[fld];
          }

          // Compute volume flux for q, qx and qy
          // A*dQ/dx
          r_Aqx[0] = -c*(r_dqdx[1]);
          r_Aqx[1] = -c*(r_dqdx[0] + sqrt(2.0)*r_dqdx[4]);
          r_Aqx[2] = -c*(r_dqdx[3]);
          r_Aqx[3] = -c*(r_dqdx[2]);
          r_Aqx[4] = -c*sqrt(2.0)*r_dqdx[1];
          r_Aqx[5] = -0.f;

          // B*dQ/dy
          r_Bqy[0] = -c*(r_dqdy[2]);
          r_Bqy[1] = -c*(r_dqdy[3]);
          r_Bqy[2] = -c*(r_dqdy[0] + sqrt(2.0)*r_dqdy[5]);
          r_Bqy[3] = -c*(r_dqdy[1]);
          r_Bqy[4] = -0.f;
          r_Bqy[5] = -c*sqrt(2.0)*r_dqdy[2];

          // body forcing
          r_f[1] = fx[es]*s_q[es][0][n]/c;
          r_f[2] = fy[es]*s_q[es][0][n]/c;
          r_f[3] = fx[es]*s_q[es][2][n]/c + fy[es]*s_q[es][1][n]/c;
          r_f[4] = fx[es]*sqrt(2.0)*s_q[es][1][n]/c;
          r_f[5] = fy[es]*sqrt(2.0)*s_q[es][2][n]/c;

          const dlong pmlId = pmlIds[et];
          const dlong id = e*p_Nfields*p_Np + n;
          const dlong pmlrhsId = pmlId*p_Npmlfields*p_Np + n;

          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields; ++fld){
            rhspmlq[pmlrhsId + 0*p_Nfields*p_Np + fld*p_Np] =  r_Aqx[fld];
            rhspmlq[pmlrhsId + 1*p_Nfields*p_Np + fld*p_Np] =  r_Bqy[fld];
            rhsq[id +fld*p_Np] =  r_Aqx[fld] + r_Bqy[fld] + r_f[fld];
          }
        }
      }
    }
  }
}

@kernel void bnsPmlVolumeTri2D(const dlong pmlNelements,
                              @restrict const  dlong *  pmlElementIds,
                              @restrict const  dlong *  pmlIds,
                              @restrict const  dfloat *  vgeo,
                              @restrict const  dfloat *  Dmatrices,
                              @restrict const  dfloat *  x,
                              @restrict const  dfloat *  y,
                              @restrict const  dfloat *  z,
                              @restrict const  dfloat *  pmlSigma,
                              const dfloat pmlAlpha,
                              const dfloat t,
                              const dfloat c,
                              const dfloat nu,
                              @restrict const  dfloat *  q,
                              @restrict const  dfloat *  pmlq,
                              @restrict dfloat *  rhsq,
                              @restrict dfloat *  rhspmlq){

  for(dlong eo=0;eo<pmlNelements;eo+=p_NblockV;@outer(0)){  // for all elements

    @shared dfloat s_q[p_NblockV][p_Nfields][p_Np];
    @exclusive dlong e;

    @exclusive dfloat fx[p_NblockV], fy[p_NblockV];

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element

        const dlong et = eo+es; // element in block
        if(et<pmlNelements){
          e = pmlElementIds[et];
          const dlong id = e*p_Nfields*p_Np + n;

          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields;++fld){
            s_q[es][fld][n] = q[id+fld*p_Np];
          }

          const dfloat r =   s_q[es][0][n];
          const dfloat u = c*s_q[es][1][n]/r;
          const dfloat v = c*s_q[es][2][n]/r;

          //Body force contribution
          fx[es] = 0.0; fy[es] = 0.0;
          bnsBodyForce2D(c, nu, t, x[e*p_Np+n], y[e*p_Np+n], r, u, v,
                         &(fx[es]), &(fy[es]));
        }
      }
    }

    // make sure all node data is loaded into @shared
    @barrier("local");

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element
        const dlong et = eo+es; // element in block
        if(et<pmlNelements){

          // Pack register variables into arrays
          dfloat r_dqdr[p_Nfields],  r_dqds[p_Nfields];
          dfloat r_dqdx[p_Nfields],  r_dqdy[p_Nfields];
          dfloat r_Aqx[p_Nfields],   r_Bqy[p_Nfields];
          dfloat r_f[p_Nfields];

          // prefetch geometric factors
          const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
          const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
          const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
          const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
          // PML terms
          const dlong pmlId = pmlIds[et];
          const dfloat sigmaxe = pmlSigma[2*pmlId*p_Np+0*p_Np+n];
          const dfloat sigmaye = pmlSigma[2*pmlId*p_Np+1*p_Np+n];
          // M-PML sigma modification
          const dfloat msigmaxe = sigmaxe + sigmaye*pmlAlpha;
          const dfloat msigmaye = sigmaye + sigmaxe*pmlAlpha;

          const dlong base    = e *p_Nfields*p_Np + n;
          const dlong pmlbase = pmlId*p_Npmlfields*p_Np + n;

           for(int fld = 0; fld<p_Nfields; fld++){
            r_Aqx[fld] = -msigmaxe*pmlq[pmlbase + 0*p_Nfields*p_Np + fld*p_Np];
            r_Bqy[fld] = -msigmaye*pmlq[pmlbase + 1*p_Nfields*p_Np + fld*p_Np];
          }

          // Initialize
          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields; ++fld){
            r_dqdr[fld]  = 0.f, r_dqds[fld]  = 0.f;
            r_dqdx[fld]  = 0.f, r_dqdy[fld]  = 0.f;
            r_f[fld]     = 0.f;
          }

          // Compute Derivatives
          #pragma unroll p_Np
          for(int i=0;i<p_Np;++i){
            const dfloat Drni = Dmatrices[n+i*p_Np+0*p_Np*p_Np];
            const dfloat Dsni = Dmatrices[n+i*p_Np+1*p_Np*p_Np];
            #pragma unroll p_Nfields
            for(int fld=0; fld<p_Nfields; ++fld){
              r_dqdr[fld] += Drni*s_q[es][fld][i];
              r_dqds[fld] += Dsni*s_q[es][fld][i];
            }
          }

          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields; ++fld){
            r_dqdx[fld] = drdx*r_dqdr[fld] + dsdx*r_dqds[fld];
            r_dqdy[fld] = drdy*r_dqdr[fld] + dsdy*r_dqds[fld];
          }

          // Compute volume flux for q, qx and qy
          // A*dQ/dx
          r_Aqx[0] -= c*(r_dqdx[1]);
          r_Aqx[1] -= c*(r_dqdx[0] + sqrt(2.0)*r_dqdx[4]);
          r_Aqx[2] -= c*(r_dqdx[3]);
          r_Aqx[3] -= c*(r_dqdx[2]);
          r_Aqx[4] -= c*sqrt(2.0)*r_dqdx[1];
          r_Aqx[5] -= 0.f;

          // B*dQ/dy
          r_Bqy[0] -= c*(r_dqdy[2]);
          r_Bqy[1] -= c*(r_dqdy[3]);
          r_Bqy[2] -= c*(r_dqdy[0] + sqrt(2.0)*r_dqdy[5]);
          r_Bqy[3] -= c*(r_dqdy[1]);
          r_Bqy[4] -= 0.f;
          r_Bqy[5] -= c*sqrt(2.0)*r_dqdy[2];

          // body forcing
          r_f[1] += fx[es]*s_q[es][0][n]/c;
          r_f[2] += fy[es]*s_q[es][0][n]/c;
          r_f[3] += fx[es]*s_q[es][2][n]/c + fy[es]*s_q[es][1][n]/c;
          r_f[4] += fx[es]*sqrt(2.0)*s_q[es][1][n]/c;
          r_f[5] += fy[es]*sqrt(2.0)*s_q[es][2][n]/c;

          #pragma unroll p_Nfields
          for(int fld=0; fld<p_Nfields; ++fld){
            rhspmlq[pmlbase + 0*p_Nfields*p_Np + fld*p_Np] =  r_Aqx[fld];
            rhspmlq[pmlbase + 1*p_Nfields*p_Np + fld*p_Np] =  r_Bqy[fld];
            rhsq[base +fld*p_Np] = r_Aqx[fld] + r_Bqy[fld] + r_f[fld];
          }
        }
      }
    }
  }
}