#define p_isq12 (1.f/sqrt(12.f))
#define p_isq6 (1.f/sqrt(6.f))
#define PML_ENABLED 1 

void upwind1D(const dfloat nx,  const dfloat ny,  const dfloat nz,
              const dfloat q1M, const dfloat q2M, const dfloat q3M, const dfloat q4M, const dfloat q5M,
              const dfloat q6M, const dfloat q7M, const dfloat q8M, const dfloat q9M, const dfloat q10M,
              const dfloat q1B, const dfloat q2B, const dfloat q3B, const dfloat q4B, const dfloat q5B, 
              const dfloat q6B, const dfloat q7B, const dfloat q8B, const dfloat q9B, const dfloat q10B, 
              @restrict @restrict dfloat * q1P, @restrict dfloat * q2P, @restrict dfloat * q3P, @restrict dfloat * q4P,  dfloat * q5P, 
              @restrict @restrict dfloat * q6P, @restrict dfloat * q7P, @restrict dfloat * q8P, @restrict dfloat * q9P,  dfloat * q10P){ 


  *q1P  = 0.5f*(q1B  + q1M)  - p_isq12*(nx*(q2B-q2M) + ny*(q3B-q3M) + nz*(q4B-q4M));
  *q2P  = 0.5f*(q2B  + q2M)  - p_isq12*nx*(q1B-q1M) - p_isq6*nx*(q8B-q8M) - 0.5f*ny*(q5B-q5M) - 0.5f*nz*(q6B-q6M);
  *q3P  = 0.5f*(q3B  + q3M)  - 0.5f*nx*(q5B-q5M) - p_isq12*ny*(q1B-q1M) - p_isq6*ny*(q9B-q9M) - 0.5f*nz*(q7B-q7M);
  *q4P  = 0.5f*(q4B  + q4M)  - 0.5f*nx*(q6B-q6M) - 0.5f*ny*(q7B-q7M) - p_isq6*nz*(q10B-q10M) - p_isq12*nz*(q1B-q1M);
  *q5P  = 0.5f*(q5B  + q5M)  - 0.5f*nx*(q3B-q3M) - 0.5f*ny*(q2B-q2M);
  *q6P  = 0.5f*(q6B  + q6M)  - 0.5f*nx*(q4B-q4M) - 0.5f*nz*(q2B-q2M);
  *q7P  = 0.5f*(q7B  + q7M)  - 0.5f*ny*(q4B-q4M) - 0.5f*nz*(q3B-q3M);
  *q8P  = 0.5f*(q8B  + q8M)  - p_isq6*nx*(q2B-q2M);
  *q9P  = 0.5f*(q9B  + q9M)  - p_isq6*ny*(q3B-q3M);
  *q10P = 0.5f*(q10B + q10M) - p_isq6*nz*(q4B-q4M);  
}

// This @kernel uses Upwind flux 
@kernel void bnsSurfaceTet3D(const dlong Nelements,
			    @restrict const  dlong *  elementIds,
			    const dfloat time,
			    const dfloat intfx,
			    const dfloat intfy,
			    const dfloat intfz,
			    @restrict const  dfloat *  sgeo,
			    @restrict const  dfloat *  LIFTT,
			    @restrict const  dlong   *  vmapM,
			    @restrict const  dlong   *  vmapP,
			    @restrict const  int   *  EToB,
			    @restrict const  dfloat *  x,
			    @restrict const  dfloat *  y,
			    @restrict const  dfloat *  z,
			    @restrict const  dfloat *  q,
			    @restrict dfloat *  rhsq){
  
  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){

    // @shared storage for flux terms
    @shared dfloat s_fluxq[p_NblockS][p_Nfields][p_Nfp*p_Nfaces];
    @exclusive dlong e;

    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong et = eo + es;
        if(et<Nelements){
	  e = elementIds[et];
          if(n<p_Nfp*p_Nfaces){
            // find face that owns this node
            int face = n/p_Nfp;          
            
            // load surface geofactors for this face
            const dlong sid   = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat nz   = sgeo[sid+p_NZID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];   

            const dlong id  = e*p_Nfp*p_Nfaces + n;
            const dlong idM = vmapM[id];
            const dlong idP = vmapP[id];

            // load traces
            const dlong eM = e;
            const dlong eP = idP/p_Np;
            const int vidM = idM%p_Np;
            const int vidP = idP%p_Np;
            //
            const dlong qidM = eM*p_Np*p_Nfields + vidM;
            const dlong qidP = eP*p_Np*p_Nfields + vidP;


            // Read trace values
            dfloat q1M  = q[qidM + 0*p_Np], q1P = q[qidP  + 0*p_Np];
            dfloat q2M  = q[qidM + 1*p_Np], q2P = q[qidP  + 1*p_Np];
            dfloat q3M  = q[qidM + 2*p_Np], q3P = q[qidP  + 2*p_Np];
            dfloat q4M  = q[qidM + 3*p_Np], q4P = q[qidP  + 3*p_Np];
            dfloat q5M  = q[qidM + 4*p_Np], q5P = q[qidP  + 4*p_Np];
            dfloat q6M  = q[qidM + 5*p_Np], q6P = q[qidP  + 5*p_Np];
            dfloat q7M  = q[qidM + 6*p_Np], q7P = q[qidP  + 6*p_Np];
            dfloat q8M  = q[qidM + 7*p_Np], q8P = q[qidP  + 7*p_Np];
            dfloat q9M  = q[qidM + 8*p_Np], q9P = q[qidP  + 8*p_Np];
            dfloat q10M = q[qidM + 9*p_Np], q10P = q[qidP + 9*p_Np];
                      
	    // apply boundary condition
	    const int bc = EToB[face+p_Nfaces*e];
	    if(bc>0){
              const dlong idB = vmapM[id];
              if(p_PML){
                boundaryConditionsPML3D(bc, time, x[idB], y[idB], z[idB], nx, ny, nz, intfx, intfy, intfz, q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, &q1P, &q2P, &q3P, &q4P, &q5P, &q6P, &q7P, &q8P, &q9P, &q10P);
	      }
	      else{
		boundaryConditions3D(bc, time, x[idB], y[idB], z[idB], nx, ny, nz, intfx, intfy, intfz, q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, &q1P, &q2P, &q3P, &q4P, &q5P, &q6P, &q7P, &q8P, &q9P, &q10P);
	      }
	    }
      
	    // compute (q^*)
	    dfloat q1U, q2U, q3U, q4U, q5U, q6U, q7U, q8U, q9U, q10U;               

	    upwind1D(nx, ny, nz, 
		     q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, 
		     q1P, q2P, q3P, q4P, q5P, q6P, q7P, q8P, q9P, q10P, 
		     &q1U, &q2U, &q3U, &q4U, &q5U, &q6U, &q7U, &q8U, &q9U, &q10U);

	    // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny)*(q^* - q^-)
	    const dfloat sc = invJ*sJ;

	    dfloat dq1, dq2, dq3, dq4, dq5, dq6, dq7, dq8, dq9, dq10;
	    //        
	    dq1  = q1U - q1M;
	    dq2  = q2U - q2M;
	    dq3  = q3U - q3M;
	    dq4  = q4U - q4M;
	    dq5  = q5U - q5M;
	    dq6  = q6U - q6M;
	    dq7  = q7U - q7M;
	    dq8  = q8U - q8M;
	    dq9  = q9U - q9M;
	    dq10 = q10U- q10M;
	    // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny+C*nz)*(q^* - q^-)
	    s_fluxq[es][0][n] = -sc*p_sqrtRT*(nx*dq2 + ny*dq3 + nz*dq4);
	    s_fluxq[es][1][n] = -sc*p_sqrtRT*(nx*(dq1+p_sqrt2*dq8) + ny*dq5 + nz*dq6);
	    s_fluxq[es][2][n] = -sc*p_sqrtRT*(nx*dq5 + ny*(dq1+p_sqrt2*dq9) + nz*dq7);
	    s_fluxq[es][3][n] = -sc*p_sqrtRT*(nx*dq6 + ny*dq7 + nz*(dq1+p_sqrt2*dq10));
	    s_fluxq[es][4][n] = -sc*p_sqrtRT*(nx*dq3 + ny*dq2);
	    s_fluxq[es][5][n] = -sc*p_sqrtRT*(nx*dq4 + nz*dq2);
	    s_fluxq[es][6][n] = -sc*p_sqrtRT*(ny*dq4 + nz*dq3);
	    s_fluxq[es][7][n] = -sc*p_sqrtRT*p_sqrt2*(nx*dq2);
	    s_fluxq[es][8][n] = -sc*p_sqrtRT*p_sqrt2*(ny*dq3);
	    s_fluxq[es][9][n] = -sc*p_sqrtRT*p_sqrt2*(nz*dq4);

	  }
	}
      }
    }
    
    // wait for all @shared memory writes of the previous inner loop to complete
    @barrier("local");

    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong et = eo + es;
        if(et<Nelements){
          if(n<p_Np){

            const dlong id = e*p_Nfields*p_Np + n ;

            dfloat r_rhsq[p_Nfields];
            #pragma unroll p_Nfields
	      for(int fld=0; fld<p_Nfields; fld++){
		r_rhsq[fld] = 0.f; 
	      }

	    // rhs += LIFT*((sJ/J)*(A*nx+B*ny)*(q^* - q^-))
            #pragma unroll p_NfacesNfp
              for(int m=0;m<p_NfacesNfp;++m){
                dfloat L = LIFTT[n+m*p_Np];
                #pragma unroll p_Nfields
		  for(int fld=0; fld<p_Nfields; fld++){
		    r_rhsq[fld] +=L*s_fluxq[es][fld][m]; 
		  }
              }
          
	    #pragma unroll p_Nfields
	      for(int fld=0; fld<p_Nfields; fld++){
		rhsq[id + fld*p_Np] += r_rhsq[fld];
	      }
	  }
        }
      }
    }
  }
}




// This @kernel uses upwind flux
@kernel void bnsPmlSurfaceTet3D(const dlong pmlNelements,
			       @restrict const  dlong   *  pmlElementIds,
			       @restrict const  dlong   *  pmlIds,
			       const dfloat time,
			       const dfloat intfx,
			       const dfloat intfy,
			       const dfloat intfz,
			       @restrict const  dfloat *  sgeo,
			       @restrict const  dfloat *  LIFTT,
			       @restrict const  dlong  *  vmapM,
			       @restrict const  dlong  *  vmapP,
			       @restrict const  int   *  EToB,
			       @restrict const  dfloat *  x,
			       @restrict const  dfloat *  y,              
			       @restrict const  dfloat *  z,              
			       @restrict const  dfloat *  q,
			       @restrict dfloat *  rhsq,
			       @restrict dfloat *  pmlrhsqx,
			       @restrict dfloat *  pmlrhsqy,
			       @restrict dfloat *  pmlrhsqz){
  
  // for all elements
  for(dlong eo=0;eo<pmlNelements;eo+=p_NblockS;@outer(0)){
    // @shared storage for flux terms
    @shared dfloat s_nxdq[p_NblockS][p_Nfields][p_maxNodes];
    @shared dfloat s_nydq[p_NblockS][p_Nfields][p_maxNodes];
    @shared dfloat s_nzdq[p_NblockS][p_Nfields][p_maxNodes];
   
    @exclusive dlong e;

    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong et = eo + es;
        if(et<pmlNelements){
	  e = pmlElementIds[et];
	  if(n<p_NfacesNfp){
	    // find face that owns this node
	    int face = n/p_Nfp;          
	    // load surface geofactors for this face
	    const dlong sid   = p_Nsgeo*(e*p_Nfaces+face);
	    const dfloat nx   = sgeo[sid+p_NXID];
	    const dfloat ny   = sgeo[sid+p_NYID];
	    const dfloat nz   = sgeo[sid+p_NZID];
	    const dfloat sJ   = sgeo[sid+p_SJID];
	    const dfloat invJ = sgeo[sid+p_IJID];   

	    const dlong id  = e*p_Nfp*p_Nfaces + n;
	    const dlong idM = vmapM[id];
	    const dlong idP = vmapP[id];

	    const dlong eM = e;
	    const dlong eP = idP/p_Np; 
	    const int vidM = idM%p_Np;
	    const int vidP = idP%p_Np; 

	    const dlong qidM = eM*p_Nfields*p_Np + vidM;
	    const dlong qidP = eP*p_Nfields*p_Np + vidP;
	    // Read trace values
	    dfloat q1M  = q[qidM + 0*p_Np], q1P  = q[qidP + 0*p_Np];
	    dfloat q2M  = q[qidM + 1*p_Np], q2P  = q[qidP + 1*p_Np];
	    dfloat q3M  = q[qidM + 2*p_Np], q3P  = q[qidP + 2*p_Np];
	    dfloat q4M  = q[qidM + 3*p_Np], q4P  = q[qidP + 3*p_Np];
	    dfloat q5M  = q[qidM + 4*p_Np], q5P  = q[qidP + 4*p_Np];
	    dfloat q6M  = q[qidM + 5*p_Np], q6P  = q[qidP + 5*p_Np];              
	    dfloat q7M  = q[qidM + 6*p_Np], q7P  = q[qidP + 6*p_Np];              
	    dfloat q8M  = q[qidM + 7*p_Np], q8P  = q[qidP + 7*p_Np];              
	    dfloat q9M  = q[qidM + 8*p_Np], q9P  = q[qidP + 8*p_Np];              
	    dfloat q10M = q[qidM + 9*p_Np], q10P = q[qidP + 9*p_Np];              
            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0){
	      const dlong idB = vmapM[id];
	      boundaryConditionsPML3D(bc, time, x[idB], y[idB], z[idB], nx, ny, nz, intfx, intfy, intfz,q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, &q1P, &q2P, &q3P, &q4P, &q5P, &q6P, &q7P, &q8P, &q9P, &q10P);
	    }
	    
	    //  // compute (q^*)
            dfloat q1U, q2U, q3U, q4U, q5U, q6U, q7U, q8U, q9U, q10U;       

	    upwind1D(nx, ny, nz, 
		     q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, 
		     q1P, q2P, q3P, q4P, q5P, q6P, q7P, q8P, q9P, q10P, 
		     &q1U, &q2U, &q3U, &q4U, &q5U, &q6U, &q7U, &q8U, &q9U, &q10U);

	    // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny)*(q^* - q^-) 
               
            dfloat r_dq[p_Nfields];      
            r_dq[0] = q1U - q1M;
            r_dq[1] = q2U - q2M;
            r_dq[2] = q3U - q3M;
            r_dq[3] = q4U - q4M;
            r_dq[4] = q5U - q5M;
            r_dq[5] = q6U - q6M;
            r_dq[6] = q7U - q7M;
            r_dq[7] = q8U - q8M;
            r_dq[8] = q9U - q9M;
            r_dq[9] = q10U- q10M;

            // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny)*(q^* - q^-)
            const dfloat sc = invJ*sJ;

            for(int fld=0; fld<p_Nfields; fld++){
              s_nxdq[es][fld][n] = sc*nx*r_dq[fld];
              s_nydq[es][fld][n] = sc*ny*r_dq[fld];
              s_nzdq[es][fld][n] = sc*nz*r_dq[fld];
            }            
          }
        }
      }
    }
    
    // wait for all @shared memory writes of the previous inner loop to complete
    @barrier("local");

    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong et = eo + es;
        if(et<pmlNelements){
	  if(n<p_Np){
	    // load rhs data from volume fluxes

	    dfloat r_lnxdq[p_Nfields], r_lnydq[p_Nfields], r_lnzdq[p_Nfields];

	    #pragma unroll p_Nfields
              for(int fld=0; fld<p_Nfields; fld++){
                r_lnxdq[fld] = 0.f; 
                r_lnydq[fld] = 0.f; 
                r_lnzdq[fld] = 0.f; 
              }
             
            // rhs += LIFT*((sJ/J)*(A*nx+B*ny)*(q^* - q^-))
            #pragma unroll p_NfacesNfp
	      for(int m=0;m<p_NfacesNfp;++m){
		const dfloat L = LIFTT[n+m*p_Np];
		#pragma unroll p_Nfields
		  for(int fld=0; fld<p_Nfields; fld++){
		    r_lnxdq[fld] += L*s_nxdq[es][fld][m];
		    r_lnydq[fld] += L*s_nydq[es][fld][m];
		    r_lnzdq[fld] += L*s_nzdq[es][fld][m];
		  }
	      }

            const dlong pmlId    = pmlIds[et];
            const dlong rhsId    = e*p_Nfields*p_Np + n    ;
            const dlong pmlrhsId = pmlId*p_Nfields*p_Np + n;
          
            dfloat r_Aqx[p_Nfields],r_Bqy[p_Nfields],r_Cqz[p_Nfields]; 
            #pragma unroll p_Nfields
	      for(int fld=0; fld<p_Nfields; fld++){
		r_Aqx[fld] = 0.f; 
		r_Bqy[fld] = 0.f;  
		r_Cqz[fld] = 0.f; 
	      }

            r_Aqx[0]  = -p_sqrtRT*(r_lnxdq[1]);
            r_Aqx[1]  = -p_sqrtRT*(r_lnxdq[0]+p_sqrt2*r_lnxdq[7]);
            r_Aqx[2]  = -p_sqrtRT*(r_lnxdq[4]);
            r_Aqx[3]  = -p_sqrtRT*(r_lnxdq[5]);
            r_Aqx[4]  = -p_sqrtRT*(r_lnxdq[2]);
            r_Aqx[5]  = -p_sqrtRT*(r_lnxdq[3]);
            r_Aqx[7]  = -p_sqrtRT*p_sqrt2*(r_lnxdq[1]);

            r_Bqy[0]  = -p_sqrtRT*(r_lnydq[2]);
            r_Bqy[1]  = -p_sqrtRT*(r_lnydq[4]);  
            r_Bqy[2]  = -p_sqrtRT*(r_lnydq[0] + p_sqrt2*r_lnydq[8]);  
            r_Bqy[3]  = -p_sqrtRT*(r_lnydq[6]);
            r_Bqy[4]  = -p_sqrtRT*(r_lnydq[1]);  
            r_Bqy[6]  = -p_sqrtRT*(r_lnydq[3]);  
            r_Bqy[8]  = -p_sqrtRT*(p_sqrt2*r_lnydq[2]);

            r_Cqz[0]  = -p_sqrtRT*(r_lnzdq[3]);
            r_Cqz[1]  = -p_sqrtRT*(r_lnzdq[5]); 
            r_Cqz[2]  = -p_sqrtRT*(r_lnzdq[6]); 
            r_Cqz[3]  = -p_sqrtRT*(r_lnzdq[0] + p_sqrt2*r_lnzdq[9]);
            r_Cqz[5]  = -p_sqrtRT*(r_lnzdq[1]);
            r_Cqz[6]  = -p_sqrtRT*(r_lnzdq[2]);
            r_Cqz[9]  = -p_sqrtRT*(p_sqrt2*r_lnzdq[3]);

            #pragma unroll p_Nfields
	      for(int fld=0; fld<p_Nfields; fld++){
		pmlrhsqx[pmlrhsId + fld*p_Np] += r_Aqx[fld];
		pmlrhsqy[pmlrhsId + fld*p_Np] += r_Bqy[fld];
		pmlrhsqz[pmlrhsId + fld*p_Np] += r_Cqz[fld];
		rhsq[rhsId+fld*p_Np]          += (r_Aqx[fld] + r_Bqy[fld] + r_Cqz[fld]); 
	      }

        
	  }
	}
      }
    }
  }
}



// This @kernel uses Upwind flux 
@kernel void bnsMRSurfaceTet3D(const dlong Nelements,
			      @restrict const  dlong *  elementIds,
			      const dlong offset,
			      const int shift,
			      const dfloat time,
			      const dfloat intfx,
			      const dfloat intfy,
			      const dfloat intfz,
			      @restrict const  dfloat *  sgeo,
			      @restrict const  dfloat *  LIFTT,
			      @restrict const  dlong   *  vmapM,
			      @restrict const  dlong   *  mapP,
			      @restrict const  int   *  EToB,
			      @restrict const  dfloat *  x,
			      @restrict const  dfloat *  y,
			      @restrict const  dfloat *  z,
			      @restrict const  dfloat *  q,
			      @restrict const  dfloat *  fQM,
			      @restrict dfloat *  rhsq){
  
  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){

    // @shared storage for flux terms
    @shared dfloat s_fluxq[p_NblockS][p_Nfields][p_Nfp*p_Nfaces];
    @exclusive dlong e;

    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong et = eo + es;
        if(et<Nelements){
	  e = elementIds[et];
          if(n<p_Nfp*p_Nfaces){
            // find face that owns this node
            int face = n/p_Nfp;          
            
            // load surface geofactors for this face
            const dlong sid   = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat nz   = sgeo[sid+p_NZID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];   

            const dlong id  = e*p_Nfp*p_Nfaces + n;
            const dlong idM = e*p_Nfp*p_Nfaces*p_Nfields + n;

            // extract + side index
            const dlong qidP = mapP[id];
            const dlong eP   = qidP/(p_Nfp*p_Nfaces); 
            const int fidP   = qidP%(p_Nfp*p_Nfaces); 
            const dlong idP  = eP*p_Nfp*p_Nfaces*p_Nfields + fidP;


            // Read trace values
            dfloat q1M  = fQM[idM + 0*p_Nfp*p_Nfaces], q1P = fQM[idP  + 0*p_Nfp*p_Nfaces];
            dfloat q2M  = fQM[idM + 1*p_Nfp*p_Nfaces], q2P = fQM[idP  + 1*p_Nfp*p_Nfaces];
            dfloat q3M  = fQM[idM + 2*p_Nfp*p_Nfaces], q3P = fQM[idP  + 2*p_Nfp*p_Nfaces];
            dfloat q4M  = fQM[idM + 3*p_Nfp*p_Nfaces], q4P = fQM[idP  + 3*p_Nfp*p_Nfaces];
            dfloat q5M  = fQM[idM + 4*p_Nfp*p_Nfaces], q5P = fQM[idP  + 4*p_Nfp*p_Nfaces];
            dfloat q6M  = fQM[idM + 5*p_Nfp*p_Nfaces], q6P = fQM[idP  + 5*p_Nfp*p_Nfaces];
            dfloat q7M  = fQM[idM + 6*p_Nfp*p_Nfaces], q7P = fQM[idP  + 6*p_Nfp*p_Nfaces];
            dfloat q8M  = fQM[idM + 7*p_Nfp*p_Nfaces], q8P = fQM[idP  + 7*p_Nfp*p_Nfaces];
            dfloat q9M  = fQM[idM + 8*p_Nfp*p_Nfaces], q9P = fQM[idP  + 8*p_Nfp*p_Nfaces];
            dfloat q10M = fQM[idM + 9*p_Nfp*p_Nfaces], q10P = fQM[idP + 9*p_Nfp*p_Nfaces];
                      
	    // apply boundary condition
	    const int bc = EToB[face+p_Nfaces*e];
	    if(bc>0){
              const dlong idB = vmapM[id];
              if(p_PML){
                boundaryConditionsPML3D(bc, time, x[idB], y[idB], z[idB], nx, ny, nz, intfx, intfy, intfz,q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, &q1P, &q2P, &q3P, &q4P, &q5P, &q6P, &q7P, &q8P, &q9P, &q10P);
	      }
	      else{
		boundaryConditions3D(bc, time, x[idB], y[idB], z[idB], nx, ny, nz, intfx, intfy, intfz,q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, &q1P, &q2P, &q3P, &q4P, &q5P, &q6P, &q7P, &q8P, &q9P, &q10P);
	      }
	    }
      
	    // compute (q^*)
	    dfloat q1U, q2U, q3U, q4U, q5U, q6U, q7U, q8U, q9U, q10U;               

	    upwind1D(nx, ny, nz, 
		     q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, 
		     q1P, q2P, q3P, q4P, q5P, q6P, q7P, q8P, q9P, q10P, 
		     &q1U, &q2U, &q3U, &q4U, &q5U, &q6U, &q7U, &q8U, &q9U, &q10U);

	    // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny)*(q^* - q^-)
	    const dfloat sc = invJ*sJ;

	    dfloat dq1, dq2, dq3, dq4, dq5, dq6, dq7, dq8, dq9, dq10;
	    //        
	    dq1  = q1U - q1M;
	    dq2  = q2U - q2M;
	    dq3  = q3U - q3M;
	    dq4  = q4U - q4M;
	    dq5  = q5U - q5M;
	    dq6  = q6U - q6M;
	    dq7  = q7U - q7M;
	    dq8  = q8U - q8M;
	    dq9  = q9U - q9M;
	    dq10 = q10U- q10M;

	    // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny+C*nz)*(q^* - q^-)
	    s_fluxq[es][0][n] = -sc*p_sqrtRT*(nx*dq2 + ny*dq3 + nz*dq4);
	    s_fluxq[es][1][n] = -sc*p_sqrtRT*(nx*(dq1+p_sqrt2*dq8) + ny*dq5 + nz*dq6);
	    s_fluxq[es][2][n] = -sc*p_sqrtRT*(nx*dq5 + ny*(dq1+p_sqrt2*dq9) + nz*dq7);
	    s_fluxq[es][3][n] = -sc*p_sqrtRT*(nx*dq6 + ny*dq7 + nz*(dq1+p_sqrt2*dq10));
	    s_fluxq[es][4][n] = -sc*p_sqrtRT*(nx*dq3 + ny*dq2);
	    s_fluxq[es][5][n] = -sc*p_sqrtRT*(nx*dq4 + nz*dq2);
	    s_fluxq[es][6][n] = -sc*p_sqrtRT*(ny*dq4 + nz*dq3);
	    s_fluxq[es][7][n] = -sc*p_sqrtRT*p_sqrt2*(nx*dq2);
	    s_fluxq[es][8][n] = -sc*p_sqrtRT*p_sqrt2*(ny*dq3);
	    s_fluxq[es][9][n] = -sc*p_sqrtRT*p_sqrt2*(nz*dq4);

	  }
	}
      }
    }
    
    // wait for all @shared memory writes of the previous inner loop to complete
    @barrier("local");

    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong et = eo + es;
        if(et<Nelements){
          if(n<p_Np){

            

            dfloat r_rhsq[p_Nfields];
            #pragma unroll p_Nfields
	      for(int fld=0; fld<p_Nfields; fld++){
		r_rhsq[fld] = 0.f; 
	      }

	    // rhs += LIFT*((sJ/J)*(A*nx+B*ny)*(q^* - q^-))
            #pragma unroll p_NfacesNfp
              for(int m=0;m<p_NfacesNfp;++m){
                dfloat L = LIFTT[n+m*p_Np];
                #pragma unroll p_Nfields
		  for(int fld=0; fld<p_Nfields; fld++){
		    r_rhsq[fld] +=L*s_fluxq[es][fld][m]; 
		  }
              }

	    const dlong id = e*p_Nfields*p_Np + n  + shift*offset;
          
	    #pragma unroll p_Nfields
	      for(int fld=0; fld<p_Nfields; fld++){
		rhsq[id + fld*p_Np] += r_rhsq[fld];
	      }
	  }
        }
      }
    }
  }
}




@kernel void bnsMRPmlSurfaceTet3D(const dlong pmlNelements,
				 @restrict const  dlong   *  pmlElementIds,
				 @restrict const  dlong   *  pmlIds,
				 const dlong offset,
				 const dlong pmloffset,
				 const int   shift,
				 const dfloat time,
				 const dfloat intfx,
				 const dfloat intfy,
				 const dfloat intfz,
				 @restrict const  dfloat *  sgeo,
				 @restrict const  dfloat *  LIFTT,
				 @restrict const  dlong  *  vmapM,
				 @restrict const  dlong  *  mapP,
				 @restrict const  int   *  EToB,
				 @restrict const  dfloat *  x,
				 @restrict const  dfloat *  y,              
				 @restrict const  dfloat *  z,              
				 @restrict const  dfloat *  q,
				 @restrict const  dfloat *  fQM,
				 @restrict dfloat *  rhsq,
				 @restrict dfloat *  pmlrhsqx,
				 @restrict dfloat *  pmlrhsqy,
				 @restrict dfloat *  pmlrhsqz){
  
  // for all elements
  for(dlong eo=0;eo<pmlNelements;eo+=p_NblockS;@outer(0)){
    // @shared storage for flux terms
    @shared dfloat s_nxdq[p_NblockS][p_Nfields][p_maxNodes];
    @shared dfloat s_nydq[p_NblockS][p_Nfields][p_maxNodes];
    @shared dfloat s_nzdq[p_NblockS][p_Nfields][p_maxNodes];
   
    @exclusive dlong e;

    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong et = eo + es;
        if(et<pmlNelements){
	  e = pmlElementIds[et];
	  if(n<p_NfacesNfp){
	    // find face that owns this node
	    int face = n/p_Nfp;          
	    // load surface geofactors for this face
	    const dlong sid   = p_Nsgeo*(e*p_Nfaces+face);
	    const dfloat nx   = sgeo[sid+p_NXID];
	    const dfloat ny   = sgeo[sid+p_NYID];
	    const dfloat nz   = sgeo[sid+p_NZID];
	    const dfloat sJ   = sgeo[sid+p_SJID];
	    const dfloat invJ = sgeo[sid+p_IJID];   

	    const dlong id  = e*p_Nfp*p_Nfaces + n;
	    const dlong idM = e*p_Nfp*p_Nfaces*p_Nfields + n;

	    // extract + side index
	    const dlong qidP = mapP[id];
	    const dlong eP   = qidP/(p_Nfp*p_Nfaces); 
	    const int fidP   = qidP%(p_Nfp*p_Nfaces); 
	    const dlong idP  = eP*p_Nfp*p_Nfaces*p_Nfields + fidP;


	    // Read trace values
	    dfloat q1M  = fQM[idM + 0*p_Nfp*p_Nfaces], q1P = fQM[idP + 0*p_Nfp*p_Nfaces];
	    dfloat q2M  = fQM[idM + 1*p_Nfp*p_Nfaces], q2P = fQM[idP + 1*p_Nfp*p_Nfaces];
	    dfloat q3M  = fQM[idM + 2*p_Nfp*p_Nfaces], q3P = fQM[idP + 2*p_Nfp*p_Nfaces];
	    dfloat q4M  = fQM[idM + 3*p_Nfp*p_Nfaces], q4P = fQM[idP + 3*p_Nfp*p_Nfaces];
	    dfloat q5M  = fQM[idM + 4*p_Nfp*p_Nfaces], q5P = fQM[idP + 4*p_Nfp*p_Nfaces];
	    dfloat q6M  = fQM[idM + 5*p_Nfp*p_Nfaces], q6P = fQM[idP + 5*p_Nfp*p_Nfaces];
	    dfloat q7M  = fQM[idM + 6*p_Nfp*p_Nfaces], q7P = fQM[idP + 6*p_Nfp*p_Nfaces];
	    dfloat q8M  = fQM[idM + 7*p_Nfp*p_Nfaces], q8P = fQM[idP + 7*p_Nfp*p_Nfaces];
	    dfloat q9M  = fQM[idM + 8*p_Nfp*p_Nfaces], q9P = fQM[idP + 8*p_Nfp*p_Nfaces];
	    dfloat q10M = fQM[idM + 9*p_Nfp*p_Nfaces], q10P= fQM[idP + 9*p_Nfp*p_Nfaces];   
            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0){
              const dlong idB = vmapM[id];
	      boundaryConditionsPML3D(bc, time, x[idB], y[idB], z[idB], nx, ny, nz, intfx, intfy, intfz, q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, &q1P, &q2P, &q3P, &q4P, &q5P, &q6P, &q7P, &q8P, &q9P, &q10P);
	    }
        
	    //  // compute (q^*)
            dfloat q1U, q2U, q3U, q4U, q5U, q6U, q7U, q8U, q9U, q10U;       

	    upwind1D(nx, ny, nz, 
		     q1M, q2M, q3M, q4M, q5M, q6M, q7M, q8M, q9M, q10M, 
		     q1P, q2P, q3P, q4P, q5P, q6P, q7P, q8P, q9P, q10P, 
		     &q1U, &q2U, &q3U, &q4U, &q5U, &q6U, &q7U, &q8U, &q9U, &q10U);

	    // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny)*(q^* - q^-) 
            dfloat r_dq[p_Nfields];      
            r_dq[0] = q1U - q1M;
            r_dq[1] = q2U - q2M;
            r_dq[2] = q3U - q3M;
            r_dq[3] = q4U - q4M;
            r_dq[4] = q5U - q5M;
            r_dq[5] = q6U - q6M;
            r_dq[6] = q7U - q7M;
            r_dq[7] = q8U - q8M;
            r_dq[8] = q9U - q9M;
            r_dq[9] = q10U- q10M;

            // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny)*(q^* - q^-)
            const dfloat sc = invJ*sJ;

            for(int fld=0; fld<p_Nfields; fld++){
              s_nxdq[es][fld][n] = sc*nx*r_dq[fld];
              s_nydq[es][fld][n] = sc*ny*r_dq[fld];
              s_nzdq[es][fld][n] = sc*nz*r_dq[fld];
            }            
          }
        }
      }
    }
    
    // wait for all @shared memory writes of the previous inner loop to complete
    @barrier("local");

    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong et = eo + es;
        if(et<pmlNelements){
	  if(n<p_Np){
	    // load rhs data from volume fluxes

	    dfloat r_lnxdq[p_Nfields], r_lnydq[p_Nfields], r_lnzdq[p_Nfields];

	    #pragma unroll p_Nfields
              for(int fld=0; fld<p_Nfields; fld++){
                r_lnxdq[fld] = 0.f; 
                r_lnydq[fld] = 0.f; 
                r_lnzdq[fld] = 0.f; 
              }
             
            // rhs += LIFT*((sJ/J)*(A*nx+B*ny)*(q^* - q^-))
            #pragma unroll p_NfacesNfp
	      for(int m=0;m<p_NfacesNfp;++m){
		const dfloat L = LIFTT[n+m*p_Np];
		#pragma unroll p_Nfields
		  for(int fld=0; fld<p_Nfields; fld++){
		    r_lnxdq[fld] += L*s_nxdq[es][fld][m];
		    r_lnydq[fld] += L*s_nydq[es][fld][m];
		    r_lnzdq[fld] += L*s_nzdq[es][fld][m];
		  }
	      }

            const dlong pmlId    = pmlIds[et];
            const dlong rhsId    = e*p_Nfields*p_Np + n     + shift*offset ;
            const dlong pmlrhsId = pmlId*p_Nfields*p_Np + n + shift*pmloffset;
          
            dfloat r_Aqx[p_Nfields],r_Bqy[p_Nfields],r_Cqz[p_Nfields]; 
            #pragma unroll p_Nfields
	      for(int fld=0; fld<p_Nfields; fld++){
		r_Aqx[fld] = 0.f; 
		r_Bqy[fld] = 0.f;  
		r_Cqz[fld] = 0.f; 
	      }

            r_Aqx[0]  = -p_sqrtRT*(r_lnxdq[1]);
            r_Aqx[1]  = -p_sqrtRT*(r_lnxdq[0]+p_sqrt2*r_lnxdq[7]);
            r_Aqx[2]  = -p_sqrtRT*(r_lnxdq[4]);
            r_Aqx[3]  = -p_sqrtRT*(r_lnxdq[5]);
            r_Aqx[4]  = -p_sqrtRT*(r_lnxdq[2]);
            r_Aqx[5]  = -p_sqrtRT*(r_lnxdq[3]);
            r_Aqx[7]  = -p_sqrtRT*p_sqrt2*(r_lnxdq[1]);

            r_Bqy[0]  = -p_sqrtRT*(r_lnydq[2]);
            r_Bqy[1]  = -p_sqrtRT*(r_lnydq[4]);  
            r_Bqy[2]  = -p_sqrtRT*(r_lnydq[0] + p_sqrt2*r_lnydq[8]);  
            r_Bqy[3]  = -p_sqrtRT*(r_lnydq[6]);
            r_Bqy[4]  = -p_sqrtRT*(r_lnydq[1]);  
            r_Bqy[6]  = -p_sqrtRT*(r_lnydq[3]);  
            r_Bqy[8]  = -p_sqrtRT*(p_sqrt2*r_lnydq[2]);

            r_Cqz[0]  = -p_sqrtRT*(r_lnzdq[3]);
            r_Cqz[1]  = -p_sqrtRT*(r_lnzdq[5]); 
            r_Cqz[2]  = -p_sqrtRT*(r_lnzdq[6]); 
            r_Cqz[3]  = -p_sqrtRT*(r_lnzdq[0] + p_sqrt2*r_lnzdq[9]);
            r_Cqz[5]  = -p_sqrtRT*(r_lnzdq[1]);
            r_Cqz[6]  = -p_sqrtRT*(r_lnzdq[2]);
            r_Cqz[9]  = -p_sqrtRT*(p_sqrt2*r_lnzdq[3]);

            #pragma unroll p_Nfields
	      for(int fld=0; fld<p_Nfields; fld++){
		pmlrhsqx[pmlrhsId + fld*p_Np] += r_Aqx[fld];
		pmlrhsqy[pmlrhsId + fld*p_Np] += r_Bqy[fld];
		pmlrhsqz[pmlrhsId + fld*p_Np] += r_Cqz[fld];
		rhsq[rhsId+fld*p_Np]          += (r_Aqx[fld] + r_Bqy[fld] + r_Cqz[fld]); 
	      }

        
	  }
	}
      }
    }
  }
}

