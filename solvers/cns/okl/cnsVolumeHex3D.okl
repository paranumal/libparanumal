
// isothermal Compressible Navier-Stokes
@kernel void cnsVolumeHex3D(const dlong Nelements,
			   const int advSwitch,
			   const dfloat fx,
			   const dfloat fy,
			   const dfloat fz, 
			   @restrict const  dfloat *  vgeo,
			   @restrict const  dfloat *  D,
			   @restrict const  dfloat *  viscousStresses,
			   @restrict const  dfloat *  q,
			   @restrict dfloat *  rhsq){
  
  for(dlong e=0;e<Nelements;++e;@outer(0)){

    @shared dfloat s_D[p_Nq][p_Nq];
    @shared dfloat s_F[p_Nfields][p_Nq][p_Nq][p_Nq];
    @shared dfloat s_G[p_Nfields][p_Nq][p_Nq][p_Nq];
    @shared dfloat s_H[p_Nfields][p_Nq][p_Nq][p_Nq];

    @exclusive dfloat r;
    
    for(int k=0;k<p_Nq;++k;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
          if(k==0)
            s_D[j][i] = D[j*p_Nq+i];
          
          // geometric factors
          const dlong gbase = e*p_Np*p_Nvgeo + k*p_Nq*p_Nq + j*p_Nq + i;
          const dfloat rx = vgeo[gbase+p_Np*p_RXID];
          const dfloat ry = vgeo[gbase+p_Np*p_RYID];
          const dfloat rz = vgeo[gbase+p_Np*p_RZID];
          const dfloat sx = vgeo[gbase+p_Np*p_SXID];
          const dfloat sy = vgeo[gbase+p_Np*p_SYID];
          const dfloat sz = vgeo[gbase+p_Np*p_SZID];
          const dfloat tx = vgeo[gbase+p_Np*p_TXID];
          const dfloat ty = vgeo[gbase+p_Np*p_TYID];
          const dfloat tz = vgeo[gbase+p_Np*p_TZID];
          const dfloat JW = vgeo[gbase+p_Np*p_JWID];

          // conserved variables
          const dlong  qbase = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;

	  r  = q[qbase+0*p_Np];
	  
          const dfloat ru = q[qbase+1*p_Np];
          const dfloat rv = q[qbase+2*p_Np];
          const dfloat rw = q[qbase+3*p_Np];
          const dfloat p  = r*p_RT;
          
          // primitive variables (velocity)
          const dfloat u = ru/r, v = rv/r, w = rw/r;
          
          // viscous stresses (precomputed by cnsStressesVolumeHex3D)
          const dlong id = e*p_Np*p_Nstresses + k*p_Nq*p_Nq + j*p_Nq + i;
          const dfloat T11 = viscousStresses[id+0*p_Np];
          const dfloat T12 = viscousStresses[id+1*p_Np];
          const dfloat T13 = viscousStresses[id+2*p_Np];
          const dfloat T22 = viscousStresses[id+3*p_Np];
          const dfloat T23 = viscousStresses[id+4*p_Np];
          const dfloat T33 = viscousStresses[id+5*p_Np];
          
          // (1/J) \hat{div} (G*[F;G])
          // questionable: why JW
          {
            // F0 = ru, G0 = rv
            const dfloat f = -advSwitch*ru;
            const dfloat g = -advSwitch*rv;
            const dfloat h = -advSwitch*rw;
            s_F[0][k][j][i] = JW*(rx*f + ry*g + rz*h);
            s_G[0][k][j][i] = JW*(sx*f + sy*g + sz*h);
            s_H[0][k][j][i] = JW*(tx*f + ty*g + tz*h);
          }
          
          {
            // F1 = 2*mu*S11 - (ru^2+p), G1 = 2*mu*S12 - (rvu)
            const dfloat f = T11-advSwitch*(ru*u+p);
            const dfloat g = T12-advSwitch*(rv*u);
            const dfloat h = T13-advSwitch*(rw*u);
            s_F[1][k][j][i] = JW*(rx*f + ry*g + rz*h);
            s_G[1][k][j][i] = JW*(sx*f + sy*g + sz*h);
            s_H[1][k][j][i] = JW*(tx*f + ty*g + tz*h);
          }
          
          {
            // F2 = 2*mu*S21 - (ruv), G2 = 2*mu*S22 - (rv^2+p)
            const dfloat f = T12-advSwitch*(rv*u);
            const dfloat g = T22-advSwitch*(rv*v+p);
            const dfloat h = T23-advSwitch*(rv*w);
            s_F[2][k][j][i] = JW*(rx*f + ry*g + rz*h);
            s_G[2][k][j][i] = JW*(sx*f + sy*g + sz*h);
            s_H[2][k][j][i] = JW*(tx*f + ty*g + tz*h);
          }
          
          {
            const dfloat f = T13-advSwitch*(rw*u);
            const dfloat g = T23-advSwitch*(rw*v);
            const dfloat h = T33-advSwitch*(rw*w+p);
            s_F[3][k][j][i] = JW*(rx*f + ry*g + rz*h);
            s_G[3][k][j][i] = JW*(sx*f + sy*g + sz*h);
            s_H[3][k][j][i] = JW*(tx*f + ty*g + tz*h);
          }
        }
      }
    }

    @barrier("local");

    for(int k=0;k<p_Nq;++k;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){    
          const dlong gid = e*p_Np*p_Nvgeo+ k*p_Nq*p_Nq + j*p_Nq +i;
          const dfloat invJW = vgeo[gid + p_IJWID*p_Np];
          //      const dfloat invJW = p_one/vgeo[gid + p_IJWID*p_Np];
          
          dfloat rhsq0 = 0, rhsq1 = 0, rhsq2 = 0, rhsq3 = 0;
          
          for(int n=0;n<p_Nq;++n){
            const dfloat Din = s_D[n][i];
            const dfloat Djn = s_D[n][j];
            const dfloat Dkn = s_D[n][k];

            rhsq0 += Din*s_F[0][k][j][n];
            rhsq0 += Djn*s_G[0][k][n][i];
            rhsq0 += Dkn*s_H[0][n][j][i];

            rhsq1 += Din*s_F[1][k][j][n];
            rhsq1 += Djn*s_G[1][k][n][i];
            rhsq1 += Dkn*s_H[1][n][j][i];

            rhsq2 += Din*s_F[2][k][j][n];
            rhsq2 += Djn*s_G[2][k][n][i];
            rhsq2 += Dkn*s_H[2][n][j][i];
            
            rhsq3 += Din*s_F[3][k][j][n];
            rhsq3 += Djn*s_G[3][k][n][i];
            rhsq3 += Dkn*s_H[3][n][j][i];

          }
          
          const dlong base = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;
          
          // move to rhs
          rhsq[base+0*p_Np] = -invJW*rhsq0;
          rhsq[base+1*p_Np] = -invJW*rhsq1 + r*fx;
          rhsq[base+2*p_Np] = -invJW*rhsq2 + r*fy;
          rhsq[base+3*p_Np] = -invJW*rhsq3 + r*fz;

        }
      }
    }
  }
}


@kernel void cnsStressesVolumeHex3D(const dlong Nelements,
                                    @restrict const  dfloat *  vgeo,
                                    @restrict const  dfloat *  D,
                                    const dfloat mu,
                                    @restrict const  dfloat *  q,
                                    @restrict dfloat *  viscousStresses){
  
  for(dlong e=0;e<Nelements;++e;@outer(0)){

    @shared dfloat s_D[p_Nq][p_Nq];
    @shared dfloat s_u[p_Nq][p_Nq][p_Nq];
    @shared dfloat s_v[p_Nq][p_Nq][p_Nq];
    @shared dfloat s_w[p_Nq][p_Nq][p_Nq];

    for(int k=0;k<p_Nq;++k;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){

          if(k==0)
            s_D[j][i] = D[j*p_Nq+i];
          
          const dlong qbase = e*p_Nfields*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
          const dfloat r  = q[qbase + 0*p_Np];
          const dfloat ru = q[qbase + 1*p_Np];
          const dfloat rv = q[qbase + 2*p_Np];
          const dfloat rw = q[qbase + 3*p_Np];
          
          s_u[k][j][i] = ru/r;
          s_v[k][j][i] = rv/r;
          s_w[k][j][i] = rw/r;
          
        }
      }
    }
      
    @barrier("local");

    for(int k=0;k<p_Nq;++k;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
          
          dfloat dudr = 0, duds = 0, dudt = 0;
          dfloat dvdr = 0, dvds = 0, dvdt = 0;
          dfloat dwdr = 0, dwds = 0, dwdt = 0;
          
          for(int n=0;n<p_Nq;++n){
            const dfloat Din = s_D[i][n];
            const dfloat Djn = s_D[j][n];
            const dfloat Dkn = s_D[k][n];
            
            dudr += Din*s_u[k][j][n];
            duds += Djn*s_u[k][n][i];
            dudt += Dkn*s_u[n][j][i];

            dvdr += Din*s_v[k][j][n];
            dvds += Djn*s_v[k][n][i];
            dvdt += Dkn*s_v[n][j][i];

            dwdr += Din*s_w[k][j][n];
            dwds += Djn*s_w[k][n][i];
            dwdt += Dkn*s_w[n][j][i];
          }

          const dlong gbase = e*p_Np*p_Nvgeo + k*p_Nq*p_Nq + j*p_Nq + i;
          const dfloat rx = vgeo[gbase+p_Np*p_RXID];
          const dfloat ry = vgeo[gbase+p_Np*p_RYID];
          const dfloat rz = vgeo[gbase+p_Np*p_RZID];
          const dfloat sx = vgeo[gbase+p_Np*p_SXID];
          const dfloat sy = vgeo[gbase+p_Np*p_SYID];
          const dfloat sz = vgeo[gbase+p_Np*p_SZID];
          const dfloat tx = vgeo[gbase+p_Np*p_TXID];
          const dfloat ty = vgeo[gbase+p_Np*p_TYID];
          const dfloat tz = vgeo[gbase+p_Np*p_TZID];
          //      const dfloat JW = vgeo[gbase+p_Np*p_JWID];
          //const dfloat J = vgeo[gbase+p_Np*p_JID];
          
          const dfloat dudx = rx*dudr + sx*duds + tx*dudt;
          const dfloat dudy = ry*dudr + sy*duds + ty*dudt;
          const dfloat dudz = rz*dudr + sz*duds + tz*dudt;

          const dfloat dvdx = rx*dvdr + sx*dvds + tx*dvdt;
          const dfloat dvdy = ry*dvdr + sy*dvds + ty*dvdt;
          const dfloat dvdz = rz*dvdr + sz*dvds + tz*dvdt;

          const dfloat dwdx = rx*dwdr + sx*dwds + tx*dwdt;
          const dfloat dwdy = ry*dwdr + sy*dwds + ty*dwdt;
          const dfloat dwdz = rz*dwdr + sz*dwds + tz*dwdt;
          
          const dlong sbase = e*p_Nstresses*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
          
          const dfloat S11 = p_half*(dudx+dudx) - p_third*(dudx+dvdy+dwdz);
          const dfloat S12 = p_half*(dudy+dvdx);
          const dfloat S13 = p_half*(dudz+dwdx);
          const dfloat S22 = p_half*(dvdy+dvdy) - p_third*(dudx+dvdy+dwdz);
          const dfloat S23 = p_half*(dvdz+dwdy);
          const dfloat S33 = p_half*(dwdz+dwdz) - p_third*(dudx+dvdy+dwdz);
          
          viscousStresses[sbase + 0*p_Np] = p_two*mu*S11;
          viscousStresses[sbase + 1*p_Np] = p_two*mu*S12;
          viscousStresses[sbase + 2*p_Np] = p_two*mu*S13;
          viscousStresses[sbase + 3*p_Np] = p_two*mu*S22;
          viscousStresses[sbase + 4*p_Np] = p_two*mu*S23;
          viscousStresses[sbase + 5*p_Np] = p_two*mu*S33;


        }
      }
    }
  }
}
