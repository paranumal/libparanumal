/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


// Compressible Navier-Stokes
@kernel void cnsVolumeTri2D(const dlong Nelements,
                            @restrict const  dfloat *  vgeo,
                            @restrict const  dfloat *  DT,
                            @restrict const  dfloat *  x,
                            @restrict const  dfloat *  y,
                            @restrict const  dfloat *  z,
                            const dfloat t,
                            const dfloat mu,
                            const dfloat gamma,
                            @restrict const  dfloat *  q,
                            @restrict const  dfloat *  gradq,
                            @restrict dfloat *  rhsq){

  for(dlong e=0;e<Nelements;++e;@outer(0)){

    @shared dfloat s_F[p_Nfields][p_Np];
    @shared dfloat s_G[p_Nfields][p_Np];

    @exclusive dfloat fx, fy;

    for(int n=0;n<p_Np;++n;@inner(0)){

      // prefetch geometric factors (constant on triangle)
      const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
      const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
      const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
      const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];

      // conserved variables
      const dlong  qbase = e*p_Np*p_Nfields + n;
      const dfloat r  = q[qbase+0*p_Np];
      const dfloat ru = q[qbase+1*p_Np];
      const dfloat rv = q[qbase+2*p_Np];
      const dfloat E  = q[qbase+3*p_Np];

      // primitive variables
      const dfloat u = ru/r, v = rv/r;
      const dfloat p = (gamma-1)*(E-0.5*r*(u*u+v*v));

      // gradients
      const dlong id = e*p_Np*p_Ngrads + n;
      const dfloat dudx = gradq[id+0*p_Np];
      const dfloat dudy = gradq[id+1*p_Np];
      const dfloat dvdx = gradq[id+2*p_Np];
      const dfloat dvdy = gradq[id+3*p_Np];

      //Body force contribution
      fx = 0.0; fy = 0.0;
      cnsBodyForce2D(gamma, mu, t, x[e*p_Np+n], y[e*p_Np+n], r, u, v, p, &fx, &fy);
      fx *= r; fy *= r;

      const dfloat T11 = 2.0*dudx - 2.0*(dudx+dvdy)/3.0;
      const dfloat T12 = dudy+dvdx;
      const dfloat T22 = 2.0*dvdy - 2.0*(dudx+dvdy)/3.0;

      //  \hat{div} (G*[F;G])
      {
        // F0 = ru, G0 = rv
        const dfloat f = -ru;
        const dfloat g = -rv;
        s_F[0][n] = drdx*f + drdy*g;
        s_G[0][n] = dsdx*f + dsdy*g;
      }

      {
        // F1 = mu*T11 - (ru^2+p), G1 = mu*T12 - (rvu)
        const dfloat f = -(ru*u+p) + mu*T11;
        const dfloat g = -(rv*u)   + mu*T12;
        s_F[1][n] = drdx*f + drdy*g;
        s_G[1][n] = dsdx*f + dsdy*g;
      }

      {
        // F2 = mu*T21 - (ruv), G2 = mu*T22 - (rv^2+p)
        const dfloat f = -(rv*u)   + mu*T12;
        const dfloat g = -(rv*v+p) + mu*T22;
        s_F[2][n] = drdx*f + drdy*g;
        s_G[2][n] = dsdx*f + dsdy*g;
      }

      {
        // F3 = mu*(u*T11+v*T12) - (uE+up), G2 = mu*(u*T12+v*T22) - (vE+vp)
        const dfloat f = -(u*E+u*p) + mu*(u*T11 + v*T12);
        const dfloat g = -(v*E+v*p) + mu*(u*T12 + v*T22);
        s_F[3][n] = drdx*f + drdy*g;
        s_G[3][n] = dsdx*f + dsdy*g;
      }
    }

    @barrier("local");

    for(int n=0;n<p_Np;++n;@inner(0)){

      dfloat rhsq0 = 0, rhsq1 = 0, rhsq2 = 0, rhsq3 = 0;

      for(int i=0;i<p_Np;++i){
        const dfloat Drni = DT[n+i*p_Np+0*p_Np*p_Np];
        const dfloat Dsni = DT[n+i*p_Np+1*p_Np*p_Np];

        rhsq0 += Drni*s_F[0][i]
                +Dsni*s_G[0][i];
        rhsq1 += Drni*s_F[1][i]
                +Dsni*s_G[1][i];
        rhsq2 += Drni*s_F[2][i]
                +Dsni*s_G[2][i];
        rhsq3 += Drni*s_F[3][i]
                +Dsni*s_G[3][i];
      }

      const dlong base = e*p_Np*p_Nfields + n;

      // move to rhs
      rhsq[base+0*p_Np] = rhsq0;
      rhsq[base+1*p_Np] = rhsq1+fx;
      rhsq[base+2*p_Np] = rhsq2+fy;
      rhsq[base+3*p_Np] = rhsq3;
    }
  }
}
