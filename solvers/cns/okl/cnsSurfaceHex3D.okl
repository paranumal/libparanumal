

// Roe averaged Riemann solver
void upwindRoeAveraged(const dfloat nx,
                       const dfloat ny,
                       const dfloat nz,
                       const dfloat tx,
                       const dfloat ty,
                       const dfloat tz,
                       const dfloat bx,
                       const dfloat by,
                       const dfloat bz, 
                       const dfloat rM,
                       const dfloat ruM,
                       const dfloat rvM,
                       const dfloat rwM,
                       const dfloat rP,
                       const dfloat ruP,
                       const dfloat rvP,
                       const dfloat rwP,
                       dfloat *rflux,
                       dfloat *ruflux,
                       dfloat *rvflux,
                       dfloat *rwflux){

  // Rotate "-" trace momentum to face normal-tangent coordinates
  dfloat runM = nx*ruM + ny*rvM + nz*rwM;
  dfloat rutM = tx*ruM + ty*rvM + tz*rwM;
  dfloat rubM = bx*ruM + by*rvM + bz*rwM;

  dfloat runP = nx*ruP + ny*rvP + nz*rwP;
  dfloat rutP = tx*ruP + ty*rvP + tz*rwP;
  dfloat rubP = bx*ruP + by*rvP + bz*rwP;
  
  dfloat unM = runM/rM;
  dfloat utM = rutM/rM;
  dfloat ubM = rubM/rM;

  dfloat unP = runP/rP;
  dfloat utP = rutP/rP;
  dfloat ubP = rubP/rP;

  dfloat pM = p_RT*rM;
  dfloat pP = p_RT*rP;
  
  // Compute Roe average variables
  dfloat rMsqr = sqrt(rM);
  dfloat rPsqr = sqrt(rP); 

  dfloat r = rMsqr*rPsqr;
  dfloat un = (rMsqr*unM + rPsqr*unP)/(rMsqr + rPsqr);
  dfloat ut = (rMsqr*utM + rPsqr*utP)/(rMsqr + rPsqr);
  dfloat ub = (rMsqr*ubM + rPsqr*ubP)/(rMsqr + rPsqr);
  
  dfloat c2  = p_RT;
  dfloat c   = p_sqrtRT;

  /* 
     Riemann fluxes

  V = [0 0 1 1; 0 0 u+c u-c; 0 1 v v; 1 0 w w];
  >> inv(V)

       ans =

       [            -w,        0, 0, 1]
       [            -v,        0, 1, 0]
       [ (c - u)/(2*c),  1/(2*c), 0, 0]
       [ (c + u)/(2*c), -1/(2*c), 0, 0]
  */
  
  dfloat W1M = rubM - ub*rM;
  dfloat W2M = rutM - ut*rM;
  dfloat W3M = p_half*((c-un)*rM + runM)/c;
  dfloat W4M = p_half*((c+un)*rM - runM)/c;

  dfloat W1P = rubP - ub*rP;
  dfloat W2P = rutP - ut*rP;
  dfloat W3P = p_half*((c-un)*rP + runP)/c;
  dfloat W4P = p_half*((c+un)*rP - runP)/c;

  // check inequalities
  dfloat W1 = (un>0) ? W1M:W1P;
  dfloat W2 = (un>0) ? W2M:W2P;
  dfloat W3 = (un+c>0) ? W3M:W3P;
  dfloat W4 = (un-c>0) ? W4M:W4P;

  dfloat rS   = W3+W4;
  dfloat runS = (c+un)*W3 + (un-c)*W4;
  dfloat rutS = W2 + ut*(W3+W4);
  dfloat rubS = W1 + ub*(W3+W4);

  dfloat ruS = nx*runS + tx*rutS + bx*rubS;
  dfloat rvS = ny*runS + ty*rutS + by*rubS;
  dfloat rwS = nz*runS + tz*rutS + bz*rubS;

  dfloat uS = ruS/rS;
  dfloat vS = rvS/rS;
  dfloat wS = rwS/rS;
  dfloat pS = p_RT*rS;

  dfloat uM = ruM/rM;
  dfloat vM = rvM/rM;
  dfloat wM = rwM/rM;
  
  *rflux  = nx*(ruS-ruM) + ny*(rvS-rvM) + nz*(rwS-rwM);
  *ruflux = nx*(ruS*uS-ruM*uM) + ny*(ruS*vS-ruM*vM) + nz*(ruS*wS-ruM*wM);
  *rvflux = nx*(rvS*uS-rvM*uM) + ny*(rvS*vS-rvM*vM) + nz*(rvS*wS-rvM*wM);
  *rwflux = nx*(rwS*uS-rwM*uM) + ny*(rwS*vS-rwM*vM) + nz*(rwS*wS-rwM*wM);

  *ruflux += nx*(pS-pM);
  *rvflux += ny*(pS-pM);
  *rwflux += nz*(pS-pM);

  //subtract F(qM)
  *rflux  -= -nx*ruM               - ny*rvM             - nz*rwM;
  *ruflux -= -nx*(ruM*ruM/rM + pM) - ny*(rvM*ruM/rM)    - nz*(rwM*ruM/rM);
  *rvflux -= -nx*(ruM*rvM/rM)      - ny*(rvM*rvM/rM+pM) - nz*(rwM*rvM/rM);
  *rwflux -= -nx*(ruM*rwM/rM)      - ny*(rvM*rwM/rM)    - nz*(rwM*rwM/rM+pM);
}



void surfaceTerms(const int e, 
                  const int sk, 
                  const int face, 
                  const int i, 
                  const int j, 
                  const int k,
                  const int advSwitch,
		  const dfloat intfx,
		  const dfloat intfy,		  
		  const dfloat intfz,
		  const dfloat time, 
                  @global const dfloat *x, 
                  @global const dfloat *y, 
                  @global const dfloat *z, 
                  @global const dfloat *sgeo, 
                  @global const dlong *vmapM, 
                  @global const dlong *vmapP,
		  @global const dlong *EToB,
                  @global const dfloat *q, 
                  @global const dfloat *viscousStresses,
                  @global dfloat *rhsq){
  
  const dfloat nx = sgeo[sk*p_Nsgeo+p_NXID];                            
  const dfloat ny = sgeo[sk*p_Nsgeo+p_NYID];                            
  const dfloat nz = sgeo[sk*p_Nsgeo+p_NZID];                            
  const dfloat tx = sgeo[sk*p_Nsgeo+p_STXID];                            
  const dfloat ty = sgeo[sk*p_Nsgeo+p_STYID];                            
  const dfloat tz = sgeo[sk*p_Nsgeo+p_STZID];                            
  const dfloat bx = sgeo[sk*p_Nsgeo+p_SBXID];                            
  const dfloat by = sgeo[sk*p_Nsgeo+p_SBYID];                            
  const dfloat bz = sgeo[sk*p_Nsgeo+p_SBZID];                            
  const dfloat sJ = sgeo[sk*p_Nsgeo+p_SJID];                            
  const dfloat invWJ = sgeo[sk*p_Nsgeo+p_WIJID];                        
                                                                        
  const dlong idM = vmapM[sk];                                          
  const dlong idP = vmapP[sk];                                          
                                                                        
  const dlong eM = e;                                                   
  const dlong eP = idP/p_Np;                                            
  const int vidM = idM%p_Np;                                            
  const int vidP = idP%p_Np;                                            
                                                                        
  const dlong qbaseM = eM*p_Np*p_Nfields + vidM;                        
  const dlong qbaseP = eP*p_Np*p_Nfields + vidP;                        
                                                                        
  const dlong sbaseM = eM*p_Np*p_Nstresses + vidM;                      
  const dlong sbaseP = eP*p_Np*p_Nstresses + vidP;                      
                                                                        
  const dfloat rM  = q[qbaseM + 0*p_Np];                                
  const dfloat ruM = q[qbaseM + 1*p_Np];                                
  const dfloat rvM = q[qbaseM + 2*p_Np];                                
  const dfloat rwM = q[qbaseM + 3*p_Np];                                
                                                                        
  const dfloat T11M = viscousStresses[sbaseM+0*p_Np];                   
  const dfloat T12M = viscousStresses[sbaseM+1*p_Np];                   
  const dfloat T13M = viscousStresses[sbaseM+2*p_Np];                   
  const dfloat T22M = viscousStresses[sbaseM+3*p_Np];                   
  const dfloat T23M = viscousStresses[sbaseM+4*p_Np];                   
  const dfloat T33M = viscousStresses[sbaseM+5*p_Np];                   
                                                                        
  dfloat rP  = q[qbaseP + 0*p_Np];                                      
  dfloat ruP = q[qbaseP + 1*p_Np];                                      
  dfloat rvP = q[qbaseP + 2*p_Np];                                      
  dfloat rwP = q[qbaseP + 3*p_Np];                                      
                                                                        
  const dfloat T11P = viscousStresses[sbaseP+0*p_Np];                   
  const dfloat T12P = viscousStresses[sbaseP+1*p_Np];                   
  const dfloat T13P = viscousStresses[sbaseP+2*p_Np];                   
  const dfloat T22P = viscousStresses[sbaseP+3*p_Np];                   
  const dfloat T23P = viscousStresses[sbaseP+4*p_Np];                   
  const dfloat T33P = viscousStresses[sbaseP+5*p_Np];                   
                                                                        
  const dfloat uM = ruM/rM;                                             
  const dfloat vM = rvM/rM;                                             
  const dfloat wM = rwM/rM;                                             
  const dfloat pM = p_RT*rM;                                            
                                                                        
  dfloat uP = ruP/rP;                                                   
  dfloat vP = rvP/rP;                                                   
  dfloat wP = rwP/rP;                                                   
  dfloat pP = p_RT*rP;                                                  
                                                                        
  const int bc = EToB[face+p_Nfaces*e];                                 
  if(bc>0){                                                             
    cnsDirichletConditions3D(bc, time, x[idM], y[idM], z[idM], nx, ny, nz, intfx, intfy, intfz, rM, uM, vM, wM, &rP, &uP, &vP, &wP); 
    ruP = rP*uP;                                                        
    rvP = rP*vP;                                                        
    rwP = rP*wP;                                                        
    pP = p_RT*rP;                                                       
  }                                                                     
                                                                        
  const dfloat sc = invWJ*sJ;                                           
                                                                        
  dfloat rflux, ruflux, rvflux, rwflux;                                 
  upwindRoeAveraged (nx, ny, nz, tx, ty, tz, bx, by, bz, rM, ruM, rvM, rwM, rP, ruP, rvP, rwP, &rflux, &ruflux, &rvflux, &rwflux); 
  rflux *= advSwitch;                                                   
  ruflux *= advSwitch;                                                  
  rvflux *= advSwitch;                                                  
  rwflux *= advSwitch;                                                  
                                                                        
  ruflux -= p_half*(nx*(T11P+T11M) + ny*(T12P+T12M) + nz*(T13P+T13M));  
  rvflux -= p_half*(nx*(T12P+T12M) + ny*(T22P+T22M) + nz*(T23P+T23M));  
  rwflux -= p_half*(nx*(T13P+T13M) + ny*(T23P+T23M) + nz*(T33P+T33M));  
                                                                        
                                                                        
  const dlong base = e*p_Np*p_Nfields+k*p_Nq*p_Nq + j*p_Nq+i;           
  rhsq[base+0*p_Np] += sc*(-rflux);                                     
  rhsq[base+1*p_Np] += sc*(-ruflux);                                    
  rhsq[base+2*p_Np] += sc*(-rvflux);                                    
  rhsq[base+3*p_Np] += sc*(-rwflux);                                    
}

// batch process elements
@kernel void cnsSurfaceHex3D(const dlong Nelements,
                            const int advSwitch,
                            @restrict const  dfloat *  sgeo,
                            @restrict const  dfloat *  LIFTT,      
                            @restrict const  dlong  *  vmapM,
                            @restrict const  dlong  *  vmapP,
                            @restrict const  int    *  EToB,
                            const dfloat time,
                            @restrict const  dfloat *  x,
                            @restrict const  dfloat *  y,
                            @restrict const  dfloat *  z,
			     const dfloat mu,
			     const dfloat intfx,
			     const dfloat intfy,
			     const dfloat intfz,
			     @restrict const  dfloat *  q,
                            @restrict const  dfloat *  viscousStresses,
                            @restrict dfloat *  rhsq){
  
  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){
    
    // for all face nodes of all elements
    // face 0 & 5
    for(int es=0;es<p_NblockS;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
          const dlong e = eo + es;
          if(e<Nelements){
            const dlong sk0 = e*p_Nfp*p_Nfaces + 0*p_Nfp + j*p_Nq + i;
            const dlong sk5 = e*p_Nfp*p_Nfaces + 5*p_Nfp + j*p_Nq + i;
            
            //            surfaceTerms(sk0,0,i,j,0     );
            surfaceTerms(e, sk0, 0, i, j, 0, advSwitch, intfx, intfy, intfz, time,
			 x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses, rhsq);

            //            surfaceTerms(sk5,5,i,j,(p_Nq-1));
            surfaceTerms(e, sk5, 5, i, j, (p_Nq-1), advSwitch, intfx, intfy, intfz, time,
                         x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses, rhsq);
          }
        }
      }
    }
    
    @barrier("global");
    
    // face 1 & 3
    for(int es=0;es<p_NblockS;++es;@inner(2)){
      for(int k=0;k<p_Nq;++k;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
          const dlong e = eo + es;
          if(e<Nelements){
            const dlong sk1 = e*p_Nfp*p_Nfaces + 1*p_Nfp + k*p_Nq + i;
            const dlong sk3 = e*p_Nfp*p_Nfaces + 3*p_Nfp + k*p_Nq + i;
            
            //            surfaceTerms(sk1,1,i,0     ,k);
            surfaceTerms(e, sk1, 1, i, 0, k, advSwitch, intfx, intfy, intfz, time,
			 x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses, rhsq);

            //surfaceTerms(sk3,3,i,(p_Nq-1),k);
            surfaceTerms(e, sk3, 3, i, (p_Nq-1), k, advSwitch, intfx, intfy, intfz, time,
			 x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses, rhsq);
          }
        }
      }
    }
    
    @barrier("global");
    
    // face 2 & 4
    for(int es=0;es<p_NblockS;++es;@inner(2)){
      for(int k=0;k<p_Nq;++k;@inner(1)){
        for(int j=0;j<p_Nq;++j;@inner(0)){
          const dlong e = eo + es;
          if(e<Nelements){
            const dlong sk2 = e*p_Nfp*p_Nfaces + 2*p_Nfp + k*p_Nq + j;
            const dlong sk4 = e*p_Nfp*p_Nfaces + 4*p_Nfp + k*p_Nq + j;
            
            //            surfaceTerms(sk2,2,(p_Nq-1),j,k);
            surfaceTerms(e, sk2, 2, (p_Nq-1), j, k, advSwitch, intfx, intfy, intfz, time,
			 x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses, rhsq);
            
            //surfaceTerms(sk4,4,0     ,j,k);
            surfaceTerms(e, sk4, 4, 0, j, k, advSwitch, intfx, intfy, intfz, time,
			 x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses, rhsq);
          }
        }
      }
    }
  }
}

void stressSurfaceTerms(const int e, 
                        const int sk,
                        const int face,
                        const int i, 
                        const int j, 
                        const int k,
			const dfloat intfx,
			const dfloat intfy,
			const dfloat intfz,
                        const dfloat time,
			const dfloat mu,
                        @global const dfloat *x, 
                        @global const dfloat *y, 
                        @global const dfloat *z, 
                        @global const dfloat *sgeo, 
                        @global const dlong *vmapM, 
                        @global const dlong *vmapP,
			@global const dlong *EToB,
                        @global const dfloat *q,
                        @global dfloat *viscousStresses){

  const dfloat nx = sgeo[sk*p_Nsgeo+p_NXID];                            
  const dfloat ny = sgeo[sk*p_Nsgeo+p_NYID];                            
  const dfloat nz = sgeo[sk*p_Nsgeo+p_NZID];                            
  const dfloat sJ = sgeo[sk*p_Nsgeo+p_SJID];                            
  const dfloat invWJ = sgeo[sk*p_Nsgeo+p_WIJID];                        
                                                                        
  const dlong idM = vmapM[sk];                                          
  const dlong idP = vmapP[sk];                                          
                                                                        
  const dlong eM = e;                                                   
  const dlong eP = idP/p_Np;                                            
  const int vidM = idM%p_Np;                                            
  const int vidP = idP%p_Np;                                            
                                                                        
  const dlong baseM = eM*p_Np*p_Nfields + vidM;                         
  const dlong baseP = eP*p_Np*p_Nfields + vidP;                         
                                                                        
  const dfloat rM  = q[baseM + 0*p_Np];                                 
  const dfloat ruM = q[baseM + 1*p_Np];                                 
  const dfloat rvM = q[baseM + 2*p_Np];                                 
  const dfloat rwM = q[baseM + 3*p_Np];                                 
                                                                        
  dfloat uM = ruM/rM;                                                   
  dfloat vM = rvM/rM;                                                   
  dfloat wM = rwM/rM;                                                   
                                                                        
  dfloat rP  = q[baseP + 0*p_Np];                                       
  dfloat ruP = q[baseP + 1*p_Np];                                       
  dfloat rvP = q[baseP + 2*p_Np];                                       
  dfloat rwP = q[baseP + 3*p_Np];                                       
                                                                        
  dfloat uP = ruP/rP;                                                   
  dfloat vP = rvP/rP;                                                   
  dfloat wP = rwP/rP;                                                   
                                                                        
  const int bc = EToB[face+p_Nfaces*e];                                 
  if(bc>0) {                                                            
    cnsDirichletConditions3D(bc, time, x[idM], y[idM], z[idM], nx, ny, nz, intfx, intfy, intfz, rM, uM, vM, wM, &rP, &uP, &vP, &wP); 
  }                                                                     
                                                                        
  const dfloat dS11 = p_half*(nx*(p_two*(uP-uM))) - p_third*(nx*(uP-uM)+ny*(vP-vM)+nz*(wP-wM)); 
  const dfloat dS12 = p_half*(ny*(uP-uM) + nx*(vP-vM));                 
  const dfloat dS13 = p_half*(nz*(uP-uM) + nx*(wP-wM));                 
  const dfloat dS22 = p_half*(ny*(p_two*(vP-vM))) - p_third*(nx*(uP-uM)+ny*(vP-vM)+nz*(wP-wM)); 
  const dfloat dS23 = p_half*(nz*(vP-vM) + ny*(wP-wM));                 
  const dfloat dS33 = p_half*(nz*(p_two*(wP-wM))) - p_third*(nx*(uP-uM)+ny*(vP-vM)+nz*(wP-wM)); 
                                                                        
  const dfloat sc = invWJ * sJ;                                         
  const dlong base = e*p_Np*p_Nstresses+k*p_Nq*p_Nq+j*p_Nq+i;   
  viscousStresses[base+0*p_Np] += sc*p_two*mu*dS11;                     
  viscousStresses[base+1*p_Np] += sc*p_two*mu*dS12;                     
  viscousStresses[base+2*p_Np] += sc*p_two*mu*dS13;                     
  viscousStresses[base+3*p_Np] += sc*p_two*mu*dS22;                     
  viscousStresses[base+4*p_Np] += sc*p_two*mu*dS23;                     
  viscousStresses[base+5*p_Np] += sc*p_two*mu*dS33;                     
}

@kernel void cnsStressesSurfaceHex3D(const int Nelements,
                                    @restrict const  dfloat *  sgeo,
                                     @restrict const  dfloat *  LIFTT,
                                    @restrict const  int   *  vmapM,
                                    @restrict const  int   *  vmapP,
                                    @restrict const  int   *  EToB,
                                    const dfloat time,
                                    @restrict const  dfloat *  x,
                                    @restrict const  dfloat *  y,
                                    @restrict const  dfloat *  z,
                                    const dfloat mu,
				     const dfloat intfx,
				     const dfloat intfy,
				     const dfloat intfz,
				     @restrict const  dfloat *  q,
                                    @restrict dfloat *  viscousStresses){
  
  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){

    // for all face nodes of all elements
    // face 0 & 5
    for(int es=0;es<p_NblockS;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
          const dlong e = eo + es;
          if(e<Nelements){
            const dlong sk0 = e*p_Nfp*p_Nfaces + 0*p_Nfp + j*p_Nq + i;
            const dlong sk5 = e*p_Nfp*p_Nfaces + 5*p_Nfp + j*p_Nq + i;
            
            //            stressSurfaceTerms(sk0,0,i,j,0     );
            stressSurfaceTerms(e, sk0, 0, i, j, 0, intfx, intfy, intfz,
                               time, mu, x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses);

            //            stressSurfaceTerms(sk5,5,i,j,(p_Nq-1));
            stressSurfaceTerms(e, sk5, 5, i, j, (p_Nq-1), intfx, intfy, intfz,
                               time, mu, x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses);
          }
        }
      }
    }
    
    @barrier("global");
      
    // face 1 & 3
    for(int es=0;es<p_NblockS;++es;@inner(2)){
      for(int k=0;k<p_Nq;++k;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
          const dlong e = eo + es;
          if(e<Nelements){
            const dlong sk1 = e*p_Nfp*p_Nfaces + 1*p_Nfp + k*p_Nq + i;
            const dlong sk3 = e*p_Nfp*p_Nfaces + 3*p_Nfp + k*p_Nq + i;
            
            //            stressSurfaceTerms(sk1,1,i,0     ,k);
            stressSurfaceTerms(e, sk1, 1, i, 0, k, intfx, intfy, intfz,
                               time, mu, x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses);

            //stressSurfaceTerms(sk3,3,i,(p_Nq-1),k);
            stressSurfaceTerms(e, sk3, 3, i, (p_Nq-1), k,  intfx, intfy, intfz,
                               time, mu, x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses);

          }
        }
      }
    }

    @barrier("global");

    // face 2 & 4
    for(int es=0;es<p_NblockS;++es;@inner(2)){
      for(int k=0;k<p_Nq;++k;@inner(1)){
        for(int j=0;j<p_Nq;++j;@inner(0)){
          const dlong e = eo + es;
          if(e<Nelements){
            const dlong sk2 = e*p_Nfp*p_Nfaces + 2*p_Nfp + k*p_Nq + j;
            const dlong sk4 = e*p_Nfp*p_Nfaces + 4*p_Nfp + k*p_Nq + j;
            
            //            stressSurfaceTerms(sk2,2,(p_Nq-1),j ,k);
            stressSurfaceTerms(e, sk2, 2, (p_Nq-1), j, k, intfx, intfy, intfz,
                               time, mu, x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses);

            //stressSurfaceTerms(sk4,4,0,     j, k);
            stressSurfaceTerms(e, sk4, 4, 0, j, k, intfx, intfy, intfz,
                               time, mu, x, y, z, sgeo, vmapM, vmapP, EToB, q, viscousStresses);
          }
        }
      }
    }
  }
}






