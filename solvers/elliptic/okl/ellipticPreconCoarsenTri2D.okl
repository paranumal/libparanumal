/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

@kernel void ellipticPreconCoarsenTri2D(const dlong Nelements,
                                  @restrict const  dfloat *  P,
                                  @restrict const  dfloat *  qN,
                                  @restrict dfloat *  q1){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockVCoarse;@outer(0)){

    @shared dfloat s_qN[p_NblockVCoarse][p_NpFine];

    for(int es=0;es<p_NblockVCoarse;++es;@inner(1)){
      for(int n=0;n<p_NpCoarse;++n;@inner(0)){
        const dlong e = eo + es;
        if (e<Nelements) {
          for (int t=n; t<p_NpFine; t+=p_NpCoarse){
            s_qN[es][t] = qN[e*p_NpFine+t];
          }
        }
      }
    }


    for(int es=0;es<p_NblockVCoarse;++es;@inner(1)){
      for(int n=0;n<p_NpCoarse;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements){
          dfloat tmp = 0;
          #pragma unroll p_NpFine
            for(int i=0;i<p_NpFine;++i){
              tmp += P[n + i*p_NpCoarse]*s_qN[es][i]; // bank conflict ?
            }
          q1[e*p_NpCoarse+n] = tmp; // *invDegree[e*p_NpCoarse+n];
        }
      }
    }
  }
}


@kernel void ellipticPartialPreconCoarsenTri2D(const dlong Nelements,
                                  @restrict const  dlong   *  elementList,
                                  @restrict const  dlong   *  GlobalToLocal,
                                  @restrict const  dfloat *  P,
                                  @restrict const  dfloat *  qN,
                                  @restrict dfloat *  q1){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockVCoarse;@outer(0)){

    @shared dfloat s_qN[p_NblockVCoarse][p_NpFine];

    for(int es=0;es<p_NblockVCoarse;++es;@inner(1)){
      for(int n=0;n<p_NpCoarse;++n;@inner(0)){
        const dlong e = eo + es;
        if (e<Nelements) {
          const dlong element = elementList[e];

          for (int t=n; t<p_NpFine; t+=p_NpCoarse){
            const dlong base = t + element*p_NpFine;
            const dlong id = GlobalToLocal[base];

            s_qN[es][t] = (id!=-1) ? qN[id] : 0.0;
          }
        }
      }
    }


    for(int es=0;es<p_NblockVCoarse;++es;@inner(1)){
      for(int n=0;n<p_NpCoarse;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements){
          dfloat tmp = 0;
          #pragma unroll p_NpFine
            for(int i=0;i<p_NpFine;++i){
              tmp += P[n + i*p_NpCoarse]*s_qN[es][i]; // bank conflict ?
            }

          const dlong element = elementList[e];
          q1[element*p_NpCoarse+n] = tmp; // *invDegree[e*p_NpCoarse+n];
        }
      }
    }
  }
}

//storing P in @shared is too much for 3D
#if 0
@kernel void ellipticPreconCoarsen_v1(const dlong Nelements,
                                  @restrict const  dfloat *  P,
                                  @restrict const  dfloat *  qN,
                                  @restrict dfloat *  q1){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockVCoarse;@outer(0)){

    @shared dfloat s_qN[p_NblockVCoarse][p_NpFine];
    @shared dfloat s_P[p_NpFine][p_NpCoarse];

    for(int es=0;es<p_NblockVCoarse;++es;@inner(1)){
      for(int n=0;n<p_NpCoarse;++n;@inner(0)){
        dlong t = n + es*p_NpCoarse;

        while(t<p_NpFine*p_NblockVCoarse){
          if(eo*p_NpFine + t< Nelements*p_NpFine)
            s_qN[0][t] = qN[eo*p_NpFine+t];
          t += p_NpCoarse*p_NblockVCoarse;
        }

        t = n + es*p_NpCoarse;

        while(t<p_NpFine*p_NpCoarse){
          s_P[0][t] = P[t];
          t += p_NpCoarse*p_NblockVCoarse;
        }
      }
    }


    for(int es=0;es<p_NblockVCoarse;++es;@inner(1)){
      for(int n=0;n<p_NpCoarse;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements){
          dfloat tmp = 0;
          #pragma unroll p_NpFine
            for(int i=0;i<p_NpFine;++i){
              tmp += s_P[i][n]*s_qN[es][i]; // bank conflict ?
            }
          q1[e*p_NpCoarse+n] = tmp; // *invDegree[e*p_NpCoarse+n];
        }
      }
    }
  }
}
#endif

