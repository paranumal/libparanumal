@kernel void insVorticityTet3D(const dlong Nelements,
                              @restrict const  dfloat *  vgeo,
                              @restrict const  dfloat *  const Dmatrices,
                              const dlong offset,
                              @restrict const  dfloat *  U,
                                    @restrict dfloat *  Vort){  
  
  // block partition of elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){
    
    @shared dfloat s_u[p_NblockV][p_Np];
    @shared dfloat s_v[p_NblockV][p_Np];
    @shared dfloat s_w[p_NblockV][p_Np];

    for(int e=eo;e<eo+p_NblockV;++e;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
        if(e<Nelements){
          const dlong id = e*p_Np+n;
          s_u[e-eo][n] = U[id+0*offset];
          s_v[e-eo][n] = U[id+1*offset];
          s_w[e-eo][n] = U[id+2*offset];
        }
      }
    }
          
    @barrier("local");

    for(int e=eo;e<eo+p_NblockV;++e;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){
        if(e<Nelements){
          const int es = e-eo;
          const dlong gid = e*p_Nvgeo;
          const dfloat drdx = vgeo[gid + p_RXID];
          const dfloat drdy = vgeo[gid + p_RYID];
          const dfloat drdz = vgeo[gid + p_RZID];
          const dfloat dsdx = vgeo[gid + p_SXID];
          const dfloat dsdy = vgeo[gid + p_SYID];
          const dfloat dsdz = vgeo[gid + p_SZID];
          const dfloat dtdx = vgeo[gid + p_TXID];
          const dfloat dtdy = vgeo[gid + p_TYID];
          const dfloat dtdz = vgeo[gid + p_TZID];    

          // compute 1D derivatives
          dfloat ur = 0, vr = 0, wr =0;
          dfloat us = 0, vs = 0, ws =0;
          dfloat ut = 0, vt = 0, wt =0;

          #pragma unroll p_Np
            for(int i=0;i<p_Np;++i) {
              const dfloat Drn = Dmatrices[n + i*p_Np+0*p_Np*p_Np];
              const dfloat Dsn = Dmatrices[n + i*p_Np+1*p_Np*p_Np];
              const dfloat Dtn = Dmatrices[n + i*p_Np+2*p_Np*p_Np];

              ur += Drn*s_u[es][i];
              us += Dsn*s_u[es][i];
              ut += Dtn*s_u[es][i];

              vr += Drn*s_v[es][i];
              vs += Dsn*s_v[es][i];
              vt += Dtn*s_v[es][i];

              wr += Drn*s_w[es][i];
              ws += Dsn*s_w[es][i];
              wt += Dtn*s_w[es][i];
            }

          const dfloat uy = drdy*ur + dsdy*us + dtdy*ut;
          const dfloat uz = drdz*ur + dsdz*us + dtdz*ut;

          const dfloat vx = drdx*vr + dsdx*vs + dtdx*vt;
          const dfloat vz = drdz*vr + dsdz*vs + dtdz*vt;

          const dfloat wx = drdx*wr + dsdx*ws + dtdx*wt;
          const dfloat wy = drdy*wr + dsdy*ws + dtdy*wt;

          const dlong id = e*p_Np+n; 
          Vort[id+0*offset] = wy-vz;
          Vort[id+1*offset] = uz-wx;
          Vort[id+2*offset] = vx-uy;
        }
      }
    }
  }
}