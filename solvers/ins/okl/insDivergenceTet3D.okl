/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


// Computes volume contribution of div(UI)
@kernel void insDivergenceVolumeTet3D(const dlong Nelements,
                                     @restrict const  dfloat *  vgeo,
                                     @restrict const  dfloat *  Dmatrices,
                                     const dlong offset,
                                     @restrict const  dfloat *  U,
                                           @restrict dfloat *  divU){
  
  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){  // for all elements

    @shared dfloat s_U[p_NblockV][p_Np];
    @shared dfloat s_V[p_NblockV][p_Np];
    @shared dfloat s_W[p_NblockV][p_Np];
  
    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element
        const dlong e = eo+es; // element in block
        if(e<Nelements){ 
          const dlong id = e*p_Np+n;
          s_U[es][n] = U[id+0*offset];
          s_V[es][n] = U[id+1*offset];         
          s_W[es][n] = U[id+2*offset];         
        }
      }
    }

    // Make sure all node data is loaded into @shared
    @barrier("local");

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element
        const dlong e = eo+es; // element in block
        if(e<Nelements){    
          // prefetch geometric factors (constant on triangle)
          // prefetch geometric factors (constant on triangle)
          const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
          const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
          const dfloat drdz = vgeo[e*p_Nvgeo + p_RZID];

          const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
          const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
          const dfloat dsdz = vgeo[e*p_Nvgeo + p_SZID];

          const dfloat dtdx = vgeo[e*p_Nvgeo + p_TXID];
          const dfloat dtdy = vgeo[e*p_Nvgeo + p_TYID];
          const dfloat dtdz = vgeo[e*p_Nvgeo + p_TZID];

          // compute 'r' and 's' derivatives of (q_m) at node n
          dfloat dudr = 0.f, duds = 0.f, dudt = 0.f; 
          dfloat dvdr = 0.f, dvds = 0.f, dvdt = 0.f; 
          dfloat dwdr = 0.f, dwds = 0.f, dwdt = 0.f; 
          #pragma unroll p_Np
          for(int i=0;i<p_Np;++i){
            const dfloat Drni = Dmatrices[n+i*p_Np+0*p_Np*p_Np];
            const dfloat Dsni = Dmatrices[n+i*p_Np+1*p_Np*p_Np];
            const dfloat Dtni = Dmatrices[n+i*p_Np+2*p_Np*p_Np];
            // 
            dudr += Drni*s_U[es][i];
            duds += Dsni*s_U[es][i];
            dudt += Dtni*s_U[es][i];

            dvdr += Drni*s_V[es][i];
            dvds += Dsni*s_V[es][i];
            dvdt += Dtni*s_V[es][i];

            dwdr += Drni*s_W[es][i];
            dwds += Dsni*s_W[es][i];
            dwdt += Dtni*s_W[es][i];
          }

          // chain rule
          dfloat dudx   = drdx*dudr + dsdx*duds + dtdx*dudt;
          dfloat dvdy   = drdy*dvdr + dsdy*dvds + dtdy*dvdt;
          dfloat dwdz   = drdz*dwdr + dsdz*dwds + dtdz*dwdt;

          const dlong id = e*p_Np+n;
          divU[id] = dudx + dvdy + dwdz;
        }
      }
    }
  }
}

@kernel void insDivergenceSurfaceTet3D(const dlong Nelements,
                                      @restrict const  dfloat *  sgeo,
                                      @restrict const  dfloat *  LIFTT,
                                      @restrict const  dlong  *  vmapM,
                                      @restrict const  dlong  *  vmapP,
                                      @restrict const  int    *  EToB,
                                      const dfloat time,
                                      @restrict const  dfloat *  x,
                                      @restrict const  dfloat *  y,
                                      @restrict const  dfloat *  z,
                                      const dlong offset,
                                      @restrict const  dfloat *  U,
                                            @restrict dfloat *  divU){ 

  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){
    @shared dfloat s_fluxU[p_NblockS][p_Nfp*p_Nfaces];

    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Nfp*p_Nfaces){
            // find face that owns this node
            const dlong face = n/p_Nfp;
            // load surface geofactors for this face
            const dlong sid    = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat nz   = sgeo[sid+p_NZID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];

            // indices of negative and positive traces of face node
            const dlong id  = e*p_Nfp*p_Nfaces + n;
            const dlong idM = vmapM[id];
            const dlong idP = vmapP[id];

            // load negative and positive trace node values of q
            const dfloat  uM = U[idM+0*offset], vM = U[idM+1*offset], wM = U[idM+2*offset];
                  dfloat  uP = U[idP+0*offset], vP = U[idP+1*offset], wP = U[idP+2*offset];
             
            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0) {
              insVelocityDirichletConditions3D(bc,time, x[idM],y[idM],z[idM], nx,ny,nz, uM,vM,wM, &uP,&vP,&wP);
              // // Use strong form of the BCs
              uP = 2.f*uP-uM;        
              vP = 2.f*vP-vM;         
              wP = 2.f*wP-wM;         
            }
      
            const dfloat sc = invJ * sJ ; 
            s_fluxU[es][n] = 0.5f*sc*(nx*(uP-uM) + ny*(vP-vM) + nz*(wP-wM)); 
          }
        }
      }
    }
    
    // wait for all @shared memory writes of the previous inner loop to complete
    @barrier("local");

    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Np){
            const dlong id = e*p_Np + n;
            dfloat divu = divU[id];    
            
            #pragma unroll p_NfacesNfp
            for(int m=0;m<p_Nfaces*p_Nfp;++m){
              const dfloat L = LIFTT[n+m*p_Np];
              divu += L*s_fluxU[es][m];
            }
            // Update
            divU[id] = divu; 
          }
        }
      }
    }
  }
}


