/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

// compute div(NU)  = div(uxu) in collocation way
@kernel void insAdvectionVolumeTet3D(const dlong Nelements,
                                    @restrict const  dfloat *  vgeo,
                                    @restrict const  dfloat *  Dmatrices,
                                    const dlong offset,
                                    @restrict const  dfloat *  U,
                                          @restrict dfloat *  NU){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockV;@outer(0)){  // for all elements
    // Hold Flux Vector on Shared, use symmetry f
    @shared dfloat s_F11[p_NblockV][p_Np];
    @shared dfloat s_F12[p_NblockV][p_Np];
    @shared dfloat s_F13[p_NblockV][p_Np];
    @shared dfloat s_F21[p_NblockV][p_Np];
    @shared dfloat s_F22[p_NblockV][p_Np];
    @shared dfloat s_F23[p_NblockV][p_Np];
    @shared dfloat s_F31[p_NblockV][p_Np];
    @shared dfloat s_F32[p_NblockV][p_Np];
    @shared dfloat s_F33[p_NblockV][p_Np];

    for(int es=0;es<p_NblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_Np;++n;@inner(0)){     // for all nodes in this element
        const dlong e = eo+es; // element in block
        if(e<Nelements){ 
          const dlong id = e*p_Np+n;

          const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
          const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
          const dfloat drdz = vgeo[e*p_Nvgeo + p_RZID];

          const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
          const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
          const dfloat dsdz = vgeo[e*p_Nvgeo + p_SZID];

          const dfloat dtdx = vgeo[e*p_Nvgeo + p_TXID];
          const dfloat dtdy = vgeo[e*p_Nvgeo + p_TYID];
          const dfloat dtdz = vgeo[e*p_Nvgeo + p_TZID];

          const dfloat Un = U[id+0*offset];
          const dfloat Vn = U[id+1*offset];
          const dfloat Wn = U[id+2*offset];

          const dfloat cUn = drdx*Un + drdy*Vn + drdz*Wn;
          const dfloat cVn = dsdx*Un + dsdy*Vn + dsdz*Wn;
          const dfloat cWn = dtdx*Un + dtdy*Vn + dtdz*Wn;     
          
          s_F11[es][n] = cUn*Un;
          s_F12[es][n] = cVn*Un;
          s_F13[es][n] = cWn*Un;

          s_F21[es][n] = cUn*Vn;
          s_F22[es][n] = cVn*Vn;
          s_F23[es][n] = cWn*Vn;

          s_F31[es][n] = cUn*Wn;
          s_F32[es][n] = cVn*Wn;
          s_F33[es][n] = cWn*Wn;
        }
      }
    }

    // Make sure all node data is loaded into @shared
    @barrier("local");

    for(int es=0;es<p_NblockV;++es;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){     
        const dlong e = eo+es; 
        if(e<Nelements){    
          dfloat nu = 0., nv = 0., nw = 0.;
          #pragma unroll p_Np
            for(int i=0;i<p_Np;++i){
              const dfloat Drni  = Dmatrices[n+i*p_Np+0*p_Np*p_Np];
              const dfloat Dsni  = Dmatrices[n+i*p_Np+1*p_Np*p_Np];
              const dfloat Dtni  = Dmatrices[n+i*p_Np+2*p_Np*p_Np];
              
              nu += Drni*s_F11[es][i]
                   +Dsni*s_F12[es][i]
                   +Dtni*s_F13[es][i];
              
              nv += Drni*s_F21[es][i]
                   +Dsni*s_F22[es][i]
                   +Dtni*s_F23[es][i];

              nw += Drni*s_F31[es][i]
                   +Dsni*s_F32[es][i]
                   +Dtni*s_F33[es][i];  
            }

          // Update
          const dlong id = e*p_Np+n;
          NU[id+0*offset] = nu;
          NU[id+1*offset] = nv;     
          NU[id+2*offset] = nw;     
        }
      }
    }
  }
}

@kernel void insAdvectionSurfaceTet3D(const dlong Nelements,
                                     @restrict const  dfloat *  sgeo,
                                     @restrict const  dfloat *  LIFTT,
                                     @restrict const  dlong  *  vmapM,
                                     @restrict const  dlong  *  vmapP,
                                     @restrict const  int    *  EToB,
                                     const dfloat time,
                                     @restrict const  dfloat *  x,
                                     @restrict const  dfloat *  y,
                                     @restrict const  dfloat *  z,
                                     const dlong offset,
                                     @restrict const  dfloat *  U,
                                           @restrict dfloat *  NU){
  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){
    // @shared storage for flux terms
    @shared dfloat s_fluxNx[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxNy[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxNz[p_NblockS][p_Nfp*p_Nfaces];
    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Nfp*p_Nfaces){
            // find face that owns this node
            const int face = n/p_Nfp;
            // load surface geofactors for this face
            const dlong sid    = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat nz   = sgeo[sid+p_NZID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];

            // indices of negative and positive traces of face node
            const dlong id  = e*p_Nfp*p_Nfaces + n;
            const dlong idM = vmapM[id];
            const dlong idP = vmapP[id];

            // load negative and positive trace node values of U, V, Pr
            const dfloat  uM = U[idM+0*offset], vM = U[idM+1*offset], wM = U[idM+2*offset];
                  dfloat  uP = U[idP+0*offset], vP = U[idP+1*offset], wP = U[idP+2*offset];

            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0){
              insVelocityDirichletConditions3D(bc,time, x[idM],y[idM],z[idM], nx,ny,nz, uM,vM,wM, &uP,&vP,&wP);
            }

            // Find max normal velocity on the face
            const dfloat unM   = fabs(nx*uM + ny*vM + nz*wM);
            const dfloat unP   = fabs(nx*uP + ny*vP + nz*wP);    
            const dfloat unMax = (unM > unP) ? unM : unP;
             // evaluate "flux" terms: LLF
            const dfloat sc = invJ * sJ ; 
            s_fluxNx[es][n] = sc*(.5f*( nx*(uP*uP - uM*uM) 
                                      + ny*(uP*vP - uM*vM) 
                                      + nz*(uP*wP - uM*wM)  
                                      + unMax*(uM-uP) ));

            s_fluxNy[es][n] = sc*(.5f*( nx*(vP*uP - vM*uM) 
                                      + ny*(vP*vP - vM*vM)
                                      + nz*(vP*wP - vM*wM)  
                                      + unMax*(vM-vP) ));

            s_fluxNz[es][n] = sc*(.5f*( nx*(wP*uP - wM*uM) 
                                      + ny*(wP*vP - wM*vM)
                                      + nz*(wP*wP - wM*wM)  
                                      + unMax*(wM-wP) ));
          }
        }
      }
    }


    // wait for all flux functions are written to @shared 
    @barrier("local");

    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Np){
            const dlong id = e*p_Np + n;
            dfloat rhsnu = NU[id+0*offset];
            dfloat rhsnv = NU[id+1*offset];
            dfloat rhsnw = NU[id+2*offset];
            // Lift
            #pragma unroll p_NfacesNfp
              for(int m=0;m<p_Nfaces*p_Nfp;++m){
                const dfloat L = LIFTT[n+m*p_Np];
                rhsnu  += L*s_fluxNx[es][m];
                rhsnv  += L*s_fluxNy[es][m];
                rhsnw  += L*s_fluxNz[es][m];
              }

            NU[id+0*offset] = rhsnu;
            NU[id+1*offset] = rhsnv;
            NU[id+2*offset] = rhsnw;
          }
        }
      }
    }
  }
}


// compute div(NU)  = div(uxu) in collocation way
@kernel void insAdvectionCubatureVolumeTet3D(const dlong Nelements,
                                            @restrict const  dfloat *  vgeo,
                                            @restrict const  dfloat *  cubvgeo,
                                            @restrict const  dfloat *  cubDWmatrices,
                                            @restrict const  dfloat *  cubInterpT,
                                            @restrict const  dfloat *  cubProjectT,
                                            const dlong offset,
                                            @restrict const  dfloat *  U,
                                                  @restrict dfloat *  cU,
                                                  @restrict dfloat *  NU){

  for(dlong eo=0;eo<Nelements;eo+=p_cubNblockV;@outer(0)){  // for all elements

    // Hold Flux Vector on Shared, use symmetry f21 = f12
    @shared dfloat s_U[p_cubNblockV][p_Np];
    @shared dfloat s_V[p_cubNblockV][p_Np];
    @shared dfloat s_W[p_cubNblockV][p_Np];

    @shared dfloat s_cF11[p_cubNblockV][p_cubNp];
    @shared dfloat s_cF12[p_cubNblockV][p_cubNp];
    @shared dfloat s_cF13[p_cubNblockV][p_cubNp];
    @shared dfloat s_cF21[p_cubNblockV][p_cubNp];
    @shared dfloat s_cF22[p_cubNblockV][p_cubNp];
    @shared dfloat s_cF23[p_cubNblockV][p_cubNp];
    @shared dfloat s_cF31[p_cubNblockV][p_cubNp];
    @shared dfloat s_cF32[p_cubNblockV][p_cubNp];
    @shared dfloat s_cF33[p_cubNblockV][p_cubNp];

    for(int es=0;es<p_cubNblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_maxNodesVolumeCub;++n;@inner(0)){      // for all nodes in this element
        const dlong e = eo+es; // element in block
        if(e<Nelements){
          if(n<p_Np){ 
            const dlong id = e*p_Np+n;
            s_U[es][n] = U[id+0*offset];
            s_V[es][n] = U[id+1*offset];
            s_W[es][n] = U[id+2*offset];
         }
        } 
      }
    }

    @barrier("local");


    for(int es=0;es<p_cubNblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_maxNodesVolumeCub;++n;@inner(0)){     // for all nodes in this element
        const dlong e = eo+es; // element in block
        if(e<Nelements && n<p_cubNp){
          const dfloat drdx = vgeo[e*p_Nvgeo + p_RXID];
          const dfloat drdy = vgeo[e*p_Nvgeo + p_RYID];
          const dfloat drdz = vgeo[e*p_Nvgeo + p_RZID];

          const dfloat dsdx = vgeo[e*p_Nvgeo + p_SXID];
          const dfloat dsdy = vgeo[e*p_Nvgeo + p_SYID];
          const dfloat dsdz = vgeo[e*p_Nvgeo + p_SZID];

          const dfloat dtdx = vgeo[e*p_Nvgeo + p_TXID];
          const dfloat dtdy = vgeo[e*p_Nvgeo + p_TYID];
          const dfloat dtdz = vgeo[e*p_Nvgeo + p_TZID];

          dfloat cUn = 0, cVn = 0, cWn = 0;
          #pragma unroll p_Np
          for(int i=0;i<p_Np;++i){
            const dfloat cIni = cubInterpT[n+i*p_cubNp];
            cUn += cIni*s_U[es][i];
            cVn += cIni*s_V[es][i];
            cWn += cIni*s_W[es][i];
          }

          const dfloat ccUn = drdx*cUn + drdy*cVn + drdz*cWn;
          const dfloat ccVn = dsdx*cUn + dsdy*cVn + dsdz*cWn;
          const dfloat ccWn = dtdx*cUn + dtdy*cVn + dtdz*cWn;

          s_cF11[es][n] = ccUn*cUn;
          s_cF12[es][n] = ccVn*cUn;
          s_cF13[es][n] = ccWn*cUn;

          s_cF21[es][n] = ccUn*cVn;
          s_cF22[es][n] = ccVn*cVn;
          s_cF23[es][n] = ccWn*cVn;

          s_cF31[es][n] = ccUn*cWn;
          s_cF32[es][n] = ccVn*cWn;
          s_cF33[es][n] = ccWn*cWn;
        }
      }
    }

    // Make sure all node data is loaded into @shared
    @barrier("local");

    for(int es=0;es<p_cubNblockV;++es;@inner(1)){// for all elements in block
      for(int n=0;n<p_maxNodesVolumeCub;++n;@inner(0)){     // for all nodes in this element
        const dlong e = eo+es; 
        if(e<Nelements && n<p_Np){    
          dfloat nu = 0., nv = 0., nw = 0.;
          #pragma unroll p_cubNp
            for(int i=0;i<p_cubNp;++i){
              const dfloat cDrWni = cubDWmatrices[n+i*p_Np+0*p_cubNp*p_Np];
              const dfloat cDsWni = cubDWmatrices[n+i*p_Np+1*p_cubNp*p_Np];
              const dfloat cDtWni = cubDWmatrices[n+i*p_Np+2*p_cubNp*p_Np];

              nu += cDrWni*s_cF11[es][i]
                   +cDsWni*s_cF12[es][i]
                   +cDtWni*s_cF13[es][i];
              
              nv += cDrWni*s_cF21[es][i]
                   +cDsWni*s_cF22[es][i]
                   +cDtWni*s_cF23[es][i];

              nw += cDrWni*s_cF31[es][i]
                   +cDsWni*s_cF32[es][i]
                   +cDtWni*s_cF33[es][i];  
            }

          // Update
          const dlong id = e*p_Np+n;
          NU[id+0*offset] = -nu; // note  change in sign
          NU[id+1*offset] = -nv;     
          NU[id+2*offset] = -nw;     
        }
      }
    }
  }
}


@kernel void insAdvectionCubatureSurfaceTet3D(const dlong Nelements,
                                             @restrict const  dfloat *  vgeo,
                                             @restrict const  dfloat *  sgeo,
                                             @restrict const  dfloat *  cubsgeo,
                                             @restrict const  dfloat *  intInterpT, // interpolate to integration nodes
                                             @restrict const  dfloat *  intLIFTT, // lift from integration to interpolation nodes
                                             @restrict const  dfloat *  cubInterpT,
                                             @restrict const  dfloat *  cubProjectT,
                                             @restrict const  dlong  *  vmapM,
                                             @restrict const  dlong  *  vmapP,
                                             @restrict const  int    *  EToB,
                                             const dfloat time,
                                             @restrict const  dfloat *  intx, // integration nodes
                                             @restrict const  dfloat *  inty,
                                             @restrict const  dfloat *  intz,
                                             const dlong offset,
                                             @restrict const  dfloat *  U,
                                                   @restrict dfloat *  NU){

  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_cubNblockS;@outer(0)){

    // @shared storage for flux terms
    @shared dfloat s_UM[p_cubNblockS][p_NfacesNfp];
    @shared dfloat s_VM[p_cubNblockS][p_NfacesNfp];
    @shared dfloat s_WM[p_cubNblockS][p_NfacesNfp];

    @shared dfloat s_UP[p_cubNblockS][p_NfacesNfp];
    @shared dfloat s_VP[p_cubNblockS][p_NfacesNfp];
    @shared dfloat s_WP[p_cubNblockS][p_NfacesNfp];

    @shared dfloat s_iFluxNx[p_cubNblockS][p_intNfpNfaces];
    @shared dfloat s_iFluxNy[p_cubNblockS][p_intNfpNfaces];
    @shared dfloat s_iFluxNz[p_cubNblockS][p_intNfpNfaces];

    // for all face nodes of all elements
    for(int es=0;es<p_cubNblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodesSurfaceCub;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements && n<(p_Nfaces*p_Nfp)){
          const dlong id  = e*p_Nfp*p_Nfaces + n;
          const dlong idM = vmapM[id];
          const dlong idP = vmapP[id];

          // load negative and positive trace node values of velocity
          s_UM[es][n] = U[idM+0*offset];
          s_VM[es][n] = U[idM+1*offset];
          s_WM[es][n] = U[idM+2*offset];

          s_UP[es][n] = U[idP+0*offset];
          s_VP[es][n] = U[idP+1*offset];
          s_WP[es][n] = U[idP+2*offset];
        }
      }
    }

    @barrier("local");

    // interpolate to surface integration nodes
    for(int es=0;es<p_cubNblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodesSurfaceCub;++n;@inner(0)){ 
        const dlong e = eo + es;
        if(e<Nelements && n<(p_Nfaces*p_intNfp)){
           // find face that owns this integration node
            const int face = n/p_intNfp; 
            // load surface geofactors for this face
            const dlong sid = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat nz   = sgeo[sid+p_NZID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];

            dfloat iUM = 0.f, iVM = 0.f, iWM = 0.f;
            dfloat iUP = 0.f, iVP = 0.f, iWP = 0.f;

            // local block interpolation (face nodes to integration nodes)
            #pragma unroll p_Nfp
            for(int m=0;m<p_Nfp;++m){
              const dfloat iInm = intInterpT[n+m*p_Nfaces*p_intNfp];
              const int fm = face*p_Nfp+m;
              iUM += iInm*s_UM[es][fm];
              iVM += iInm*s_VM[es][fm];
              iWM += iInm*s_WM[es][fm];

              iUP += iInm*s_UP[es][fm];
              iVP += iInm*s_VP[es][fm];
              iWP += iInm*s_WP[es][fm];
            }

            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0){
              insVelocityDirichletConditions3D(bc,time, intx[n+e*p_Nfaces*p_intNfp],inty[n+e*p_Nfaces*p_intNfp],intz[n+e*p_Nfaces*p_intNfp], nx, ny,nz, iUM,iVM,iWM, &iUP, &iVP,&iWP);
            }

            // Find max normal velocity on the face // LLF
            const dfloat unM   = fabs(nx*iUM + ny*iVM + nz*iWM);
            const dfloat unP   = fabs(nx*iUP + ny*iVP + nz*iWP);    
            const dfloat unMax = (unM > unP) ? unM : unP;

            // evaluate "flux" terms: LLF
            const dfloat sc = invJ * sJ ;  // notice sign changes in flux because of weak form
            s_iFluxNx[es][n] = sc*(.5f*(   nx*(iUP*iUP + iUM*iUM) 
                                         + ny*(iUP*iVP + iUM*iVM) 
                                         + nz*(iUP*iWP + iUM*iWM) 
                                         + unMax*(iUM-iUP) ));

            s_iFluxNy[es][n] = sc*(.5f*(   nx*(iVP*iUP + iVM*iUM) 
                                         + ny*(iVP*iVP + iVM*iVM) 
                                         + nz*(iVP*iWP + iVM*iWM) 
                                         + unMax*(iVM-iVP) ));

            s_iFluxNz[es][n] = sc*(.5f*(   nx*(iWP*iUP + iWM*iUM) 
                                         + ny*(iWP*iVP + iWM*iVM) 
                                         + nz*(iWP*iWP + iWM*iWM) 
                                         + unMax*(iWM-iWP) ));
          }
      }
    }

    // wait for all @shared memory writes of the previous inner loop to complete
    @barrier("local");

    // lift from surface integration to volume nodes
    for(int es=0;es<p_cubNblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodesSurfaceCub;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements && n<p_Np ){
          const dlong id = n + p_Np*e;
          // prefetch volume rhs
          dfloat NUn = NU[id+0*offset];
          dfloat NVn = NU[id+1*offset];
          dfloat NWn = NU[id+2*offset];

          #pragma unroll p_intNfp*p_Nfaces
          for(int m=0;m<(p_intNfp*p_Nfaces);++m){
            // RefMassMatrix^{-1}*cInterp^t*cWeight
            const dfloat L = intLIFTT[n+m*p_Np];
            NUn += L*s_iFluxNx[es][m];
            NVn += L*s_iFluxNy[es][m];
            NWn += L*s_iFluxNz[es][m];
          }

          NU[id+0*offset] = NUn;
          NU[id+1*offset] = NVn;
          NU[id+2*offset] = NWn;
        }
      }
    }
  }
}







// dfloat myMax(dfloat a, dfloat  b){
//   dfloat max = 0.f;
//   if(a>=b) 
//     max = a;
//   else 
//     max = b;

// return max;
// }

// @kernel void insAdvectionCubatureSurface3D_0(const int Nelements,
//             @restrict const  dfloat *  sgeo,
//             @restrict const  dfloat *  intInterpT, // interpolate to integration nodes
//             @restrict const  dfloat *  intLIFTT, // lift from integration to interpolation nodes
//             @restrict const  int   *  vmapM,
//             @restrict const  int   *  vmapP,
//             @restrict const  int   *  EToB,
//             const dfloat time,
//             @restrict const  dfloat *  x, // integration nodes
//             @restrict const  dfloat *  y,
//             @restrict const  dfloat *  z,
//             @restrict const  dfloat *  intx, // integration nodes
//             @restrict const  dfloat *  inty,
//             @restrict const  dfloat *  intz,
//             const int offset,
//             @restrict const  dfloat *  U,
//             @restrict const  dfloat *  V,
//             @restrict const  dfloat *  W,
//             @restrict dfloat *  NU,
//             @restrict dfloat *  NV,
//             @restrict dfloat *  NW){
 
//  // Just for testing modify later

//   // for all elements
//   for(int eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){

//     @shared volatile  dfloat s_a[p_NblockS][p_Nfaces][p_Nfp];
//     // @shared storage for flux terms
//     @shared dfloat s_UM[p_NblockS][p_NfacesNfp];
//     @shared dfloat s_VM[p_NblockS][p_NfacesNfp];
//     @shared dfloat s_WM[p_NblockS][p_NfacesNfp];

//     @shared dfloat s_UP[p_NblockS][p_NfacesNfp];
//     @shared dfloat s_VP[p_NblockS][p_NfacesNfp];
//     @shared dfloat s_WP[p_NblockS][p_NfacesNfp];

//     @shared dfloat s_iFluxNx[p_NblockS][p_intNfpNfaces];
//     @shared dfloat s_iFluxNy[p_NblockS][p_intNfpNfaces];
//     @shared dfloat s_iFluxNz[p_NblockS][p_intNfpNfaces];

//     @exclusive int f, m;

//     // for all face nodes of all elements
//     for(int es=0;es<p_NblockS;++es;@inner(1)){
//       for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){
//         int e = eo + es;
//         if(e<Nelements && n<(p_Nfaces*p_Nfp)){
//           const int id  = e*p_Nfp*p_Nfaces + n;
//           int idM = vmapM[id]+offset*p_Np;
//           int idP = vmapP[id]+offset*p_Np;

//           if(idP<0) idP = idM;
//           // load negative and positive trace node values of velocity
//           dfloat uM = U[idM], vM = V[idM], wM = W[idM];
//           dfloat uP = U[idP], vP = V[idP], wP = W[idP];
          
//           // load negative and positive trace node values of velocity
//           s_UM[es][n] = uM; s_VM[es][n] = vM; s_WM[es][n] = wM;
//           s_UP[es][n] = uP; s_VP[es][n] = vP; s_WP[es][n] = wP;
//           //
//           int face = n/p_Nfp;
//           int sid    = p_Nsgeo*(e*p_Nfaces+face);
//           dfloat nx   = sgeo[sid+p_NXID];
//           dfloat ny   = sgeo[sid+p_NYID];
//           dfloat nz   = sgeo[sid+p_NZID];

//           // apply boundary condition
//           int bc = EToB[face+p_Nfaces*e];
//           if(bc>0){
//             idM = vmapM[id];
//             insAdvectionBoundaryConditions3D(bc,time, x[idM],y[idM],z[idM], nx,ny,nz, uM,vM,wM, &uP,&vP,&wP);
//           }
//           // Find max normal velocity on the face
//           dfloat unM   = fabs(nx*uM + ny*vM + nz*wM);
//           dfloat unP   = fabs(nx*uP + ny*vP + nz*wP);    
          
//           dfloat unMax = (unM > unP) ? unM : unP;

//           #if 1
//             f = n/p_Nfp;
//             m = n%p_Nfp;
//             s_a[es][f][m] = unMax;
//           #endif        
//         }    
//      }
//     }

//     @barrier("local");

//     for(int es=0;es<p_NblockS;++es;@inner(1)){
//       for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){ 
//         if(n<(p_Nfaces*p_Nfp)){ 
//         if(m<128 && m+128 < p_Nfp)
//           s_a[es][f][m]  = myMax(s_a[es][f][m],s_a[es][f][m+128]);
//        }
//       }
//     }

//     @barrier("local");

//     for(int es=0;es<p_NblockS;++es;@inner(1)){
//       for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){ 
//         if(n<(p_Nfaces*p_Nfp)){ 
//         if(m<64 && m+64<p_Nfp)
//           s_a[es][f][m]  = myMax(s_a[es][f][m],s_a[es][f][m+64]);
//       }
//       }
//     }


//     @barrier("local");

//     for(int es=0;es<p_NblockS;++es;@inner(1)){
//       for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){ 
//         if(n<(p_Nfaces*p_Nfp)){ 
//         if(m<32 && m+32<p_Nfp)
//           s_a[es][f][m]  = myMax(s_a[es][f][m],s_a[es][f][m+32]);
//       }
//       }
//     }

//   @barrier("local"); // OCCA COMPLAINS!!!!

//     for(int es=0;es<p_NblockS;++es;@inner(1)){
//       for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){ 
//         if(n<(p_Nfaces*p_Nfp)){ 
//         if(m<16 && m+16<p_Nfp)
//           s_a[es][f][m]  = myMax(s_a[es][f][m],s_a[es][f][m+16]);
//       }
//       }
//     }

//     @barrier("local");

//       for(int es=0;es<p_NblockS;++es;@inner(1)){
//         for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){ 
//           if(n<(p_Nfaces*p_Nfp)){ 
//           if(m<8 && m+8<p_Nfp)
//             s_a[es][f][m]  = myMax(s_a[es][f][m],s_a[es][f][m+8]);
//         }
//         }
//       }

//   @barrier("local");

//     for(int es=0;es<p_NblockS;++es;@inner(1)){
//       for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){ 
//         if(n<(p_Nfaces*p_Nfp)){ 
//         if(m<4 && m+4<p_Nfp)
//           s_a[es][f][m]  = myMax(s_a[es][f][m],s_a[es][f][m+4]);
//       }
//       }
//     }

//     @barrier("local");

//     for(int es=0;es<p_NblockS;++es;@inner(1)){
//       for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){
//        if(n<(p_Nfaces*p_Nfp)){ 
//          if(m<2 && m+2<p_Nfp)
//           s_a[es][f][m]  = myMax(s_a[es][f][m],s_a[es][f][m+2]);
//       }
//       }
//     }
//   @barrier("local");

//   for(int es=0;es<p_NblockS;++es;@inner(1)){
//         for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){ 
//           if(n<(p_Nfaces*p_Nfp)){ 
//           if(m<1 && (m+1)<p_Nfp)
//             s_a[es][f][m]  = myMax(s_a[es][f][m],s_a[es][f][m+1]);
//         }
//         }
//       }

//   @barrier("local");

//     // interpolate to surface integration nodes
//     for(int es=0;es<p_NblockS;++es;@inner(1)){
//       for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){ 
//         int e = eo + es;
//         if(e<Nelements && n<(p_Nfaces*p_intNfp)){
//            // find face that owns this integration node
//             const int face = n/p_intNfp; 
//             // load surface geofactors for this face
//             const int sid = p_Nsgeo*(e*p_Nfaces+face);
//             const dfloat nx   = sgeo[sid+p_NXID];
//             const dfloat ny   = sgeo[sid+p_NYID];
//             const dfloat nz   = sgeo[sid+p_NZID];
//             const dfloat sJ   = sgeo[sid+p_SJID];
//             const dfloat invJ = sgeo[sid+p_IJID];

//             dfloat iUM = 0.f, iVM = 0.f, iWM = 0.f;
//             dfloat iUP = 0.f, iVP = 0.f, iWP = 0.f;

//             // local block interpolation (face nodes to integration nodes)
//             #pragma unroll p_Nfp
//             for(int m=0;m<p_Nfp;++m){
//               const dfloat iInm = intInterpT[n+m*p_Nfaces*p_intNfp];
//               const int fm = face*p_Nfp+m;
//               iUM += iInm*s_UM[es][fm];
//               iVM += iInm*s_VM[es][fm];
//               iWM += iInm*s_WM[es][fm];

//               iUP += iInm*s_UP[es][fm];
//               iVP += iInm*s_VP[es][fm];
//               iWP += iInm*s_WP[es][fm];
//             }

//             // apply boundary condition
//             int bc = EToB[face+p_Nfaces*e];
//             if(bc>0){
//               insAdvectionBoundaryConditions3D(bc,time, intx[n+e*p_Nfaces*p_intNfp],inty[n+e*p_Nfaces*p_intNfp],intz[n+e*p_Nfaces*p_intNfp], nx, ny,nz, iUM,iVM,iWM, &iUP, &iVP,&iWP);
//             }

           
//               dfloat lambda = s_a[es][face][0];
              
//             // evaluate "flux" terms: LLF
//             const dfloat sc = invJ * sJ ;  // notice sign changes in flux because of weak form
//             s_iFluxNx[es][n] = sc*(.5f*(   nx*(iUP*iUP + iUM*iUM) 
//                                          + ny*(iUP*iVP + iUM*iVM) 
//                                          + nz*(iUP*iWP + iUM*iWM) 
//                                          + lambda*(iUM-iUP) ));

//             s_iFluxNy[es][n] = sc*(.5f*(   nx*(iVP*iUP + iVM*iUM) 
//                                          + ny*(iVP*iVP + iVM*iVM) 
//                                          + nz*(iVP*iWP + iVM*iWM) 
//                                          + lambda*(iVM-iVP) ));

//             s_iFluxNz[es][n] = sc*(.5f*(   nx*(iWP*iUP + iWM*iUM) 
//                                          + ny*(iWP*iVP + iWM*iVM) 
//                                          + nz*(iWP*iWP + iWM*iWM) 
//                                          + lambda*(iWM-iWP) ));
//           }
//       }
//     }


//     @barrier("local");

//     // lift from surface integration to volume nodes
//     for(int es=0;es<p_NblockS;++es;@inner(1)){
//       for(int n=0;n<p_maxSurfaceNodes;++n;@inner(0)){
//         const int e = eo + es;
//         if(e<Nelements && n<p_Np ){
//             int id = n + p_Np*(e+offset);
//             // prefetch volume rhs
//             dfloat NUn = NU[id];
//             dfloat NVn = NV[id];
//             dfloat NWn = NW[id];

//             #pragma unroll p_intNfp*p_Nfaces
//             for(int m=0;m<(p_intNfp*p_Nfaces);++m){
//               // RefMassMatrix^{-1}*cInterp^t*cWeight
//               const dfloat L = intLIFTT[n+m*p_Np];
//               NUn += L*s_iFluxNx[es][m];
//               NVn += L*s_iFluxNy[es][m];
//               NWn += L*s_iFluxNz[es][m];
//             }

//             NU[id] = NUn;
//             NV[id] = NVn;
//             NW[id] = NWn;
//           }
//       }
//     }
//   }
// }
